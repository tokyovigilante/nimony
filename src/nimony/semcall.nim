# included in sem.nim

proc fetchCallableType(c: var SemContext; dest: var TokenBuf; n: Cursor; s: Sym): TypeCursor =
  if s.kind == NoSym:
    let s = getIdent(n)
    if s != StrId(0):
      c.buildErr dest, n.info, "undeclared identifier: " & pool.strings[s]
    else:
      c.buildErr dest, n.info, "undeclared identifier"
    result = c.types.autoType
  else:
    let res = declToCursor(c, dest, s)
    if res.status == LacksNothing:
      var d = res.decl
      if s.kind.isLocal:
        skipToLocalType d
      result = d
    else:
      c.buildErr dest, n.info, "could not load symbol: " & pool.syms[s.name] & "; errorCode: " & $res.status
      result = c.types.autoType

proc pickBestMatch(c: var SemContext; m: openArray[Match]; flags: set[SemFlag] = {}): int =
  result = -1
  var other = -1
  for i in 0..<m.len:
    if not m[i].err:
      if result < 0:
        result = i
      else:
        case cmpMatches(m[result], m[i], preferIterators = PreferIterators in flags)
        of NobodyWins:
          other = i
          #echo "ambiguous ", pool.syms[m[result].fn.sym], " vs ", pool.syms[m[i].fn.sym]
        of FirstWins:
          discard "result remains the same"
        of SecondWins:
          result = i
          other = -1
  if other >= 0: result = -2 # ambiguous

type MagicCallKind = enum
  NonMagicCall, MagicCall, MagicCallNeedsSemcheck

proc addFn(c: var SemContext; dest: var TokenBuf; fn: FnCandidate; fnOrig: Cursor; m: var Match): MagicCallKind =
  result = NonMagicCall
  if fn.fromConcept and fn.sym != SymId(0):
    # The matched symbol comes from a concept body; it is a phantom
    # declaration with no implementation, so the call has to be re-resolved
    # at instantiation time. Preserve the original symbol-choice (containing
    # every overload visible at the *definition* site) so that re-sem at the
    # call site sees both the def-site overloads and any extra overloads
    # introduced at the call site. Without this, identifiers like `hash`
    # inside a generic body would be re-looked-up against the call site's
    # imports only, which can pick up an unrelated `hash` overload from a
    # third-party module that happens to be in scope.
    if fnOrig.isTagLit and fnOrig.exprKind in {OchoiceX, CchoiceX}:
      dest.addSubtree fnOrig
    else:
      dest.addIdent(symToIdent(fn.sym), fnOrig.info)
  elif fn.kind in RoutineKinds:
    assert fn.sym != SymId(0)
    let res = tryLoadSym(fn.sym)
    if res.status == LacksNothing:
      var n = res.decl
      inc n # skip the symbol kind
      if n.isSymbolDef:
        inc n # skip the SymbolDef
        if n.isTagLit:
          if n.exprKind in {DefinedX, DeclaredX, AstToStrX, CompilesX, TypeofX,
              LowX, HighX, AddrX, EnumtostrX, DefaultobjX, DefaulttupX, DefaultdistinctX,
              ArratX, DerefX, TupatX, SizeofX, InternalTypeNameX, IsX, ProccallX, DelayX,
              BindSymX, BindSymNameX}:
            # magic needs semchecking after overloading
            result = MagicCallNeedsSemcheck
          else:
            result = MagicCall
          # ^ export marker position has a `(`? If so, it is a magic!
          let callHeadAt = lastValueStart(dest)
          let info = readonlyCursorAt(dest, callHeadAt).info
          dest[callHeadAt] = n.load # overwrite the `(call` node with the magic itself
          n = sub(n) # bound the magic-body walk
          if n.isIntLit:
            if n.intVal == TypedMagic:
              # use type of first param
              var paramType = fn.typ
              assert paramType.typeKind in RoutineTypes
              skipToParams paramType
              assert paramType.substructureKind == ParamsU
              inc paramType
              assert paramType.symKind == ParamY
              paramType = asLocal(paramType).typ
              if m.inferred.len != 0:
                paramType = instantiateType(c, paramType, m.inferred)
              removeModifier(paramType)
              dest.addSubtree paramType
            else:
              dest.addSubtree n
            inc n
          if n.hasMore:
            bug "broken `magic`: expected ')', but got: ", n
    if result == NonMagicCall:
      dest.addSymUse(fn.sym, fnOrig.info)
  else:
    dest.addSubtree fnOrig

proc typeofCallIs(c: var SemContext; dest: var TokenBuf; it: var Item; beforeCall: int; returnType: TypeCursor) {.inline.} =
  let expected = it.typ
  it.typ = returnType
  commonType c, dest, it, beforeCall, expected

proc deferPluginCall(c: var SemContext; dest: var TokenBuf; it: var Item;
                     beforeCall: int; info: NifLineInfo) =
  ## The plugin answered `(deferexpansion)`. Rewrite the call into
  ## `(at <template> <args>…)` and type it as `typedesc[…]`, so `exprToType`
  ## installs that node as the type. `(at …)` is *already* nimony's unresolved
  ## type application — the shape `Foo[T]` takes inside a generic body — so it
  ## passes the post-sem validator in a type slot, compares with `sameTrees`,
  ## and gets substituted by `subsGenericProc` for free. The instantiation's
  ## re-sem then routes it through `semInvoke`, which recognizes the plugin head
  ## and asks the plugin again, now with concrete arguments.
  ##
  ## Deferring is only well-founded when some argument can still change: with
  ## nothing left to substitute, the retry would ask the same question forever.
  var isGeneric = false
  block:
    var head = readonlyCursorAt(dest, beforeCall)
    var a = childCursor(head)
    skip a # the callee
    while a.hasMore:
      if containsGenericParams(a):
        isGeneric = true
        break
      skip a
    endRead head
  if not isGeneric:
    expectUnique dest
    shrink dest, beforeCall
    c.buildErr dest, info,
      "plugin deferred the expansion, but no argument contains a type variable; " &
      "it would never be asked again"
    it.typ = c.types.autoType
    return
  var atBuf = createTokenBuf(16)
  block:
    var head = readonlyCursorAt(dest, beforeCall)
    atBuf.addParLe(AtT, info)
    var ch = childCursor(head)
    while ch.hasMore:
      atBuf.addSubtree ch
      skip ch
    atBuf.addParRi()
    endRead head
  let typeStart = dest.len
  dest.addParLe(TypedescT, info)
  block:
    var ab = beginRead(atBuf)
    dest.addSubtree ab
    endRead ab
  dest.addParRi()
  it.typ = typeToCursor(c, dest, typeStart)
  expectUnique dest
  shrink dest, typeStart

proc semTemplateCall(c: var SemContext; dest: var TokenBuf; it: var Item; fnId: SymId; beforeCall: int;
                     m: Match; flags: set[SemFlag]) =
  var expandedInto = createTokenBuf(30)

  # If we are about to expand a template whose published body is still
  # in unresolved Ident form (phase 2's `takeTree` published it with
  # `phase = SemcheckSignatures`), promote it on demand so
  # `expandTemplateImpl`'s SymId-keyed substitution can find the params.
  # See `tryPromoteTemplateBody` for why this must be lazy rather than
  # eager. Must run before `declToCursor` reads the decl into the local
  # `res` since promotion swaps the registry buffer out. Routed through the
  # unified on-demand driver `loadSymWithPhase` (the promotion is its only
  # registered driver today).
  discard loadSymWithPhase(c, fnId, SemcheckBodies)

  let s = fetchSym(c, fnId)
  let res = declToCursor(c, dest, s)
  if res.status == LacksNothing:
    # `cursorTailAt`: for a zero-arg call (or a vararg position past the
    # last arg) these positions sit right after the sealed call's last
    # token — its physical close is elided under `-d:virtualParRi`.
    var callHead = readonlyCursorAt(dest, beforeCall)
    let callInfo = callHead.info
    var pastFn = childCursor(callHead)
    skip pastFn # past the callee; the rebuilt args (if any) follow
    let argsPos = beforeCall + cursorToPosition(callHead, pastFn)
    var args = cursorTailAt(dest, argsPos)
    var firstVarargMatch = cursorTailAt(dest, argsPos + m.firstVarargPosition)
    var pluginErr = ""
    let outcome = expandTemplate(c, expandedInto, res.decl, args, firstVarargMatch,
                                 addr m.inferred, callInfo, pluginErr)
    # Release the rc refs the cursor constructors bumped on `dest`, so the
    # subsequent `shrink dest` + body re-sem can mutate `dest` without
    # forcing the COW slow path (a full copy of the entire module buffer).
    # The earlier `endRead(dest)` calls were buffer-side no-ops; the cursor
    # overload is what actually dec-refs. `callHead` counts too: both it and
    # the throwaway `readonlyCursorAt` that used to supply `.info` bumped the
    # rc and were never released, so `shrink` always took the copying path.
    # `pastFn` is a plain `childCursor` copy sharing `callHead`'s ref, so it
    # must not be released separately.
    endRead args
    endRead firstVarargMatch
    endRead callHead
    if outcome == PluginDeferred:
      deferPluginCall c, dest, it, beforeCall, callInfo
      return
    if outcome == PluginFailed:
      expectUnique dest
      shrink dest, beforeCall
      c.buildErr dest, callInfo, pluginErr
      it.typ = c.types.autoType
      return
    expectUnique dest
    shrink dest, beforeCall
    expandedInto.addDotToken() # sentinel so the final `inc` stays in bounds
    var a = Item(n: cursorAt(expandedInto, 0), typ: c.types.autoType)
    let aInfo = a.n.info
    # make sure template body expression matches return type, mirrored with `semProcBody`:
    # Hoisted above `semExpr` so the void case can be known before emitting:
    # both `m.returnType` and `m.inferred` are fixed by `sigmatch` before we run.
    let returnType =
      if m.inferred.len == 0 or m.returnType.isDotToken:
        m.returnType
      else:
        instantiateType(c, m.returnType, m.inferred)
    inc c.routine.inInst
    # An `untyped` template's body is published unresolved, so its field
    # accesses resolve HERE for the first time and must be judged against the
    # template's own module. (A typed template's body carries the `"x"` access
    # token instead and takes the `bypassVis` path.) Tokens in the expanded
    # tree that come from another file are call-site arguments substituted in,
    # and stay judged against the caller's module — see `visibilityModule`.
    c.visOwner.add VisOwner(module: extractModule(pool.syms[fnId]),
                            file: res.decl.info.file.uint32)
    semExpr c, dest, a, flags
    case returnType.typeKind
    of UntypedT:
      # untyped return type ignored, maybe could be handled in commonType
      discard
    of VoidT:
      typecheck(c, dest, aInfo, a.typ, returnType)
    else:
      commonType c, dest, a, beforeCall, returnType
    discard c.visOwner.pop()
    dec c.routine.inInst
    # Record where this expansion came from (#1987). The provenance rides in the
    # line-info filename of the emitted heads - `__crucial\0<fn>\0<realfile>` -
    # so the debug backend can emit them as a DWARF inlined frame while every
    # other consumer just sees `realFile()`.
    #
    # After `semExpr`, not before: a template called *inside* this body has
    # already expanded and marked its own heads by now, so a body that is
    # nothing but another template call prepends onto the existing chain and
    # the order comes out outermost first, which is what nesting `inlinedAt`
    # needs.
    #
    # Tokens substituted in from the call site keep their own file: they belong
    # to the caller's frame, and are recognised by already carrying `callInfo`'s.
    forgeExpansionInfo(c, dest, beforeCall, fnId, res.decl.info, callInfo)
    # now match to expected type:
    it.kind = a.kind
    typeofCallIs c, dest, it, beforeCall, a.typ
  else:
    c.buildErr dest, it.n.info, "could not load symbol: " & pool.syms[fnId] & "; errorCode: " & $res.status

type
  FnCandidates = object
    a: seq[FnCandidate]
    marker: HashSet[SymId]

proc addUnique(c: var FnCandidates; x: FnCandidate) =
  if not containsOrIncl(c.marker, x.sym):
    c.a.add x

proc conceptMethodAlreadyListed(cands: FnCandidates; routine: Cursor): bool =
  for existing in cands.a:
    if existing.fromConcept and sameConceptRoutineTrees(routine, existing.typ, equivKinds = true):
      return true
  false

proc sameConceptMethod(a, b: FnCandidate): bool {.inline.} =
  sameConceptRoutineTrees(a.typ, b.typ, equivKinds = true)

proc collectConceptMethodsFor(fn: StrId; concpt: Cursor): seq[FnCandidate] =
  ## All routines named `fn` required by `concpt` (including its parents),
  ## deduplicated by signature shape.
  result = @[]
  for _, routine in conceptHierarchyRoutines(concpt):
    var prc = routine
    inc prc
    if prc.isSymbolDef and sameIdent(prc.symId, fn):
      var dup = false
      for ex in result:
        if sameConceptRoutineTrees(routine, ex.typ, equivKinds = true):
          dup = true
          break
      if not dup:
        result.add FnCandidate(kind: routine.symKind, sym: prc.symId,
                               typ: routine, fromConcept: true)

proc conceptMethodsForConstraint(fn: StrId; typ: Cursor): seq[FnCandidate] =
  ## Candidate routines named `fn` that are *guaranteed* to be available on a
  ## type variable constrained by `typ`. `and` exposes the union of its
  ## operands' operations; `or` exposes only their intersection: an operation is
  ## available solely when every alternative provides a matching one. Treating
  ## `or` as a union (as a naive fix does) is unsound, since the type may satisfy
  ## just one alternative. See nim-lang/nimony#2029.
  result = @[]
  if typ.isSymbol:
    let section = getTypeSection typ.symId
    if section.body.typeKind == ConceptT:
      result = collectConceptMethodsFor(fn, section.body)
  elif typ.typeKind == AndT:
    var t = typ
    t.into:
      while t.hasMore:
        for cand in conceptMethodsForConstraint(fn, t):
          var dup = false
          for ex in result:
            if sameConceptMethod(cand, ex):
              dup = true
              break
          if not dup:
            result.add cand
        skip t
  elif typ.typeKind == OrT:
    var t = typ
    var first = true
    t.into:
      while t.hasMore:
        let branch = conceptMethodsForConstraint(fn, t)
        if first:
          result = branch
          first = false
        else:
          var keep: seq[FnCandidate] = @[]
          for cand in result:
            for other in branch:
              if sameConceptMethod(cand, other):
                keep.add cand
                break
          result = keep
        skip t
    if first: result = @[]

proc maybeAddConceptMethods(c: var SemContext; fn: StrId; typevar: SymId; cands: var FnCandidates) =
  let res = tryLoadSym(typevar)
  assert res.status == LacksNothing
  let local = asLocal(res.decl)
  if local.kind == TypevarY and not local.typ.isDotToken:
    for cand in conceptMethodsForConstraint(fn, local.typ):
      if not conceptMethodAlreadyListed(cands, cand.typ):
        cands.addUnique cand

proc hasAttachedParam(params: Cursor; typ: SymId): bool =
  result = false
  var params = params
  params.into ParamsU:
    while params.hasMore:
      let param = takeLocal(params, SkipFinalParRi)
      let root = nominalRoot(param.typ)
      if root != SymId(0) and root == typ:
        while params.hasMore: skip params  # mop-up before early-exit
        return true

proc addTypeboundOps(c: var SemContext; fn: StrId; s: SymId; cands: var FnCandidates) =
  let res = tryLoadSym(s)
  assert res.status == LacksNothing
  let decl = asTypeDecl(res.decl)
  if decl.kind == TypeY:
    let moduleSuffix = extractModule(pool.syms[s])
    if moduleSuffix == "" or
        # with --noSystem, magic types can have the system module suffix
        # without the system module being loaded
        # just ignore symbols from the system module,
        # their bound ops should be in scope anyway
        moduleSuffix == SystemModuleSuffix:
      discard
    elif moduleSuffix == c.thisModuleSuffix:
      # XXX probably redundant over normal lookup but `OchoiceX` does not work yet
      # do not use cache, check symbols from toplevel scope:
      for topLevelSym in topLevelSyms(c, fn):
        let res = tryLoadSym(topLevelSym)
        assert res.status == LacksNothing
        let routine = asRoutine(res.decl)
        if routine.kind in RoutineKinds and hasAttachedParam(routine.params, s):
          cands.addUnique FnCandidate(kind: routine.kind, sym: topLevelSym, typ: routine.params)
    else:
      if c.cachedTypeboundOps.hasKey((s, fn)):
        for fnSym in c.cachedTypeboundOps.getOrQuit((s, fn)):
          let res = tryLoadSym(fnSym)
          assert res.status == LacksNothing
          let routine = asRoutine(res.decl)
          cands.addUnique FnCandidate(kind: routine.kind, sym: fnSym, typ: routine.params)
      else:
        var ops: seq[SymId] = @[]
        for topLevelSym in loadSyms(moduleSuffix, fn):
          let res = tryLoadSym(topLevelSym)
          assert res.status == LacksNothing
          let routine = asRoutine(res.decl)
          if routine.kind in RoutineKinds and hasAttachedParam(routine.params, s):
            ops.add topLevelSym
            cands.addUnique FnCandidate(kind: routine.kind, sym: topLevelSym, typ: routine.params)
        c.cachedTypeboundOps[(s, fn)] = ops
  elif decl.kind == TypevarY:
    maybeAddConceptMethods c, fn, s, cands

type
  CallState = object
    beforeCall: int
    fn: Item
    fnKind: SymKind
    fnName: StrId
    callNode: NifToken
    callNodeInfo: NifLineInfo
    scope: Cursor
      ## the call node's head, captured by `semCall`; every exit path
      ## leaves it via `leaveCall`/`it.n = cs.scope; skip it.n` instead of
      ## consuming a physical ParRi
    dest, genericDest: TokenBuf
    args: seq[CallArg]
    hasGenericArgs, hasNamedArgs: bool
    flags: set[SemFlag]
    source: TransformedCallSource
      ## type of expression the call was transformed from
    argsScopeClosed: bool

proc leaveCall(dest: var TokenBuf; it: var Item; cs: CallState) =
  ## Closes the call tree in `dest` and advances `it.n` past the call
  ## node's (real or virtual) closing `)`, keeping the close's line info.
  dest.addParRi(it.n.endInfo)
  it.n = cs.scope; skip it.n

proc closeArgsScope(c: var SemContext; cs: var CallState; merge = true) =
  assert not cs.argsScopeClosed, "args scope already closed"
  if merge:
    commitShadowScope(c.currentScope)
  else:
    rollbackShadowScope(c.currentScope)
  cs.argsScopeClosed = true

proc untypedCall(c: var SemContext; dest: var TokenBuf; it: var Item; cs: var CallState) =
  closeArgsScope c, cs, merge = false
  dest.addParLe(cs.callNode.tagId, cs.callNodeInfo)
  dest.addSubtree cs.fn.n
  for a in cs.args:
    # XXX call semTemplBody for orig instead?
    dest.addSubtree a.n
  # close the `(call ...)` tree before `typeofCallIs`, otherwise `commonType`
  # hands `typematch` a cursor over an unterminated subtree and `addSubtree`
  # walks past the buffer end (assertion in nifcursors.load).
  leaveCall dest, it, cs
  # untyped propagates to the result type:
  typeofCallIs c, dest, it, cs.beforeCall, c.types.untypedType

proc semConvFromCall(c: var SemContext; dest: var TokenBuf; it: var Item; cs: CallState) =
  let beforeExpr = dest.len
  let info = cs.callNodeInfo
  var destType = cs.fn.typ
  if destType.typeKind == TypedescT: inc destType
  if destType.typeKind in {SinkT, LentT} and cs.args[0].typ.typeKind == TypedescT:
    var nullary = destType
    inc nullary
    if not nullary.hasMore:
      # sink T/lent T call
      var typeBuf = createTokenBuf(16)
      typeBuf.addParLe(destType.cursorTagId, destType.info)
      typeBuf.addSubtree cs.args[0].n
      typeBuf.addParRi()
      var item = Item(n: beginRead(typeBuf), typ: it.typ)
      semLocalTypeExpr(c, dest, item)
      # No call tree was opened in `dest` here, so unlike the ConvX path
      # below there is no close to emit — an unmatched `addParRi` would
      # seal the enclosing scope early under ParRi elision (the caller's
      # rollback `shrink` cannot undo a seal). Only advance past the call:
      it.n = cs.scope; skip it.n
      it.typ = item.typ
      return
  dest.addParLe(ConvX, info)
  dest.copyTree destType
  semConvArg(c, dest, destType, Item(n: cs.args[0].n, typ: cs.args[0].typ), info, beforeExpr)
  leaveCall dest, it, cs
  let expected = it.typ
  it.typ = destType
  commonType c, dest, it, beforeExpr, expected

proc semObjConstr(c: var SemContext; dest: var TokenBuf, it: var Item)

proc semObjConstrFromCall(c: var SemContext; dest: var TokenBuf; it: var Item; cs: CallState) =
  it.n = cs.scope; skip it.n
  var objBuf = createTokenBuf()
  objBuf.addParLe(OconstrX, cs.callNodeInfo)
  objBuf.addSubtree cs.fn.n
  objBuf.addParRi()
  var objConstr = Item(n: cursorAt(objBuf, 0), typ: it.typ)
  semObjConstr c, dest, objConstr
  it.typ = objConstr.typ

proc isAnumEfld(sym: SymId): bool =
  result = false
  let res = tryLoadSym(sym)
  if res.status == LacksNothing and res.decl.substructureKind == EfldU:
    var n = res.decl
    skipToLocalType n
    if n.isSymbol:
      let typeRes = tryLoadSym(n.symId)
      if typeRes.status == LacksNothing:
        let typeDecl = asTypeDecl(typeRes.decl)
        result = typeDecl.body.typeKind == AnumT

proc semSumTypeConstrFromCall(c: var SemContext; dest: var TokenBuf;
                               it: var Item; cs: var CallState) =
  it.n = cs.scope; skip it.n
  let info = cs.callNodeInfo
  let expected = it.typ
  assert cs.fn.n.isSymbol
  let efldSym = cs.fn.n.symId
  var objBuf = createTokenBuf(32)
  objBuf.addParLe(OconstrX, info)
  if expected.typeKind == AutoT:
    # Use ident so semObjConstr can do generic type inference:
    let efldName = symToIdent(efldSym)
    objBuf.addIdent(efldName, info)
  else:
    objBuf.addSubtree expected
    let kindName = pool.strings.getOrIncl("`kind")
    objBuf.addParLe(KvU, info)
    objBuf.addIdent(kindName, info)
    objBuf.addSymUse(efldSym, info)
    objBuf.addParRi()
  for arg in cs.args:
    let orig = arg.orig
    if orig.substructureKind == VvU:
      var scan = orig
      inc scan
      objBuf.addParLe(KvU, orig.info)
      objBuf.addSubtree scan
      skip scan
      objBuf.addSubtree scan
      objBuf.addParRi()
    else:
      buildErr c, dest, orig.info, "sum type constructor requires named arguments"
      return
  objBuf.addParRi()
  var objConstr = Item(n: cursorAt(objBuf, 0), typ: it.typ)
  semObjConstr c, dest, objConstr
  it.typ = objConstr.typ

proc anyArgTypeIsError(cs: CallState): bool =
  ## True when an argument already carries an `(err)` type, i.e. semchecking it
  ## failed and reported a diagnostic of its own.
  for a in cs.args:
    if a.typ.typeKind == ErrT: return true
  result = false

proc buildCallSource(buf: var TokenBuf; cs: CallState; callee: Cursor) =
  case cs.source
  of RegularCall:
    buf.addParLe(cs.callNode.tagId, cs.callNodeInfo)
    buf.addSubtree callee
    for a in cs.args:
      buf.addSubtree a.n
  of MethodCall:
    assert cs.args.len >= 1
    buf.addParLe(cs.callNode.tagId, cs.callNodeInfo)
    buf.addParLe(DotX, cs.callNodeInfo)
    buf.addSubtree cs.args[0].n
    buf.addParRi()
    buf.addSubtree callee
    for i in 1 ..< cs.args.len:
      buf.addSubtree cs.args[i].n
  of DotCall:
    assert cs.args.len == 1
    buf.addParLe(DotX, cs.callNodeInfo)
    buf.addSubtree cs.args[0].n
    buf.addSubtree callee
  of SubscriptCall:
    buf.addParLe(AtX, cs.callNodeInfo)
    for a in cs.args:
      buf.addSubtree a.n
  of CurlyatCall:
    buf.addParLe(CurlyatX, cs.callNodeInfo)
    for a in cs.args:
      buf.addSubtree a.n
  of DotAsgnCall:
    assert cs.args.len == 2
    buf.addParLe(AsgnS, cs.callNodeInfo)
    buf.addParLe(DotX, cs.callNodeInfo)
    buf.addSubtree cs.args[0].n
    let nameId = getIdent(callee)
    assert nameId != StrId(0)
    var name = pool.strings[nameId]
    assert name[^1] == '='
    name.setLen name.len - 1
    buf.addIdent(pool.strings.getOrIncl(name), cs.callNodeInfo)
    buf.addParRi()
    buf.addSubtree cs.args[1].n
  of SubscriptAsgnCall:
    buf.addParLe(AsgnS, cs.callNodeInfo)
    buf.addParLe(AtX, cs.callNodeInfo)
    let valueIndex = cs.args.len - 1
    for i in 0 ..< valueIndex:
      buf.addSubtree cs.args[i].n
    buf.addParRi()
    buf.addSubtree cs.args[valueIndex].n
  of CurlyatAsgnCall:
    buf.addParLe(AsgnS, cs.callNodeInfo)
    buf.addParLe(CurlyatX, cs.callNodeInfo)
    let valueIndex = cs.args.len - 1
    for i in 0 ..< valueIndex:
      buf.addSubtree cs.args[i].n
    buf.addParRi()
    buf.addSubtree cs.args[valueIndex].n
  buf.addParRi()

proc considerTypeboundOps(c: var SemContext; m: var seq[Match]; fnName: StrId; args: openArray[CallArg], genericArgs: Cursor, hasNamedArgs: bool) =
  # scope extension: procs attached to argument types are also considered
  # If the type is Typevar and it has attached
  # a concept, use the concepts symbols too:
  # This is somewhat similar to C++'s ADL (Argument Dependent Lookup).
  if fnName != StrId(0):
    # XXX maybe only trigger for open symchoice/ident callee, but the latter is not tracked
    var candidates = FnCandidates(marker: initHashSet[SymId]())
    # mark already matched symbols so that they don't get added:
    for i in 0 ..< m.len:
      if m[i].fn.sym != SymId(0):
        candidates.marker.incl m[i].fn.sym
    # add attached ops for each arg:
    for arg in args:
      let root = nominalRoot(arg.typ, allowTypevar = true)
      if root != SymId(0):
        addTypeboundOps c, fnName, root, candidates
    # now match them:
    for candidate in candidates.a:
      m.add createMatch(addr c)
      sigmatchNamedArgs(m[^1], candidate, args, genericArgs, hasNamedArgs)

proc addArgsInstConverters(c: var SemContext; dest: var TokenBuf; m: var Match; origArgs: openArray[CallArg]) =
  if not (m.genericConverter or m.refineArgType or m.insertedParam):
    dest.add m.args
  else:
    # no terminator token: `beginRead`'s cursor is bounded by the buffer
    var f = m.fn.typ
    if f.typeKind in RoutineTypes:
      skipToParams f
    assert f.substructureKind == ParamsU
    f = sub(f) # bound the param walk
    var arg = beginRead(m.args)
    var i = 0
    while arg.hasMore:
      if m.insertedParam and arg.isDotToken:
        let param = asLocal(f)
        assert not param.val.isDotToken
        var defaultValueBuf = createTokenBuf(30)
        var defaultValue = Item(n: param.val, typ: c.types.autoType)
        instantiateExprIntoBuf(c, defaultValueBuf, defaultValue, m.inferred)
        let prevErr = m.err
        swap m.args, dest
        typematch(m, param.typ, defaultValue)
        swap m.args, dest
        if m.err and not prevErr:
          c.typeMismatch dest, arg.info, defaultValue.typ, param.typ
        inc arg
      elif m.refineArgType and (isEmptyContainer(arg) or isEmptyOpenArrayCall(arg)):
        let isCall = arg.exprKind in CallKinds
        let start = dest.len
        var callStart = default(Cursor)
        if isCall:
          dest.addParLe(arg.cursorTagId, arg.info)
          callStart = arg
          arg = sub(arg)
          takeTree dest, arg
        let isDoubleCall = arg.exprKind in CallKinds # `@` call inside `toOpenArray` call case
        var innerCallStart = default(Cursor)
        if isDoubleCall:
          dest.addParLe(arg.cursorTagId, arg.info)
          innerCallStart = arg
          arg = sub(arg)
          takeTree dest, arg
        dest.addParLe(arg.cursorTagId, arg.info)
        let aconstrStart = arg
        arg = sub(arg)
        if containsGenericParams(arg):
          dest.addSubtree instantiateType(c, arg, m.inferred)
          skip arg
        else:
          takeTree dest, arg
        dest.addParRi(arg.endInfo)
        arg = aconstrStart; skip arg
        if isDoubleCall:
          dest.addParRi(arg.endInfo)
          arg = innerCallStart; skip arg
        if isCall:
          dest.addParRi(arg.endInfo)
          arg = callStart; skip arg
          # instantiate `@`/`toOpenArray` call, done by semchecking:
          var callBuf = createTokenBuf(dest.len - start)
          # balanced span: raw copy keeps its seals
          for tok in start ..< dest.len:
            callBuf.add dest[tok]
          dest.shrink start
          var call = Item(n: beginRead(callBuf), typ: c.types.autoType)
          semCall c, dest, call, {}
      elif m.genericConverter:
        # wrappers are entered with explicit scopes so their (possibly
        # elided) closes can be re-emitted by the mop-up loop below
        var wrapperScopes: seq[Cursor] = @[]
        while true:
          case arg.exprKind
          of HconvX:
            dest.addParLe(arg.cursorTagId, arg.info)
            wrapperScopes.add arg
            arg = sub(arg)
            dest.takeTree arg # skip type
          of BaseobjX:
            dest.addParLe(arg.cursorTagId, arg.info)
            wrapperScopes.add arg
            arg = sub(arg)
            # genericConverter is reused for object conversions to generic types
            if containsGenericParams(arg):
              dest.addSubtree instantiateType(c, arg, m.inferred)
              skip arg
            else:
              takeTree dest, arg
            dest.takeTree arg # skip intlit
          of HderefX, HaddrX:
            dest.addParLe(arg.cursorTagId, arg.info)
            wrapperScopes.add arg
            arg = sub(arg)
          else:
            break
        if arg.exprKind == HcallX:
          let convInfo = arg.info
          dest.addParLe(arg.cursorTagId, arg.info)
          wrapperScopes.add arg
          arg = sub(arg)
          if arg.isSymbol:
            let sym = arg.symId
            takeTree dest, arg
            let res = tryLoadSym(sym)
            if res.status == LacksNothing and res.decl.symKind == ConverterY:
              let routine = asRoutine(res.decl)
              if isGeneric(routine):
                let conv = FnCandidate(kind: routine.kind, sym: sym, typ: routine.params)
                var convMatch = createMatch(addr c)
                if i >= origArgs.len:
                  bug "addArgsInstConverters: i=" & $i & " origArgs.len=" & $origArgs.len &
                    " at " & arg.info.infoToStr
                sigmatch convMatch, conv, [CallArg(n: arg, typ: origArgs[i].typ)], emptyNode(c)
                # ^ could also use origArgs[i] directly but commonType would have to keep the expression alive
                assert not convMatch.err
                buildTypeArgs(convMatch)
                if convMatch.err:
                  # adding type args errored
                  buildErr c, dest, convInfo, getErrorMsg(convMatch)
                elif c.inGenericDefinition == 0:
                  let inst = c.requestRoutineInstance(conv.sym, convMatch.typeArgs, convMatch.inferred, convInfo)
                  setSymIdAt(dest, lastValueStart(dest), inst.targetSym)
                else:
                  # in generics, cannot instantiate yet
                  dest.shrink lastValueStart(dest)
                  dest.addParLe(AtX, convInfo)
                  dest.addSymUse(conv.sym, convInfo)
                  dest.add convMatch.typeArgs
                  dest.addParRi()
        # copy the wrapped expression's remainder and re-emit the closes;
        # without any wrapper the argument itself is copied:
        if wrapperScopes.len == 0:
          takeTree dest, arg
        else:
          while wrapperScopes.len > 0:
            if not arg.hasMore:
              dest.addParRi(arg.endInfo)
              let h = wrapperScopes.pop(); arg = h; skip arg
            else:
              takeTree dest, arg
      elif m.refineArgType and arg.exprKind == HconvX:
        var item = Item(n: arg, typ: c.types.autoType)
        semConv c, dest, item
        arg = item.n
      else:
        takeTree dest, arg
      skip f # should not be parri
      inc i
    assert not f.hasMore

proc tryConverterMatch(c: var SemContext; convMatch: var Match; f: TypeCursor, arg: CallArg): bool =
  ## looks for a converter from `arg` to `f`, returns `true` if found and
  ## sets `convMatch` to the match to the converter
  result = false
  let root = nominalRoot(f)
  if root == SymId(0) and LenientConvertersFeature notin c.features: return
  var converters = c.converters.getOrDefault(root)
  if root != SymId(0) and LenientConvertersFeature in c.features:
    for conv in c.converters.getOrDefault(SymId(0)):
      converters.add conv
  var convMatches: seq[Match] = @[]
  for conv in items converters:
    # f(a)
    # --> f(conv(a)) ?
    # conv's return type must match `f`.
    # conv's input type must match `a`.
    let res = tryLoadSym(conv)
    assert res.status == LacksNothing
    var fn = asRoutine(res.decl)
    assert fn.kind == ConverterY

    var inputMatch = createMatch(addr c)
    let candidate = FnCandidate(kind: fn.kind, sym: conv, typ: fn.params)

    var isEmptyOpenArray = false
    if arg.typ.typeKind == AutoT and isEmptyContainer(arg.n) and
        # normal overload of `toOpenArray` for arrays:
        (pool.syms[conv] == "toOpenArray.0." & SystemModuleSuffix or
          # normal overload of `toOpenArray` for seqs:
          pool.syms[conv] == "toOpenArray.1." & SystemModuleSuffix):
      # infer generic params of openarray converter, then match instantiated empty array/seq arg:
      isEmptyOpenArray = true
      var returnTypeMatch = createMatch(addr c)
      var returnType = candidate.typ
      skip returnType # get to return type
      typematch(returnTypeMatch, returnType, Item(n: emptyNode(c), typ: f))
      # if for some reason the openarray type doesn't match the converter:
      if classifyMatch(returnTypeMatch) notin {EqualMatch, GenericMatch}:
        continue
      inputMatch.inferred = returnTypeMatch.inferred

    # first match the input argument of `conv` so that the unification algorithm works as expected:
    sigmatch(inputMatch, candidate, [arg], emptyNode(c))
    if classifyMatch(inputMatch) notin {EqualMatch, GenericMatch, SubtypeMatch}:
      continue
    # use inputMatch.returnType here so the caller doesn't have to instantiate it again:
    if inputMatch.inferred.len != 0 and containsGenericParams(inputMatch.returnType):
      inputMatch.returnType = instantiateType(c, inputMatch.returnType, inputMatch.inferred)
    if isEmptyOpenArray:
      # argument is some empty array/seq literal populated with
      # toOpenArray's generic param as the type,
      # instantiate the type in the literal relative to the converter's generic params
      # so that only the generic params of the full call remain (if any exist)
      var instArgBuf = createTokenBuf(16)
      var argToInst = beginRead(inputMatch.args)
      assert isEmptyContainer(argToInst)
      let isCall = argToInst.exprKind in CallKinds
      var callStart = default(Cursor)
      if isCall:
        instArgBuf.addParLe(argToInst.cursorTagId, argToInst.info)
        callStart = argToInst
        argToInst = sub(argToInst)
        takeTree instArgBuf, argToInst # call symbol
      instArgBuf.addParLe(argToInst.cursorTagId, argToInst.info) # array constructor tag
      let aconstrStart = argToInst
      argToInst = sub(argToInst)
      instArgBuf.addSubtree instantiateType(c, argToInst, inputMatch.inferred)
      skip argToInst
      instArgBuf.addParRi(argToInst.endInfo) # array constructor
      argToInst = aconstrStart; skip argToInst
      if isCall:
        instArgBuf.addParRi(argToInst.endInfo) # call
        argToInst = callStart; skip argToInst
      inputMatch.args = instArgBuf

    let dest = inputMatch.returnType
    var callBuf = createTokenBuf(16) # dummy call node to use for matching dest type
    callBuf.addParLe(HcallX, arg.n.info)
    callBuf.addSymUse(conv, arg.n.info)
    callBuf.add inputMatch.args
    callBuf.addParRi()
    var newArg = Item(n: beginRead(callBuf), typ: dest)
    var fMatch = f
    var destMatch = createMatch(addr c)
    typematch(destMatch, fMatch, newArg)
    if classifyMatch(destMatch) in {EqualMatch, GenericMatch}:
      if isEmptyOpenArray:
        inputMatch.refineArgType = true
        # make argument type `auto` so sigmatch can identify it and match it
        # needed if `f` is generic, since we don't know the generic parameters yet
        inputMatch.returnType = c.types.autoType
      elif isGeneric(fn):
        inputMatch.genericConverter = true
      convMatches.add inputMatch
  let idx = pickBestMatch(c, convMatches)
  if idx >= 0:
    result = true
    # Move out of the seq instead of copying. Match's auto-derived `=dup`
    # would raw-bitcopy its TokenBuf fields (because TokenBuf has
    # `=copy.error.` and no usable `=dup`), aliasing the owner pointer
    # and producing a double-free at scope-end destroy. `convMatches`
    # goes out of scope right after, so the wasMoved-zeroed slot's
    # destroy is a no-op.
    convMatch = ensureMove(convMatches[idx])

proc varargsHasConverter(t: Cursor): bool =
  var t = t
  assert t.typeKind == VarargsT
  inc t
  skip t
  # Trailing StrLit is the openArray mangle hint planted by
  # `semcompat.compatRewriteParam`, not a converter.
  result = t.hasMore and t.kind != StrLit

proc tryVarargsConverter(c: var SemContext; convMatch: var Match; f: TypeCursor, arg: CallArg): bool =
  result = false
  var baseType = f
  assert baseType.typeKind == VarargsT
  inc baseType
  var conv = baseType
  skip conv
  assert conv.hasMore

  var callBuf = createTokenBuf(16)
  callBuf.addParLe(HcallX, arg.n.info)
  callBuf.addSubtree conv
  callBuf.addSubtree arg.n
  callBuf.addParRi()
  var call = beginRead(callBuf)
  var it = Item(n: call, typ: c.types.autoType)
  var destBuf = createTokenBuf(16)
  semCall c, destBuf, it, {} # might error
  it.n = beginRead(destBuf)

  var match = createMatch(addr c)
  typematch(match, baseType, it)
  let matchKind = classifyMatch(match)
  if matchKind >= GenericMatch:
    if matchKind == GenericMatch:
      match.genericConverter = true
    result = true
    convMatch = ensureMove(match)

proc runCompiledMacroPlugin(c: var SemContext; dest: var TokenBuf; it: var Item; cs: var CallState; finalFn: SymId) =
  # A macro's plugin binary is compiled when its DECLARATION is semchecked
  # (semdecls: `kind == MacroY and pass == checkBody`), which registers it in
  # `c.compiledMacros`. A macro IMPORTED from another module has its
  # declaration semchecked in the *defining* module's run, not here, so it is
  # absent from this run's `compiledMacros` — but the dependency build already
  # compiled its plugin into the shared nifcache. Accept that on-disk plugin so
  # imported macros (e.g. an importable `{.async.}`) expand too.
  if finalFn in c.compiledMacros or
     macroPluginExists(c.g.config.nifcachePath, finalFn):
    # Serialize arguments to NIF. Prefer `arg.orig` (raw, pre-sem AST) so
    # macros that walk their bodies aren't tripped by sem-attached `(err
    # …)` diagnostics or other sem rewrites the user never wrote.
    var argsBuf = createTokenBuf(30)
    argsBuf.addParLe StmtsS, cs.callNodeInfo
    for a in cs.args:
      if not cursorIsNil(a.orig):
        argsBuf.addSubtree a.orig
      else:
        argsBuf.addSubtree a.n
    argsBuf.addParRi()

    # Run the macro plugin
    var expandedInto = createTokenBuf(30)
    let success = runMacroPlugin(c.g.config.nifcachePath, expandedInto,
                                  cs.callNodeInfo, finalFn, argsBuf)
    if success:
      # Shrink dest to before the call and semcheck the expanded output
      dest.shrink cs.beforeCall
      expandedInto.addDotToken() # sentinel so the final `inc` stays in bounds
      var a = Item(n: cursorAt(expandedInto, 0), typ: c.types.autoType)
      inc c.routine.inInst
      # Plugin output is semchecked as if written in the macro's own module.
      # Its tokens carry the macro decl's file, so a private access the plugin
      # synthesised is judged against the macro's module while call-site
      # arguments it echoed back keep the caller's — see `visibilityModule`.
      let macroRes = tryLoadSym(finalFn)
      if macroRes.status == LacksNothing:
        c.visOwner.add VisOwner(module: extractModule(pool.syms[finalFn]),
                                file: macroRes.decl.info.file.uint32)
      else:
        c.visOwner.add VisOwner(module: extractModule(pool.syms[finalFn]),
                                file: cs.callNodeInfo.file.uint32)
      semExpr c, dest, a
      discard c.visOwner.pop()
      dec c.routine.inInst
      it.kind = a.kind
      typeofCallIs c, dest, it, cs.beforeCall, a.typ
    else:
      buildErr c, dest, cs.callNodeInfo, "macro plugin execution failed"
  else:
    buildErr c, dest, cs.callNodeInfo, "macro '" & pool.syms[finalFn] & "' not compiled"

proc inferTypevarsFromExpected(c: var SemContext; m: var Match; expected: TypeCursor) =
  ## When argument matching leaves a generic routine's typevars unbound but the
  ## call site has a concrete expected type, unify the routine's return type with
  ## it to bind the rest — e.g. `let r: Result[int, string] = ok(5)` infers `E`
  ## from the target even though only `T` appears in `ok`'s argument. Runs after
  ## overload selection, so it cannot affect which candidate is chosen; it only
  ## fills in bindings before `buildTypeArgs` checks they are all present. Merges
  ## only on a clean (non-converting) unify, so a conversion-to-expected (handled
  ## later by `commonType`) is left untouched.
  if m.err or m.fn.kind notin RoutineKinds or m.fn.sym == SymId(0): return
  if cursorIsNil(expected) or expected.isDotToken or
     expected.typeKind in {AutoT, VoidT} or containsGenericParams(expected): return
  if cursorIsNil(m.returnType) or m.returnType.isDotToken: return
  var rtMatch = createMatch(addr c)
  rtMatch.inferred = m.inferred  # seed with the bindings already found from args
  var rt = m.returnType
  typematch(rtMatch, rt, Item(n: emptyNode(c), typ: expected))
  if not rtMatch.err and classifyMatch(rtMatch) in {EqualMatch, GenericMatch}:
    m.inferred = rtMatch.inferred

proc resolveOverloads(c: var SemContext; dest: var TokenBuf; it: var Item; cs: var CallState) =
  # Everything the candidate collection below writes to `dest` is a
  # DIAGNOSTIC ("attempt to call routine", a symchoice element that cannot be
  # loaded, ...) and every one of them falls through to the error tail, which
  # emits an error tree of its own. Two trees out of one `semExpr` breaks its
  # one-expression contract: `(cast T (err ...) (err ...))` then has three
  # children and the next reader trips over the unconsumed rest
  # (nim-lang/nimony#2301). So remember where the diagnostics start and move
  # them out of `dest` once the candidate set is known.
  let errStart = dest.len
  let genericArgs =
    if cs.hasGenericArgs: cursorAt(cs.genericDest, 0)
    else: emptyNode(c)

  var m: seq[Match] = @[]
  if cs.fn.n.exprKind in {OchoiceX, CchoiceX}:
    var f = cs.fn.n
    f = sub(f) # bound the candidate walk
    while f.hasMore:
      if f.isSymbol:
        let sym = f.symId
        let s = fetchSym(c, sym)
        let typ = fetchCallableType(c, dest, f, s)
        let maybeProc = typ.skipModifier
        if maybeProc.typeKind in RoutineTypes:
          let candidate = FnCandidate(kind: s.kind, sym: sym, typ: maybeProc)
          m.add createMatch(addr c)
          sigmatchNamedArgs(m[^1], candidate, cs.args, genericArgs, cs.hasNamedArgs)
      else:
        buildErr c, dest, cs.fn.n.info, "`choice` node does not contain `symbol`"
      inc f
    considerTypeboundOps(c, m, cs.fnName, cs.args, genericArgs, cs.hasNamedArgs)
    if m.len == 0:
      # symchoice contained no callable symbols and no typebound ops
      assert cs.fnName != StrId(0)
      buildErr c, dest, cs.fn.n.info, "attempt to call routine: '" & pool.strings[cs.fnName] & "'"
  elif cs.fn.n.isIdent:
    # Callee stayed as an `Ident` because `AllowUndeclared` was set (typical
    # for dot-call desugaring and generic bodies). Still run ADL / concept
    # lookup on the argument types so e.g. `t.one()` → `one(t)` can match a
    # concept requirement without a real `one` in scope yet.
    considerTypeboundOps(c, m, cs.fnName, cs.args, genericArgs, cs.hasNamedArgs)
  elif cs.fn.typ.typeKind == TypedescT and cs.args.len == 1:
    closeArgsScope c, cs
    semConvFromCall c, dest, it, cs
    return
  elif cs.fn.typ.typeKind == TypedescT and cs.args.len == 0:
    closeArgsScope c, cs
    semObjConstrFromCall c, dest, it, cs
    return
  elif cs.fnKind == EfldY and cs.fn.n.isSymbol and isAnumEfld(cs.fn.n.symId):
    closeArgsScope c, cs
    semSumTypeConstrFromCall c, dest, it, cs
    return
  else:
    # Keep in mind that proc vars are a thing:
    let sym = if cs.fn.n.isSymbol: cs.fn.n.symId else: SymId(0)
    let typ = cs.fn.typ
    let maybeProc = typ.skipModifier
    if maybeProc.typeKind in RoutineTypes:
      let candidate = FnCandidate(kind: cs.fnKind, sym: sym, typ: maybeProc)
      m.add createMatch(addr c)
      sigmatchNamedArgs(m[^1], candidate, cs.args, genericArgs, cs.hasNamedArgs)
      considerTypeboundOps(c, m, cs.fnName, cs.args, genericArgs, cs.hasNamedArgs)
    elif sym != SymId(0):
      # non-callable symbol, look up all overloads
      assert cs.fnName != StrId(0)
      var choiceBuf = createTokenBuf(16)
      discard buildSymChoice(c, choiceBuf, cs.fnName, cs.fn.n.info, FindAll)
      # could store choiceBuf in CallState but cs.fn should not outlive it
      cs.fn = Item(n: beginRead(choiceBuf), typ: c.types.autoType, kind: CchoiceY)
      resolveOverloads(c, dest, it, cs)
      return
    else:
      buildErr c, dest, cs.fn.n.info, "cannot call expression of type " & typeToString(typ)
  # From here on exactly one tree is appended to `dest`: the call, or a single
  # `(err ...)`. `earlyErr` keeps the first diagnostic so the error tail can
  # still report it instead of its generic fallback. It stays a `default`
  # buffer on the overwhelmingly common path where nothing errored — that
  # costs no allocation, while `createTokenBuf` always allocates storage plus
  # a literals and a tag pool.
  var earlyErr = default(TokenBuf)
  if dest.len > errStart:
    earlyErr = createTokenBuf(4)
    earlyErr.addSubtree readonlyCursorAt(dest, errStart)
    dest.shrink errStart
  var idx = pickBestMatch(c, m, cs.flags)

  if idx < 0:
    # try converters
    var matchAdded = false
    let L = m.len
    var csArgsOrig: seq[CallArg] = @[]
    if cs.hasNamedArgs:
      csArgsOrig = move cs.args
    for mi in 0 ..< L:
      if not m[mi].err: continue
      var newMatch = createMatch(addr c)
      var newArgs: seq[CallArg] = @[]
      var newArgBufs: seq[TokenBuf] = @[] # to keep alive
      var param = skipProcTypeToParams(m[mi].fn.typ)
      if cs.hasNamedArgs:
        cs.args = orderArgs(newMatch, param, csArgsOrig)
      assert param.isParamsTag
      param = sub(param) # throwaway copy; bounds the walk under vpr
      var ai = 0
      var anyConverters = false
      while param.hasMore:
        # varargs not handled yet
        if ai >= cs.args.len: break
        let f = asLocal(param).typ
        let isVarargs = f.typeKind == VarargsT
        if not isVarargs:
          skip param
        var arg = cs.args[ai]
        var convMatch = default(Match)
        if isVarargs and varargsHasConverter(f) and tryVarargsConverter(c, convMatch, f, arg):
          anyConverters = true
          # match already built call, just use it
          let bufPos = newArgBufs.len
          newArgBufs.add ensureMove(convMatch.args)
          var baseType = f
          inc baseType
          if convMatch.genericConverter:
            # can just instantiate here
            baseType = instantiateType(c, baseType, convMatch.inferred)
          newArgs.add CallArg(n: beginRead(newArgBufs[bufPos]), typ: baseType)
        elif tryConverterMatch(c, convMatch, f, arg):
          anyConverters = true
          var argBuf = createTokenBuf(16)
          argBuf.addParLe(HcallX, arg.n.info)
          argBuf.addSymUse(convMatch.fn.sym, arg.n.info)
          if convMatch.refineArgType:
            # empty openarray converter
            newMatch.refineArgType = true
          elif convMatch.genericConverter:
            # instantiate after match
            newMatch.genericConverter = true
          argBuf.add convMatch.args
          argBuf.addParRi()
          let bufPos = newArgBufs.len
          newArgBufs.add ensureMove(argBuf)
          newArgs.add CallArg(n: beginRead(newArgBufs[bufPos]), typ: convMatch.returnType)
        else:
          newArgs.add arg
        inc ai
      if anyConverters:
        sigmatch(newMatch, m[mi].fn, newArgs, genericArgs)
        m.add newMatch
        matchAdded = true
    if matchAdded: # m.len != L
      idx = pickBestMatch(c, m, cs.flags)
    if idx < 0 and cs.hasNamedArgs:
      # restore original args for error message generation:
      cs.args = csArgsOrig

  if idx >= 0:
    dest.addParLe(cs.callNode.tagId, cs.callNodeInfo)
    let finalFn = m[idx].fn
    # only merge symbols defined in args to scope if we did not match a macro/template:
    closeArgsScope c, cs, merge = finalFn.kind notin {MacroY, TemplateY}
    let isMagic = c.addFn(dest, finalFn, cs.fn.n, m[idx])
    if finalFn.kind notin {TemplateY, MacroY}:
      # Nim 2 compat: if a typed-varargs slot matched, replace the flat
      # tail of `m.args` with the openArray bundle so the next line emits
      # `(call f (hcall toOpenArray.0[I,T] (aconstr …)))` straight into
      # `dest`. Skipped for templates (`firstVarargMatch` substitution
      # needs the flat args) and macros (their plugin reads `cs.args`).
      let varargsElem = compatVarargsParamElem(m[idx].fn)
      if not cursorIsNil(varargsElem):
        compatBundleVarargsInMatch c, m[idx], varargsElem, cs.callNodeInfo
    addArgsInstConverters(c, dest, m[idx], cs.args)
    leaveCall dest, it, cs
    if m[idx].hasUnboundTypevars:
      inferTypevarsFromExpected(c, m[idx], it.typ)
    buildTypeArgs(m[idx])

    if m[idx].err:
      # adding args or type args may have errored
      if finalFn.sym != SymId(0) and
          # overload of `@` with empty array param:
          pool.syms[finalFn.sym] == "@.1." & SystemModuleSuffix and
          (AllowEmpty in cs.flags or isSomeSeqType(it.typ) or isSomeOpenArrayType(it.typ)):
        # empty seq will be handled, either by `commonType` now or
        # the call this is an argument of in the case of AllowEmpty
        typeofCallIs c, dest, it, cs.beforeCall, c.types.autoType
      else:
        # `leaveCall` has already closed the call tree at `cs.beforeCall`, so
        # the error has to REPLACE it: appended, it would be a second child of
        # whatever encloses the call, and a node with a fixed arity — `(ret X)`
        # above all — then has one child too many. The next reader trips over
        # the leftover (`defer` lowering asserted on it, nim-lang/nimony#2400).
        buildErrAt c, dest, cs.beforeCall, getErrorMsg(m[idx])
    elif finalFn.kind == TemplateY:
      if c.templateInstCounter <= MaxNestedTemplates:
        c.expanded.addSymUse finalFn.sym, cs.callNodeInfo
        inc c.templateInstCounter
        withErrorContext c, cs.callNodeInfo:
          semTemplateCall c, dest, it, finalFn.sym, cs.beforeCall, m[idx], cs.flags
        dec c.templateInstCounter
      else:
        # same as above: replace the closed call tree, never append to it
        buildErrAt c, dest, cs.beforeCall, "recursion limit exceeded for template expansions"
    elif finalFn.kind == MacroY:
      # Run compiled macro plugin
      runCompiledMacroPlugin(c, dest, it, cs, finalFn.sym)
    elif isMagic == MagicCallNeedsSemcheck:
      # semcheck produced magic expression
      var magicExprBuf = createTokenBuf(dest.len - cs.beforeCall)
      var magicExprN = cursorAt(dest, cs.beforeCall)
      magicExprBuf.addUnstructured magicExprN
      # Drop the rc before rewinding: a live cursor turns `shrink` into a full
      # copy of `dest`.
      endRead magicExprN
      dest.shrink cs.beforeCall
      var magicExpr = Item(n: cursorAt(magicExprBuf, 0), typ: it.typ)
      semExpr c, dest, magicExpr, cs.flags
      it.typ = magicExpr.typ
    elif finalFn.kind == IteratorY and PreferIterators notin cs.flags:
      buildErr c, dest, cs.callNodeInfo, "Iterators can be called only in `for` statements"
    elif m[idx].inferred.len > 0:
      # Move out of the seq — see convMatch note above. The seq goes out
      # of scope right after resolveOverloads returns.
      var matched = ensureMove(m[idx])
      let returnType: Cursor
      if isMagic == NonMagicCall and c.inGenericDefinition == 0 and
          isGeneric(getProcDecl(finalFn.sym)):
        let inst = c.requestRoutineInstance(finalFn.sym, matched.typeArgs, matched.inferred, cs.callNodeInfo)
        # `addFn` emits the callee in different shapes — usually a
        # single `Symbol` at `cs.beforeCall+1`, but a phantom-concept
        # match yields an `Ident` or a `(cchoice …)` subtree (see
        # `addFn`'s `fromConcept` path), and re-sem at instantiation
        # time picks the actual overload. Patch the sym in place only
        # when the slot is a real symbol token; otherwise leave the
        # callee shape alone and rely on the later re-sem.
        # the call head may carry line-info suffix tokens; head+1 is not
        # necessarily the callee slot
        let calleePos = cs.beforeCall + tokenWidth(readonlyCursorAt(dest, cs.beforeCall))
        if calleePos < dest.len and readonlyCursorAt(dest, calleePos).isSymbol:
          setSymIdAt(dest, calleePos, inst.targetSym)
        var instReturnType = createTokenBuf(16)
        var subsReturnType = inst.returnType
        returnType = semReturnType(c, instReturnType, subsReturnType)
      else:
        if isMagic == NonMagicCall and cs.hasGenericArgs:
          # add back explicit generic args since we cannot instantiate
          var invokeBuf = createTokenBuf(16)
          invokeBuf.addParLe(AtX, cs.fn.n.info)
          invokeBuf.addSymUse(finalFn.sym, cs.fn.n.info)
          var genericArgsRead = genericArgs
          while genericArgsRead.hasMore:
            takeTree invokeBuf, genericArgsRead
          invokeBuf.addParRi()
          # head+1 may be a line-info suffix; locate the callee positionally
          # and account for its real width (a Symbol may carry suffixes too)
          let calleePos = cs.beforeCall + tokenWidth(readonlyCursorAt(dest, cs.beforeCall))
          let growth = invokeBuf.len - subtreeWidth(readonlyCursorAt(dest, calleePos))
          replace dest, beginRead(invokeBuf), calleePos
          # the call tree is already sealed; `replace` cannot widen it itself:
          widenSealed dest, cs.beforeCall, growth
        if matched.returnType.isDotToken:
          returnType = matched.returnType
        else:
          returnType = instantiateType(c, matched.returnType, matched.inferred)
      typeofCallIs c, dest, it, cs.beforeCall, returnType
    else:
      var returnType = m[idx].returnType

      var returnTypeBuf = createTokenBuf()
      returnType = semReturnType(c, returnTypeBuf, returnType)

      typeofCallIs c, dest, it, cs.beforeCall, returnType

  else:
    it.n = cs.scope; skip it.n
    # do not add symbols defined in args on failed match:
    closeArgsScope c, cs, merge = false
    var errored = createTokenBuf(4)
    buildCallSource errored, cs, cs.fn.n
    let erroredN = cursorAt(errored, 0)
    var errorMsg: string
    if idx == -2:
      errorMsg = "ambiguous call: '"
      if cs.fnName != StrId(0):
        errorMsg.add pool.strings[cs.fnName]
      errorMsg.add "'"
      # Add proc signatures and location for each match
      for i in 0..<m.len:
        errorMsg.add "\n"
        errorMsg.add typeToString(m[i].fn.typ, {renderNoBody})
        if m[i].fn.sym != NoSymId:
          let res = tryLoadSym(m[i].fn.sym)
          if res.status == LacksNothing:
            errorMsg.add " (declared in " & res.decl.info.infoToStr & ")"
    elif cs.source in {DotCall, DotAsgnCall} and cs.fnName != StrId(0):
      errorMsg = "undeclared field: '"
      if cs.fnName != StrId(0):
        errorMsg.add pool.strings[cs.fnName]
      errorMsg.add "'"
      if cs.args.len != 0: # just to be safe
        errorMsg.add " for type "
        errorMsg.add typeToString(cs.args[0].typ)
    elif m.len > 0 and anyArgTypeIsError(cs):
      # Every candidate failed only because an argument is already erroneous;
      # the diagnostic was reported where that argument was produced. Emit a
      # message-less `(err …)` — `reportErrors` counts it (so the module still
      # fails) but prints nothing — instead of dumping the whole overload set
      # on top of an error the user has already been shown. Otherwise a single
      # bad operand turns into one `expected: <candidate> but got: <type error>`
      # line per candidate, eleven of them for `*` alone.
      dest.buildTree ErrT, cs.callNodeInfo:
        dest.addSubtree erroredN
        dest.addStrLit("", cs.callNodeInfo)
      return
    elif allUninstantiable(m):
      # Not a type mismatch — the arguments matched. `sigmatch` dropped every
      # candidate because a type parameter of it can never be bound, and that
      # message says so precisely.
      buildErr c, dest, cs.callNodeInfo, getErrorMsg(m[0])
      return
    elif m.len > 0:
      errorMsg = "Type mismatch at [position]\n"
      errorMsg.add asNimCode erroredN
      for i in 0..<m.len:
        errorMsg.add "\n"
        addErrorMsg errorMsg, m[i]
        if m[i].fn.sym != NoSymId:
          let res = tryLoadSym(m[i].fn.sym)
          if res.status == LacksNothing:
            errorMsg.add " (declared in " & res.decl.info.infoToStr & ")"
    elif earlyErr.len > 0:
      # A precise diagnostic was already produced while collecting candidates
      # (typically "attempt to call routine: 'x'" for a non-callable symbol).
      # Use it as the callee of the reconstructed source instead of claiming
      # the name is undeclared — it is declared, just not callable.
      var calleeN = beginRead(earlyErr)
      buildCallSource dest, cs, calleeN
      endRead calleeN
      return
    else:
      errorMsg = "undeclared identifier: '"
      if cs.fnName != StrId(0):
        errorMsg.add pool.strings[cs.fnName]
      errorMsg.add "'"
      var calleeBuf = createTokenBuf(4)
      buildErr c, calleeBuf, cs.fn.n.info, errorMsg, cs.fn.n
      var calleeN = beginRead(calleeBuf)
      buildCallSource dest, cs, calleeN
      endRead calleeN
      return
    buildErr c, dest, cs.callNodeInfo, errorMsg, erroredN

proc getFnIdent(c: var SemContext; dest: var TokenBuf): StrId =
  var n = beginRead(dest)
  result = takeIdent(n)

proc findMagicInSyms(syms: Cursor): ExprKind =
  ## Looks for a magic in a bare symbol or anywhere in a symchoice tree.
  var syms = syms
  result = NoExpr
  case syms.kind
  of Symbol:
    let res = tryLoadSym(syms.symId)
    if res.status == LacksNothing:
      var n = res.decl
      inc n # skip the symbol kind
      if n.isSymbolDef:
        inc n # skip the SymbolDef
        if n.isTagLit:
          result = n.exprKind
  of TagLit:
    if syms.exprKind in {OchoiceX, CchoiceX}:
      syms.loopInto:
        result = findMagicInSyms(syms)
        if result != NoExpr: return
        skip syms
  else: discard

proc unoverloadableMagicCall(c: var SemContext; dest: var TokenBuf; it: var Item; cs: var CallState; magic: ExprKind) =
  if cs.args.len != 0:
    # keep args after if they were produced by dotcall: retag the head tree
    # in place and reopen it so the appended args + `addParRi` re-seal it.
    var t = cs.dest[0]
    setTag(t, cast[TagId](uint32(ord(magic))))
    cs.dest[0] = t
    cs.dest.reopenLastTree(0)
  else:
    cs.dest.shrink 0
    cs.dest.addParLe(magic, cs.callNodeInfo)
  while it.n.hasMore:
    # add all args in call:
    takeTree cs.dest, it.n
  cs.dest.addParRi(it.n.endInfo)
  it.n = cs.scope; skip it.n
  var magicCall = Item(n: beginRead(cs.dest), typ: it.typ)
  semExpr c, dest, magicCall, cs.flags
  it.typ = magicCall.typ

proc atHasTypeArgs(c: var SemContext; n: Cursor): bool =
  ## True when `n` points at type arguments inside `(at Op Type ...)`.
  ## Distinguishes explicit generic instantiation from value subscripting
  ## `(at arr index)` where the next son is a value.
  if not n.hasMore or not n.hasMore: return false
  if n.isSymbol:
    let res = tryLoadSym(n.symId)
    return res.status == LacksNothing and res.decl.symKind == TypeY
  if n.isTagLit:
    return typeKind(n) != NoType
  false

proc semCall(c: var SemContext; dest: var TokenBuf; it: var Item; flags: set[SemFlag]; source: TransformedCallSource = RegularCall) =
  var cs = CallState(
    beforeCall: dest.len,
    callNode: it.n.load(),
    callNodeInfo: it.n.info,
    dest: createTokenBuf(16),
    source: source,
    flags: {InTypeContext, AllowEmpty, PreferIterators}*flags
  )
  cs.scope = it.n
  it.n = sub(it.n)
  # open temp scope for args, has to be closed after matching:
  openShadowScope(c.currentScope)
  swap dest, cs.dest
  cs.fn = Item(n: it.n, typ: c.types.autoType)
  var argIndexes: seq[int] = @[]
  if cs.fn.n.exprKind == AtX:
    let atStart = cs.fn.n # skip tag
    cs.fn.n = sub(cs.fn.n)
    var lhsBuf = createTokenBuf(4)
    var lhs = Item(n: cs.fn.n, typ: c.types.autoType)
    # `AllowOverloads` so an explicit generic instantiation `foo[...]` sees the
    # overload set; the module symbol sharing the name is filtered out of the
    # sym choice (nim-lang/nimony#2130):
    semExpr c, lhsBuf, lhs, {KeepMagics, AllowUndeclared, AllowOverloads}
    cs.fn.n = lhs.n
    lhs.n = cursorAt(lhsBuf, 0)
    var maybeRoutine = lhs.n
    if maybeRoutine.exprKind in {OchoiceX, CchoiceX}:
      inc maybeRoutine
    var treatAsGenericInst = false
    if maybeRoutine.isSymbol:
      let res = tryLoadSym(maybeRoutine.symId)
      assert res.status == LacksNothing
      if isRoutine(res.decl.symKind) and isGeneric(asRoutine(res.decl)):
        treatAsGenericInst = true
    if not treatAsGenericInst and lhs.kind != TypeY and atHasTypeArgs(c, cs.fn.n):
      treatAsGenericInst = true
    if treatAsGenericInst:
      cs.hasGenericArgs = true
      cs.genericDest = createTokenBuf(16)
      swap dest, cs.genericDest
      while cs.fn.n.hasMore:
        semLocalTypeImpl c, dest, cs.fn.n, AllowValues
      # no terminator: `genericDest` consumers walk with `hasMore`
      cs.fn.n = atStart; skip cs.fn.n
      swap dest, cs.genericDest
      it.n = cs.fn.n
      dest.addSubtree lhs.n
      cs.fn.typ = lhs.typ
      cs.fn.kind = lhs.kind
      cs.fnName = getFnIdent(c, dest)
    if not cs.hasGenericArgs:
      semBuiltinSubscript(c, dest, cs.fn, lhs, atStart)
      cs.fnName = getFnIdent(c, dest)
      it.n = cs.fn.n
  elif cs.fn.n.exprKind == DotX:
    let dotStart = dest.len
    let dotInfo = cs.fn.n.info
    # read through the dot expression first:
    let dotHead = cs.fn.n # skip tag
    cs.fn.n = sub(cs.fn.n)
    var lhsBuf = createTokenBuf(4)
    let lhsOrig = cs.fn.n
    var lhs = Item(n: cs.fn.n, typ: c.types.autoType)
    semExpr c, lhsBuf, lhs, {AllowModuleSym}
    cs.fn.n = lhs.n
    lhs.n = cursorAt(lhsBuf, 0)
    let fieldNameCursor = cs.fn.n
    let fieldName = takeIdent(cs.fn.n)
    # skip optional inheritance depth:
    if cs.fn.n.isIntLit:
      inc cs.fn.n
    var dotFlags: set[SemFlag] = {KeepMagics, AllowUndeclared, AllowOverloads}
    var dotAccessToken = ""
    if cs.fn.n.isStringLit:
      dotFlags.incl BypassFieldVis
      dotAccessToken = pool.strings[cs.fn.n.strId]
      inc cs.fn.n
    cs.fn.n = dotHead; skip cs.fn.n
    it.n = cs.fn.n
    # now interpret the dot expression:
    let dotState = tryBuiltinDot(c, dest, cs.fn, lhs, fieldName, dotInfo,
                                  dotFlags, dotAccessToken)
    if dotState == FailedDot and dotLhsModuleSym(lhs) != SymId(0):
      # `m.nosuchproc(...)`: a module qualifier has no UFCS fallback, so the
      # miss is simply an error. Report it and leave instead of running the
      # rest of the call machinery on an `(err ...)` callee, which buries this
      # message under "cannot call expression of type auto" (#2308).
      dest.shrink dotStart
      swap dest, cs.dest
      closeArgsScope c, cs, merge = false
      buildErr c, dest, dotInfo, "undeclared identifier in module: '" &
                 pool.strings[fieldName] & "'"
      it.n = cs.scope; skip it.n
      it.typ = c.types.autoType
      return
    elif dotState == FailedDot or
        # also ignore non-proc fields:
        (dotState == MatchedDotField and cs.fn.typ.typeKind notin RoutineTypes):
      cs.source = MethodCall
      # turn a.b(...) into b(a, ...)
      # first, delete the output of `tryBuiltinDot`:
      dest.shrink dotStart
      # sem b:
      cs.fn = Item(n: fieldNameCursor, typ: c.types.autoType)
      semExpr c, dest, cs.fn, {KeepMagics, AllowUndeclared, AllowOverloads}
      cs.fnName = getFnIdent(c, dest)
      # add a as argument:
      let lhsIndex = dest.len
      dest.addSubtree lhs.n
      argIndexes.add lhsIndex
      cs.args.add CallArg(typ: lhs.typ, orig: lhsOrig) # n will be set by argIndexes
  else:
    semExpr(c, dest, cs.fn, {KeepMagics, AllowUndeclared, AllowOverloads})
    cs.fnName = getFnIdent(c, dest)
    it.n = cs.fn.n
  if EarlyMagicsFeature in c.features and cs.fnName in c.unoverloadableMagics:
    # transform call early before semchecking arguments
    let syms = beginRead(dest)
    let magic = findMagicInSyms(syms)
    if magic != NoExpr:
      swap dest, cs.dest
      unoverloadableMagicCall(c, dest, it, cs, magic)
      return
  when defined(debug):
    let oldDebugAllowErrors = c.debugAllowErrors
    if cs.fnName in c.unoverloadableMagics:
      c.debugAllowErrors = true
  cs.fnKind = cs.fn.kind
  var skipSemCheck = false
  while it.n.hasMore:
    let argOrig = it.n
    var arg = Item(n: it.n, typ: c.types.autoType)
    argIndexes.add dest.len
    let named = arg.n.substructureKind == VvU
    var namedStart = default(Cursor)
    if named:
      cs.hasNamedArgs = true
      dest.addParLe(arg.n.cursorTagId, arg.n.info)
      namedStart = arg.n
      arg.n = sub(arg.n)
      takeTree dest, arg.n
    semExpr c, dest, arg, {AllowEmpty}
    if named:
      dest.addParRi(arg.n.endInfo)
      # reset only reachable inside `if named:`, where namedStart was set:
      if not cursorIsNil(namedStart):
        arg.n = namedStart; skip arg.n
    if arg.typ.typeKind == UntypedT:
      skipSemCheck = true
    it.n = arg.n
    cs.args.add CallArg(typ: arg.typ, orig: argOrig) # n will be set by argIndexes
  when defined(debug):
    c.debugAllowErrors = oldDebugAllowErrors
  assert cs.args.len == argIndexes.len
  swap dest, cs.dest
  cs.fn.n = beginRead(cs.dest)
  for i in 0 ..< cs.args.len:
    cs.args[i].n = cursorAt(cs.dest, argIndexes[i])
  if skipSemCheck:
    untypedCall c, dest, it, cs
  else:
    resolveOverloads c, dest, it, cs
  assert cs.argsScopeClosed
