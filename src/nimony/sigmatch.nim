#       Nimony
# (c) Copyright 2024 Andreas Rumpf
#
# See the file "license.txt", included in this
# distribution, for details about the copyright.

import std / [sets, tables, assertions]

include ".." / lib / nifprelude
include ".." / lib / compat2

import nimony_model, decls, programs, semdata, typeprops, xints, builtintypes, renderer, asthelpers,
  features, symtabs, sigconcepts, expreval, staticmatches
import ".." / lib / symparser
import ".." / models / tags

type
  CallArg* = object
    n*, typ*: Cursor
    orig*: Cursor ## original tree before semchecking, used for untyped args

  FnCandidate* = object
    kind*: SymKind
    sym*: SymId
    typ*: Cursor
    fromConcept*: bool

  MatchErrorKind* = enum
    InvalidMatch
    InvalidRematch
    ConstraintMismatch
    FormalTypeNotAtEndBug
    FormalParamsMismatch
    CallConvMismatch
    RaisesMismatch
    ClosureMismatch
    PassiveMismatch
    UnavailableSubtypeRelation
    ImplicitConversionNotMutable
    VarNeeded
    UnhandledTypeBug
    MismatchBug
    MissingExplicitGenericParameter
    ExtraGenericParameter
    RoutineIsNotGeneric
    CouldNotInferTypeVar
    TooManyArguments
    TooFewArguments
    NameNotFound
    ParamAlreadyGiven

  MatchError* = object
    info: PackedLineInfo
    #msg: string
    kind: MatchErrorKind
    typeVar: SymId
    expected, got: TypeCursor
    pos: int

  Match* = object
    inferred*: Table[SymId, Cursor]
    tvars: HashSet[SymId]
    fn*: FnCandidate
    args*, typeArgs*: TokenBuf
    err*, flipped*: bool
    concreteMatch: bool
    ignoreConstraints: bool ## tie-breaking only: let a typevar formal bind any
                            ## arg regardless of its constraint, so relative
                            ## specificity ("concrete beats typevar") is decided
                            ## structurally from the two signatures
    hasError: bool # mark that error message was set
    skippedMod: TypeKind
    argInfo: PackedLineInfo
    pos, opened: int
    inheritanceCosts, intLitCosts, intConvCosts, convCosts: int
    returnType*: Cursor
    context: ptr SemContext
    error: MatchError
    firstVarargPosition*: int
    varargsEndPosition*: int
    genericConverter*, refineArgType*, insertedParam*: bool

proc createMatch*(context: ptr SemContext): Match =
  Match(context: context, firstVarargPosition: -1, varargsEndPosition: -1)

proc scopeBump(m: Match): int =
  ## Models an implicit `Scope` parameter on every routine. Same-module
  ## calls pass `Scope` (exact match); cross-module calls pass
  ## `ImportScope`, where `ImportScope = object of Scope` (subtype match,
  ## depth 1). Returns the phantom parameter's contribution to
  ## `inheritanceCosts`: 0 for a same-module candidate, 1 otherwise.
  ##
  ## Treating module-of-origin as a subtyping dimension makes overload
  ## resolution prefer locally defined routines over imported ones
  ## without a separate scope filter; the rule composes naturally with
  ## the existing match ranking (implicit conversions still beat scope
  ## preference because `convCosts` is ordered before `inheritanceCosts`
  ## in `cmpMatches`).
  ##
  ## Evaluated lazily by `cmpMatches`, so unique-resolution call sites
  ## pay nothing: only candidates that actually compete with another
  ## successful match are inspected.
  if m.context == nil or m.fn.sym == SymId(0): return 0
  let s = pool.syms[m.fn.sym]
  # Locate the dot that introduces the module suffix without allocating
  # a substring. Mirrors `extractModule` in lib/symparser.nim: a trailing
  # numeric segment means no module suffix at all (treated as local).
  var i = s.len - 2
  while i > 0:
    if s[i] == '.':
      if s[i+1] in {'0'..'9'}: return 0
      let suf = m.context.thisModuleSuffix
      let mLen = s.len - i - 1
      if mLen != suf.len: return 1
      for j in 0 ..< mLen:
        if s[i+1+j] != suf[j]: return 1
      return 0
    dec i
  return 0

proc error(m: var Match; k: MatchErrorKind; expected, got: Cursor) =
  m.err = true
  if m.hasError: return # first error is the important one
  m.hasError = true
  m.error = MatchError(info: m.argInfo, kind: k,
                       expected: expected, got: got, pos: m.pos+1)
  #writeStackTrace()
  #echo "ERROR: ", typeToString(m.error.expected)

proc error0(m: var Match; k: MatchErrorKind) =
  m.err = true
  if m.hasError: return # first error is the important one
  m.hasError = true
  m.error = MatchError(info: m.argInfo, kind: k, pos: m.pos+1)

proc errorTypevar(m: var Match; k: MatchErrorKind; expected, got: Cursor; typevar: SymId) =
  m.err = true
  if m.hasError: return # first error is the important one
  m.hasError = true
  m.error = MatchError(info: m.argInfo, kind: k,
                       typeVar: typevar,
                       expected: expected, got: got, pos: m.pos+1)

proc error0Typevar(m: var Match; k: MatchErrorKind; typevar: SymId) =
  m.err = true
  if m.hasError: return # first error is the important one
  m.hasError = true
  m.error = MatchError(info: m.argInfo, kind: k,
                       typeVar: typevar, pos: m.pos+1)

proc constraintToString(c: Cursor): string =
  ## For a typevar formal, render its *constraint* (e.g. `Comparable`) rather
  ## than its name, so a mismatch reads "T does not match constraint Comparable"
  ## instead of the unhelpful "T does not match constraint T". Only ever called
  ## on the error-reporting path, so the extra `tryLoadSym` costs nothing in the
  ## common case.
  if c.kind == Symbol:
    let res = tryLoadSym(c.symId)
    if res.status == LacksNothing and res.decl.symKind == TypevarY:
      let tv = asTypevar(res.decl)
      # Render only a *named* constraint (a concept like `Comparable`), where
      # `typeToString` is a simple symbol lookup. Structural typeclasses such
      # as `(ordinal)` or `(or …)` don't render standalone (gtype skips into
      # the closing `)`), and `.` means unconstrained — both fall back to the
      # typevar's own name.
      if tv.typ.kind == Symbol:
        return typeToString(tv.typ)
  typeToString(c)

proc getErrorMsg*(m: Match): string =
  case m.error.kind
  of InvalidMatch:
    "expected: " & typeToString(m.error.expected) & " but got: " & typeToString(m.error.got)
  of InvalidRematch:
    "Could not match again: " & pool.syms[m.error.typeVar] & " expected " &
      typeToString(m.error.expected) & " but got " & typeToString(m.error.got)
  of ConstraintMismatch:
    typeToString(m.error.got) & " does not match constraint " &
      constraintToString(m.error.expected)
  of FormalTypeNotAtEndBug:
    "BUG: formal type not at end!"
  of FormalParamsMismatch:
    "parameter lists do not match"
  of CallConvMismatch:
    "calling conventions do not match"
  of RaisesMismatch:
    "`.raises` mismatch"
  of ClosureMismatch:
    "`.closure` mismatch"
  of PassiveMismatch:
    "`.passive` mismatch"
  of UnavailableSubtypeRelation:
    "subtype relation not available for `out` parameters"
  of ImplicitConversionNotMutable:
    "implicit conversion to " & typeToString(m.error.expected) & " is not mutable"
  of VarNeeded:
    "expression is not a mutable lvalue, cannot be passed to " &
      typeToString(m.error.expected) & " parameter"
  of UnhandledTypeBug:
    "BUG: unhandled type: " & pool.tags[m.error.expected.tagId]
  of MismatchBug:
    "BUG: expected: " & typeToString(m.error.expected) & " but got: " & typeToString(m.error.got)
  of MissingExplicitGenericParameter:
    "missing explicit generic parameter for " & pool.syms[m.error.typeVar]
  of ExtraGenericParameter:
    "extra generic parameter"
  of RoutineIsNotGeneric:
    "routine is not generic"
  of CouldNotInferTypeVar:
    "could not infer type for " & pool.syms[m.error.typeVar]
  of TooManyArguments:
    "too many arguments"
  of TooFewArguments:
    "too few arguments"
  of NameNotFound:
    "named argument not found"
  of ParamAlreadyGiven:
    "parameter already given"

proc addErrorMsg*(dest: var string; m: Match) =
  assert m.err
  dest.add "[" & $(m.error.pos) & "] " & getErrorMsg(m)

proc addErrorMsg*(dest: var TokenBuf; m: Match) =
  assert m.err
  dest.addParLe nifstreams.ErrT, m.argInfo
  dest.addDotToken()
  let str = "For type " & typeToString(m.fn.typ) & " mismatch at position\n" &
    "[" & $(m.pos+1) & "] " & getErrorMsg(m)
  dest.addStrLit str
  dest.addParRi()

proc getProcDecl*(s: SymId): Routine =
  let res = tryLoadSym(s)
  assert res.status == LacksNothing
  result = asRoutine(res.decl, SkipInclBody)

proc isObjectType*(s: SymId): bool =
  let res = tryLoadSym(s)
  assert res.status == LacksNothing
  var n = res.decl
  if n.stmtKind == TypeS:
    inc n # skip ParLe
    for i in 1..4:
      skip(n) # name, export marker, pragmas, generic parameter
    if n.typeKind in {RefT, PtrT}:
      inc n
    result = n.typeKind == ObjectT
  else:
    result = false

proc isObjectType(n: Cursor): bool =
  var n = n
  if n.typeKind == InvokeT:
    inc n
  if n.kind == Symbol:
    result = isObjectType(n.symId)
  else:
    result = false

proc isEnumType*(n: Cursor): bool =
  if n.kind == Symbol:
    let impl = getTypeSection(n.symId)
    result = impl.kind == TypeY and impl.body.typeKind in {EnumT, HoleyEnumT, AnumT}
  else:
    result = false

proc matchConceptSym(m: var Match; conceptSym: SymId; a: Cursor): bool
proc matchConceptBody(m: var Match; conceptSym: SymId; body: Cursor; a: Cursor): bool

type LinearMatchFlag = enum
  ExactBits ## do not normalize bits
  InferActualTypevar ## infer impl typevars from concrete concept types

const ConstraintMatchFlags = {InferActualTypevar}

proc linearMatch(m: var Match; f, a: var Cursor; flags: set[LinearMatchFlag] = {})

proc tryLinearMatch(m: var Match; f, a: var Cursor; flags: set[LinearMatchFlag] = {}): bool {.inline.} =
  let oldErr = m.err
  let oldHasError = m.hasError
  m.err = false
  m.hasError = false
  linearMatch m, f, a, flags
  result = not m.err
  m.err = oldErr
  m.hasError = oldHasError

proc matchesConstraint*(m: var Match; f: var Cursor; a: Cursor): bool

proc matchSymbolConstraint(m: var Match; f: var Cursor; a: Cursor): bool =
  result = false
  let fOrig = f
  let fs = f.symId
  inc f
  let res = tryLoadSym(fs)
  assert res.status == LacksNothing
  var typeImpl = asTypeDecl(res.decl)
  # check if symbol has typeclass behavior:
  if typeImpl.kind == TypeY:
    if typeImpl.body.typeKind == ConceptT:
      return matchConceptSym(m, fs, a)
    if typeImpl.typevars.substructureKind == TypevarsU:
      # matching generic base symbol, acts as typeclass
      # XXX does not consider inheritance
      var inst = a
      if a.kind == Symbol:
        if fs == a.symId:
          return true
        let resa = tryLoadSym(a.symId)
        assert resa.status == LacksNothing
        var aDecl = asTypeDecl(resa.decl)
        if aDecl.typevars.typeKind == InvokeT:
          inst = aDecl.typevars
      if inst.typeKind == InvokeT:
        inc inst
        assert inst.kind == Symbol
        if fs == inst.symId:
          return true
  # otherwise, match symbol as a regular type (includes typevar case):
  # XXX typevars inferred to have typevar values will try to match individual constraints here
  f = fOrig
  var a = a
  # XXX this means conversions are not allowed, i.e. T: cstring cannot match "abc"
  result = tryLinearMatch(m, f, a, ConstraintMatchFlags)

proc matchTypeConstraint(m: var Match; f: var Cursor; a: Cursor): bool =
  result = false
  case f.typeKind
  of ConceptT:
    result = matchConceptBody(m, SymId(0), f, a)
    skip f
  of TypekindT:
    var aTag = a
    if aTag.typeKind == InvokeT:
      inc aTag
    if aTag.kind == Symbol:
      aTag = typeImpl(aTag.symId)
    if aTag.typeKind == TypekindT:
      inc aTag
    f.into:
      assert f.kind == ParLe
      result = aTag.kind == ParLe and f.tagId == aTag.tagId
      skip f # the empty `(tag)` child
  of OrdinalT:
    case a.typeKind
    of OrdinalT:
      result = true
    of TypekindT:
      var aTag = a
      inc aTag
      result = isOrdinalTypeKind(aTag.typeKind)
    else:
      result = isOrdinalType(a)
    skip f
  else:
    # match as a regular type:
    var a = a
    # XXX this means conversions are not allowed, i.e. T: cstring cannot match "abc"
    result = tryLinearMatch(m, f, a, ConstraintMatchFlags)

proc matchSingleConstraint(m: var Match; f: var Cursor; a: Cursor): bool {.inline.} =
  if f.kind == Symbol:
    result = matchSymbolConstraint(m, f, a)
  else:
    result = matchTypeConstraint(m, f, a)

proc matchConstraintSplitAnd(m: var Match; f: var Cursor; a: Cursor): bool =
  if a.typeKind == AndT:
    # an argument with `and` type is not understood by typeclasses
    # consider at least one branch the `and` type can take enough to match the constraint
    # since we need to consider every branch, this has to be done last
    result = false
    var a = a
    # depth-first over the leaves of the nested `and` tree; the scope stack
    # replaces the classic ParRi-counting walk (closes are elided under
    # `-d:virtualParRi`)
    var scopes = @[a]
    a = sub(a)
    while scopes.len > 0:
      if not a.hasMore:
        let h = scopes.pop(); a = h; skip a
      elif a.typeKind == AndT:
        scopes.add a
        a = sub(a)
      else:
        var f2 = f
        # XXX `a` can be an `or` type again here which will not match properly
        # a fix might be to split `a` into sum of products form before matching, i.e.
        # (A or B) and (C or D) becomes (A and C) or (A and D) or (B and C) or (B and D)
        # same for `not`
        result = matchSingleConstraint(m, f2, a)
        if result: break
        skip a
    skip f
  else:
    result = matchSingleConstraint(m, f, a)

proc matchBooleanConstraint(m: var Match; f: var Cursor; a: Cursor): bool =
  result = false
  case f.typeKind
  of AndT:
    f.into:
      result = true
      while f.hasMore:
        var f2 = f
        if not matchBooleanConstraint(m, f2, a):
          result = false
          break
        skip f
      while f.hasMore: skip f                  # consume the rest after early break
  of OrT:
    f.into:
      while f.hasMore:
        var f2 = f
        if matchBooleanConstraint(m, f2, a):
          result = true
          break
        skip f
      while f.hasMore: skip f
  of NotT:
    # XXX handle not/not case somehow
    f.into:
      result = not matchBooleanConstraint(m, f, a)
  else:
    # standalone typeclass
    result = matchConstraintSplitAnd(m, f, a)

proc matchConstraintSplitOr(m: var Match; f: var Cursor; a: Cursor): bool =
  if a.typeKind == OrT:
    # an argument with `or` type is not understood by typeclasses
    # each possible branch the `or` type can take needs to match the constraint independently,
    # so we split it before matching any typeclasses
    result = false
    var a = a
    # same scope-stack walk as in `matchConstraintSplitAnd`
    var scopes = @[a]
    a = sub(a)
    while scopes.len > 0:
      if not a.hasMore:
        let h = scopes.pop(); a = h; skip a
      elif a.typeKind == OrT:
        scopes.add a
        a = sub(a)
      else:
        var f2 = f
        result = matchBooleanConstraint(m, f2, a)
        if not result: break
        skip a
    skip f
  else:
    result = matchBooleanConstraint(m, f, a)

proc matchesConstraintAux(m: var Match; f: var Cursor; a: Cursor): bool =
  if a.typeKind in {OrT, AndT, NotT}:
    # typeclass matching typeclass, might need to be reordered to match properly:
    if isSumOfProducts(a):
      result = matchConstraintSplitOr(m, f, a)
    else:
      var reorderBuf = createTokenBuf(32)
      var a = a
      reorderSumOfProducts(reorderBuf, a)
      var reordered = beginRead(reorderBuf)
      result = matchConstraintSplitOr(m, f, reordered)
  else:
    result = matchBooleanConstraint(m, f, a)

proc matchesConstraint*(m: var Match; f: var Cursor; a: Cursor): bool =
  result = false
  if f.kind == DotToken:
    inc f
    return a.typeKind != AutoT
  if a.kind == Symbol:
    let res = tryLoadSym(a.symId)
    assert res.status == LacksNothing
    if isTypevarLike(res.decl.symKind):
      # for a value parameter its element type stands in for its "type"
      var typevar = asTypevar(res.decl)
      return matchesConstraint(m, f, typevar.typ)
  result = matchesConstraintAux(m, f, a)

proc foldValueExpr(m: var Match; a: Cursor; depth = 0): xint =
  ## The tiny fixed-opcode evaluator for compile-time *values* in type
  ## positions: folds `+ - *` over integer literals and rewrites an
  ## array-index `rangetype` to its length. Anything else — in particular a
  ## still-symbolic expression — yields NaN and is compared syntactically
  ## instead; matching never solves for a value parameter backwards.
  result = createNaN()
  if depth > 10: return
  case a.kind
  of IntLit:
    result = createXint(pool.integers[a.intId])
  of UIntLit:
    result = createXint(pool.uintegers[a.uintId])
  of Symbol:
    # an already-inferred value typevar (e.g. `R` in `array[R * C, T]`): fold to
    # the value it was bound to, so array-length matching resolves once bound.
    if isStaticTypevar(a.symId) and m.inferred.contains(a.symId):
      let inferred = m.inferred.getOrQuit(a.symId)
      result = foldValueExpr(m, inferred, depth+1)
  of ParLe:
    case a.exprKind
    of AddX, SubX, MulX:
      let opc = a.exprKind
      var n = a
      inc n # tag
      if n.typeKind notin {IntT, UIntT}: return
      skip n # type
      let x = foldValueExpr(m, n, depth+1)
      if x.isNaN: return
      skip n
      let y = foldValueExpr(m, n, depth+1)
      if y.isNaN: return
      case opc
      of AddX: result = x + y
      of SubX: result = x - y
      else: result = x * y
    of SufX:
      var n = a
      inc n
      result = foldValueExpr(m, n, depth+1)
    else:
      if a.typeKind == RangetypeT and m.context != nil:
        result = lengthOrd(m.context[], a)
  else:
    discard

proc foldStaticArg(m: var Match; elemType, a: Cursor): Cursor =
  ## Fold `a` to the canonical typed value a value (`static`) parameter should
  ## bind, using the shared `expreval` engine in a mode that never shells out
  ## to a sub-compile (overload resolution must stay in-process and cheap).
  ## `annotateConstantType` re-types the folded value against `elemType`, so an
  ## enum-valued `const` recovers its field symbol instead of collapsing to a
  ## bare ordinal (which would drop the enum type). Returns `default(Cursor)`
  ## when `a` cannot be folded locally or does not match `elemType`.
  result = default(Cursor)
  if m.context == nil: return
  var ec = initEvalContext(m.context, noExecute = true)
  var cur = a
  let folded = eval(ec, cur)
  if folded.kind == ParLe and folded.tagId == nifstreams.ErrT:
    return
  # `folded` lives in a temporary buffer; consume it immediately by re-typing
  # it into a fresh buffer before any other evaluation runs.
  var buf = createTokenBuf(16)
  annotateConstantType(buf, elemType, folded)
  let typed = cursorAt(buf, 0)
  if typed.kind == ParLe and typed.tagId == nifstreams.ErrT:
    return
  result = typeToCursor(m.context[], buf, 0)

proc isEnumFieldSym(a: Cursor): bool =
  if a.kind != Symbol: return false
  let res = tryLoadSym(a.symId)
  result = res.status == LacksNothing and res.decl.symKind == EfldY

proc staticValueToBind(m: var Match; elemType: Cursor; a: Cursor): Cursor =
  ## For a value (`static`) generic parameter: the value to bind from `a`, or
  ## `default(Cursor)` when `a` is not an acceptable argument. An array-index
  ## `rangetype` is rewritten to its length, so `N` binds `3` when `array[N, T]`
  ## is matched against an `array[3, T]`. Binding only happens from such bare
  ## positions; a value is never solved backwards out of arithmetic.
  result = default(Cursor)
  let k = elemType.typeKind
  case a.kind
  of IntLit:
    if k in {IntT, UIntT}: result = a
  of UIntLit:
    if k in {UIntT, IntT}: result = a
  of FloatLit:
    if k == FloatT: result = a
  of CharLit:
    if k == CharT: result = a
  of StringLit:
    if m.context != nil and sameTrees(elemType, m.context.types.stringType):
      result = a
  of Symbol:
    if isStaticTypevar(a.symId):
      # a symbolic value: a value parameter of an enclosing generic; it is
      # compared or substituted later, no value is computed here
      result = a
    elif isEnumFieldSym(a) and isOrdinalType(elemType, allowEnumWithHoles = true):
      # an enum field is already the canonical typed value of its enum type;
      # bind it verbatim (folding to the bare ordinal would lose the type)
      result = a
    else:
      # a `const` (or other foldable symbol): resolve it through the shared
      # expreval engine and bind the value it aliases, exactly as if that value
      # had been written in the argument position.
      result = foldStaticArg(m, elemType, a)
  of ParLe:
    case a.exprKind
    of FalseX, TrueX:
      if k == BoolT: result = a
    of SufX:
      var inner = a
      inc inner
      case inner.kind
      of IntLit, UIntLit:
        if k in {IntT, UIntT}: result = a
      of FloatLit:
        if k == FloatT: result = a
      else: discard
    else:
      if isStaticValue(a) and staticValueTypeMatches(elemType, staticValueType(a)):
        # a typed aggregate constructor (array/set/tuple/object), or an
        # `openArray`/`varargs` value satisfied by an array literal
        result = a
      elif isStaticValue(a) and elemType.typeKind == InvokeT:
        # a *dependent* generic element type such as `Shape[N]`, where an
        # enclosing value parameter `N` parameterizes this parameter's type.
        # Unify the element type against the value's concrete type (`Shape[2]`),
        # which binds or equality-checks the enclosing parameters through the
        # ordinary matcher. See #2108 / issue #2104.
        let vt = staticValueType(a)
        if not cursorIsNil(vt):
          var f = elemType
          var av = vt
          if tryLinearMatch(m, f, av):
            result = a
      elif containsGenericParams(a):
        # a symbolic expression over value parameters, e.g. `N1 + N2`:
        # compared syntactically, never solved
        result = a
      elif k in {IntT, UIntT} and m.context != nil:
        # a concrete value expression (an array-index `rangetype` or fully
        # substituted arithmetic like `2 * 3`): fold it and bind the value
        let v = foldValueExpr(m, a)
        var err = false
        let vv = asSigned(v, err)
        if not (err or v.isNaN):
          var buf = createTokenBuf(2)
          buf.addIntLit(vv, a.info)
          result = typeToCursor(m.context[], buf, 0)
  else:
    discard

proc bindStaticTypevar(m: var Match; fs: SymId; elemType: Cursor; a: Cursor): bool =
  ## Bind or check a value (`static`) generic parameter against the value `a`:
  ## bind from a bare position; a repeated parameter is an equality check.
  let av = staticValueToBind(m, elemType, a)
  if av == default(Cursor):
    return false
  if m.concreteMatch:
    return true
  if m.inferred.contains(fs):
    let prev = m.inferred.getOrQuit(fs)
    if sameTrees(prev, av):
      return true
    # both concrete? then compare the folded values
    let pv = foldValueExpr(m, prev)
    if pv.isNaN: return false
    let av2 = foldValueExpr(m, av)
    return not av2.isNaN and pv == av2
  m.inferred[fs] = av
  return true

proc bindStaticTypevar(m: var Match; fs: SymId; a: Cursor): bool =
  let res = tryLoadSym(fs)
  assert res.status == LacksNothing
  result = bindStaticTypevar(m, fs, asTypevar(res.decl).typ, a)

proc matchesConstraint(m: var Match; f: SymId; a: Cursor): bool =
  let res = tryLoadSym(f)
  assert res.status == LacksNothing
  var typevar = asTypevar(res.decl)
  if typevar.kind == StaticTypevarY:
    # for a value parameter "matching the constraint" means: `a` is an
    # acceptable *value* of the declared element type
    result = staticValueToBind(m, typevar.typ, a) != default(Cursor)
  else:
    assert typevar.kind == TypevarY
    result = matchesConstraint(m, typevar.typ, a)

proc conceptReturnTypesMatch(m: var Match; cRet, aRet: Cursor): bool =
  var c = cRet
  var a = aRet
  if tryLinearMatch(m, c, a, ConstraintMatchFlags):
    return true
  c = cRet
  a = aRet
  if tryLinearMatch(m, a, c, ConstraintMatchFlags):
    return true
  if matchesConstraint(m, c, a):
    return true
  c = cRet
  a = aRet
  if matchesConstraint(m, a, c):
    return true
  if sameTreesButIgnoreSymIds(cRet, aRet):
    return true
  c = cRet
  a = aRet
  if c.kind == ParLe and a.kind == ParLe and c.tagId == a.tagId:
    let kind = c.typeKind
    inc c
    inc a
    skipRoutineDeclPrefix(c, kind)
    skipRoutineDeclPrefix(a, kind)
    return tryLinearMatch(m, c, a, ConstraintMatchFlags)
  false

proc matchConceptParamTypes(m: var Match; conceptTyp, implTyp: Cursor): bool =
  var c = conceptTyp
  var i = implTyp
  if tryLinearMatch(m, c, i, ConstraintMatchFlags):
    return true
  c = conceptTyp
  i = implTyp
  if tryLinearMatch(m, i, c, ConstraintMatchFlags):
    return true
  false

proc matchConceptRoutineSig(m: var Match; conceptR, implR: Cursor): bool =
  if not conceptRoutineKindsCompatible(conceptR.symKind, implR.symKind, implR):
    return false
  var cf = conceptR
  var ca = implR
  skipToParams cf
  skipToParams ca
  if cf.substructureKind != ParamsU or ca.substructureKind != ParamsU:
    return false
  cf.into ParamsU:
    ca.into ParamsU:
      while cf.hasMore and ca.hasMore:
        let cTyp = takeLocal(cf, SkipFinalParRi).typ
        let aTyp = takeLocal(ca, SkipFinalParRi).typ
        if not matchConceptParamTypes(m, cTyp, aTyp):
          return false
      if cf.hasMore:
        return false
      while ca.hasMore:
        let extra = takeLocal(ca, SkipFinalParRi)
        if extra.val.kind == DotToken:
          return false
  # The `into` blocks advanced `cf`/`ca` past the params subtree, so they now
  # sit on the return types — no need to re-skip from the routine head.
  conceptReturnTypesMatch(m, cf, ca)

proc matchConceptSym(m: var Match; conceptSym: SymId; a: Cursor): bool =
  if isConceptType(a):
    if conceptExtends(a.symId, conceptSym):
      return true
  matchConceptBody(m, conceptSym, getTypeSection(conceptSym).body, a)

proc restoreConceptSelfInference(m: var Match; selfSyms: seq[SymId];
                                 savedSelf: openArray[(SymId, Cursor)]) =
  var restored = initHashSet[SymId]()
  for entry in savedSelf:
    let (selfSym, saved) = entry
    m.inferred[selfSym] = saved
    restored.incl selfSym
  for selfSym in selfSyms:
    if selfSym notin restored:
      m.inferred.del(selfSym)

proc conceptRoutineAvailable(m: var Match; conceptSym: SymId; body: Cursor; routine: Cursor; a: Cursor; actualBody: Cursor): bool =
  if m.context == nil:
    return true
  if isConceptType(a):
    return conceptRequirementInBody(routine, actualBody)
  let selfSyms = conceptSelfSyms(body, routine)
  var savedSelf: seq[(SymId, Cursor)] = @[]
  for selfSym in selfSyms:
    if m.inferred.hasKey(selfSym):
      savedSelf.add (selfSym, m.inferred.getOrDefault(selfSym, default(Cursor)))
    m.inferred[selfSym] = a
  let basename = conceptRoutineBasename(routine)
  let inferenceBase = m.inferred
  for cand in conceptRoutineCandidates(m.context, conceptSym, basename):
    let res = tryLoadSym(cand)
    if res.status != LacksNothing:
      continue
    if res.decl.symKind notin RoutineKinds:
      continue
    m.inferred = inferenceBase
    for selfSym in selfSyms:
      m.inferred[selfSym] = a
    let oldErr = m.err
    let oldHasError = m.hasError
    m.err = false
    m.hasError = false
    let sigMatch = matchConceptRoutineSig(m, routine, res.decl)
    m.err = oldErr
    m.hasError = oldHasError
    if sigMatch:
      restoreConceptSelfInference(m, selfSyms, savedSelf)
      return true
  restoreConceptSelfInference(m, selfSyms, savedSelf)
  false

proc collectMissingConceptRequirements(m: var Match; conceptSym: SymId; body: Cursor; a: Cursor): seq[Cursor] =
  let actualIsConcept = isConceptType(a)
  let actualBody = if actualIsConcept: getTypeSection(a.symId).body else: default(Cursor)
  let parents = conceptParentsSlot(body)
  let hasParents = conceptHasParents(parents)
  if hasParents:
    for parent in conceptParentSyms(parents):
      let parentBody = getTypeSection(parent).body
      let parentMissing = collectMissingConceptRequirements(m, parent, parentBody, a)
      if parentMissing.len > 0:
        return parentMissing
  if not actualIsConcept and not hasParents:
    if not conceptTargetNeedsStrictCheck(a):
      return @[]
  result = @[]
  for cbody, routine in conceptHierarchyRoutines(body):
    if not conceptRoutineAvailable(m, conceptSym, cbody, routine, a, actualBody):
      result.add routine

proc collectMissingConceptRequirementsFromConstraint(m: var Match; f: Cursor; a: Cursor): seq[Cursor] =
  var f = f
  var a = a
  if f.kind == DotToken:
    return @[]
  if a.kind == Symbol:
    let res = tryLoadSym(a.symId)
    assert res.status == LacksNothing
    if res.decl.symKind == TypevarY:
      var typevar = asTypevar(res.decl)
      return collectMissingConceptRequirementsFromConstraint(m, f, typevar.typ)
  if f.kind == Symbol:
    if isConceptSym(f.symId):
      let body = getTypeSection(f.symId).body
      return collectMissingConceptRequirements(m, f.symId, body, a)
    let res = tryLoadSym(f.symId)
    if res.status == LacksNothing and res.decl.symKind == TypevarY:
      var typevar = asTypevar(res.decl)
      return collectMissingConceptRequirementsFromConstraint(m, typevar.typ, a)
  if f.typeKind == ConceptT:
    return collectMissingConceptRequirements(m, SymId(0), f, a)
  @[]

proc constraintMismatchMsg*(m: var Match; constraint, arg: Cursor): string =
  result = "type " & typeToString(arg) & " does not match constraint: " & typeToString(constraint)
  let missing = collectMissingConceptRequirementsFromConstraint(m, constraint, arg)
  if missing.len > 0:
    result.add "; missing required "
    if missing.len == 1:
      result.add "proc: "
    else:
      result.add "procs: "
    for i, routine in missing:
      if i > 0:
        result.add ", "
      result.add asNimCode(routine, {renderNoBody})

proc matchConceptBody(m: var Match; conceptSym: SymId; body: Cursor; a: Cursor): bool =
  let actualIsConcept = isConceptType(a)
  let actualBody = if actualIsConcept: getTypeSection(a.symId).body else: default(Cursor)
  let parents = conceptParentsSlot(body)
  let hasParents = conceptHasParents(parents)
  if hasParents:
    for parent in conceptParentSyms(parents):
      if not matchConceptSym(m, parent, a):
        return false
  if not actualIsConcept and not hasParents:
    if not conceptTargetNeedsStrictCheck(a):
      return a.kind != DotToken
  for cbody, routine in conceptHierarchyRoutines(body):
    if not conceptRoutineAvailable(m, conceptSym, cbody, routine, a, actualBody):
      return false
  true

proc isTypevar(s: SymId): bool =
  let res = tryLoadSym(s)
  assert res.status == LacksNothing
  let typevar = asTypevar(res.decl)
  result = isTypevarLike(typevar.kind)

proc cmpTypeBits(context: ptr SemContext; f, a: Cursor): int =
  if (f.kind == IntLit or f.kind == InlineInt) and
     (a.kind == IntLit or a.kind == InlineInt):
    result = typebits(context.g.config, f.load) - typebits(context.g.config, a.load)
  else:
    result = -1

proc cmpExactTypeBits(f, a: Cursor): int =
  # compares type bits without normalizing
  if (f.kind == IntLit or f.kind == InlineInt) and
     (a.kind == IntLit or a.kind == InlineInt):
    result = typebits(f.load) - typebits(a.load)
  else:
    result = -1

proc sameSymbol(a, b: SymId): bool =
  if a == b:
    return true
  # symbols might be different for instantiations from different modules,
  # consider this case by checking if the instantiation keys are equal:
  let sa = pool.syms[a]
  let sb = pool.syms[b]
  result = isInstantiation(sa) and isInstantiation(sb) and
    removeModule(sa) == removeModule(sb)

proc expectParRi(m: var Match; f: var Cursor; start: Cursor) =
  ## Closes a type-tree scope opened via `sub`: the tree must be
  ## fully consumed, else the match errors (`m.err` doubles as the
  ## mismatch signal). On the error path `f` stays mid-tree — callers
  ## either bail on `m.err` or use a saved original.
  if f.kind == ParRi:
    f = start; skip f
  else:
    m.error FormalTypeNotAtEndBug, f, f

proc expectPtrParRi(m: var Match; f: var Cursor; start: Cursor) =
  if f.hasMore: skip f # skip nil/not nil annotation
  # Inlined importc'd pointer aliases drag importc/header attrs into the
  # type body — they are bookkeeping, not part of type identity.
  while f.pragmaKind in {ImportcP, ImportcppP, HeaderP}:
    skip f
  if f.kind == ParRi:
    f = start; skip f
  else:
    m.error FormalTypeNotAtEndBug, f, f

proc matchNilAnnotations(m: var Match; f, a: var Cursor; fOrig, aOrig: Cursor) =
  ## Match nil annotations on pointer-like types. `(unchecked)` is compatible
  ## with any other annotation (or none). `(notnil)` and `(nil)` must match exactly.
  let fHas = isNilAnnotation(f)
  let aHas = isNilAnnotation(a)
  if fHas and aHas:
    if f.substructureKind == UncheckedU or a.substructureKind == UncheckedU:
      # unchecked is compatible with anything
      skip f
      skip a
    elif f.substructureKind == a.substructureKind:
      skip f
      skip a
    else:
      m.error(InvalidMatch, fOrig, aOrig)
  elif fHas:
    if f.substructureKind == UncheckedU:
      skip f # unchecked is compatible with no annotation
    else:
      m.error(InvalidMatch, fOrig, aOrig)
  elif aHas:
    if a.substructureKind == UncheckedU:
      skip a # unchecked is compatible with no annotation
    else:
      m.error(InvalidMatch, fOrig, aOrig)

proc procTypeMatch(m: var Match; f, a: var Cursor)

proc rematchInferredTypevar(m: var Match; fs: SymId; prev: Cursor;
                            f, a: var Cursor; fOrig, aOrig: Cursor;
                            flags: set[LinearMatchFlag] = {}) =
  ## Rematch a later argument against a type variable already inferred
  ## from an earlier parameter. A scalar typevar binding (e.g. `T` from
  ## `Complex[T]`) must not be widened via concept constraints to accept
  ## a generic constructor over the same variable (`Complex[T]`).
  let pv = foldValueExpr(m, prev)
  if not pv.isNaN:
    # the previous binding is a concrete compile-time *value* (e.g. a
    # substituted `2 * 2` recorded for an explicit instantiation): compare
    # folded values, so it also matches the already folded index type `0..3`
    let av = foldValueExpr(m, a)
    if not av.isNaN:
      if pv == av:
        inc f
        skip a
      else:
        m.errorTypevar InvalidRematch, prev, a, fs
      return
  if prev.kind == Symbol and isTypevar(prev.symId) and a.typeKind == InvokeT:
    m.errorTypevar InvalidRematch, prev, a, fs
  elif prev.kind == Symbol and isTypevar(prev.symId) and sameTrees(prev, a):
    inc f
    skip a
  else:
    m.concreteMatch = true
    var prev2 = prev
    linearMatch(m, prev2, a, flags)
    m.concreteMatch = false
    inc f

proc linearMatchTree(m: var Match; f, a: var Cursor; fOrig, aOrig: Cursor;
                     flags: set[LinearMatchFlag]) =
  ## Matches a single tree/token of `f` against `a`, advancing both past it.
  ## On mismatch `m.err` is set and the cursors may be left mid-tree —
  ## `linearMatch` restores them from the saved originals. `fOrig`/`aOrig`
  ## are the whole trees of the enclosing `linearMatch`, used for error
  ## reporting exactly like the classic token-wise loop did.
  if f.kind == Symbol and isTypevar(f.symId):
    # type vars are specal:
    let fs = f.symId
    if isStaticTypevar(fs):
      # a value parameter: bind from a bare position; a repeated
      # parameter is an equality check
      if bindStaticTypevar(m, fs, a):
        inc f
        skip a
      else:
        m.error(ConstraintMismatch, f, a)
    elif m.concreteMatch:
      # generic param is from provided argument type
      # instead of considering inference, treat as a standalone value
      if matchesConstraint(m, fs, a):
        inc f
        skip a
      else:
        m.error(ConstraintMismatch, f, a)
    elif m.inferred.contains(fs):
      var prev = m.inferred.getOrQuit(fs)
      rematchInferredTypevar(m, fs, prev, f, a, fOrig, aOrig, flags)
    elif matchesConstraint(m, fs, a):
      m.inferred[fs] = a # NOTICE: Can introduce modifiers for a type var!
      inc f
      skip a
    else:
      m.error(ConstraintMismatch, f, a)
  elif InferActualTypevar in flags and a.kind == Symbol and isTypevar(a.symId):
    let aSym = a.symId
    if m.concreteMatch:
      if matchesConstraint(m, aSym, f):
        inc a
        skip f
      else:
        m.error(ConstraintMismatch, f, a)
    elif m.inferred.contains(aSym):
      var prev = m.inferred.getOrQuit(aSym)
      m.concreteMatch = true
      linearMatch(m, f, prev, flags)
      m.concreteMatch = false
      inc a
    elif matchesConstraint(m, aSym, f):
      m.inferred[aSym] = f
      skip f
      inc a
    else:
      m.error(ConstraintMismatch, f, a)
  elif f.kind == a.kind:
    case f.kind
    of UnknownToken, EofToken,
        DotToken, Ident, SymbolDef,
        StringLit, CharLit, IntLit, UIntLit, FloatLit:
      if f.uoperand != a.uoperand:
        m.error(InvalidMatch, fOrig, aOrig)
      else:
        inc f
        inc a
    of Symbol:
      if not sameSymbol(f.symId, a.symId):
        m.error(InvalidMatch, fOrig, aOrig)
      else:
        inc f
        inc a
    of ParLe:
      # special cases:
      case f.typeKind
      of RoutineTypes:
        if a.typeKind notin RoutineTypes:
          m.error(InvalidMatch, fOrig, aOrig)
        else:
          var a2 = a # since procTypeMatch does not skip it properly
          procTypeMatch m, f, a2
          skip a # XXX consider when a is (params)
      of IntT, UIntT, FloatT, CharT:
        if a.typeKind != f.typeKind:
          m.error(InvalidMatch, fOrig, aOrig)
        else:
          let fStart = f
          f = sub(f)
          let aStart = a
          a = sub(a)
          let bitsDiffer =
            if ExactBits in flags: cmpExactTypeBits(f, a) != 0
            else: cmpTypeBits(m.context, f, a) != 0
          if bitsDiffer:
            m.error(InvalidMatch, fOrig, aOrig)
          else:
            skip f
            skip a
            # importc part
            while f.pragmaKind in {ImportcP, ImportcppP, HeaderP}:
              skip f
            while a.pragmaKind in {ImportcP, ImportcppP, HeaderP}:
              skip a
            expectParRi m, f, fStart
            expectParRi m, a, aStart
      of CstringT, PointerT:
        if a.typeKind != f.typeKind:
          m.error(InvalidMatch, fOrig, aOrig)
        else:
          let fStart = f
          f = sub(f)
          let aStart = a
          a = sub(a)
          matchNilAnnotations m, f, a, fOrig, aOrig
          # importc/header attrs are an inlined-alias bookkeeping detail,
          # not part of type identity — ignore them on both sides.
          while f.pragmaKind in {ImportcP, ImportcppP, HeaderP}:
            skip f
          while a.pragmaKind in {ImportcP, ImportcppP, HeaderP}:
            skip a
          expectParRi m, f, fStart
          expectParRi m, a, aStart
      of PtrT, RefT:
        if a.typeKind != f.typeKind:
          m.error(InvalidMatch, fOrig, aOrig)
        else:
          let fStart = f
          f = sub(f)
          let aStart = a
          a = sub(a)
          linearMatch m, f, a # match base type
          matchNilAnnotations m, f, a, fOrig, aOrig
          expectParRi m, f, fStart
          expectParRi m, a, aStart
      else:
        # generic tree: same tag, then match the children pairwise.
        # Compare tag ids, not raw operands — under `-d:virtualParRi` the
        # operand carries the sealed jump, which differs whenever the two
        # subtrees have different token counts (e.g. typevar vs. concrete).
        if f.tagId != a.tagId:
          m.error(InvalidMatch, fOrig, aOrig)
        else:
          let fStart = f
          f = sub(f)
          let aStart = a
          a = sub(a)
          while f.hasMore and a.hasMore and not m.err:
            linearMatchTree m, f, a, fOrig, aOrig, flags
          if not m.err:
            if f.kind == ParRi and a.kind == ParRi:
              f = fStart; skip f
              a = aStart; skip a
            else:
              # different child counts
              m.error(InvalidMatch, fOrig, aOrig)
    of ParRi:
      # unreachable: subtree ends are consumed by the bounded scopes above
      m.error(InvalidMatch, fOrig, aOrig)
  elif f.typeKind == InvokeT and a.kind == Symbol:
    # Keep in mind that (invok GenericHead Type1 Type2 ...)
    # is tyGenericInvokation in the old Nim. A generic *instance*
    # is always a nominal type ("Symbol") like
    # `(type GeneratedName (invok MyInst ConcreteTypeA ConcreteTypeB) (object ...))`.
    # This means a Symbol can match an InvokT.
    var t = getTypeSection(a.symId)
    if t.kind == TypeY and t.typevars.typeKind == InvokeT:
      linearMatch m, f, t.typevars, flags # skips f
      inc a
    else:
      m.error(InvalidMatch, fOrig, aOrig)
  else:
    m.error(InvalidMatch, fOrig, aOrig)

proc linearMatch(m: var Match; f, a: var Cursor; flags: set[LinearMatchFlag] = {}) =
  let fOrig = f
  let aOrig = a
  linearMatchTree m, f, a, fOrig, aOrig, flags
  if m.err:
    # a mismatch (or an error latched before this call) may have left the
    # cursors mid-tree: resynchronize by skipping both whole trees. For a
    # successful match this lands on the exact same positions the match
    # produced, so it is safe when `m.err` was already set on entry.
    f = fOrig
    a = aOrig
    skip f
    skip a

type
  ProcProperties* = object
    cc*: CallConv
    usesRaises*: bool
    raisesType*: Cursor  # The actual exception type from .raises pragma
    usesClosure*: bool
    usesPassive*: bool

proc extractProcProps*(c: var Cursor): ProcProperties =
  result = ProcProperties(cc: Nimcall, usesRaises: false, usesClosure: false, usesPassive: false)
  if c.substructureKind == PragmasU:
    c.into:
      while c.hasMore:
        let res = callConvKind(c)
        if res != NoCallConv:
          result.cc = res
        elif c.pragmaKind == RaisesP:
          result.usesRaises = true
          # Extract the raises type from the pragma
          var raisesNode = c
          raisesNode = sub(raisesNode) # bounds it, so `hasMore` is exact
          if raisesNode.hasMore:
            result.raisesType = raisesNode
        elif c.pragmaKind == ClosureP:
          result.usesClosure = true
        elif c.pragmaKind == PassiveP:
          result.usesPassive = true
        skip c
  elif c.kind == DotToken:
    inc c
  else:
    bug "No pragmas found"

proc skipRoutinePrefix*(n: var Cursor; kind: TypeKind) =
  ## Skips from a routine type's first child (just inside the opening tag)
  ## to its params slot; mirrors `skipToParams` sans the scope entry.
  if kind in {ProctypeT, ItertypeT}:
    skip n # nilability tag
  else:
    skip n # name
    skip n # export marker
    skip n # pattern
    skip n # generics

proc procTypeMatch(m: var Match; f, a: var Cursor) =
  ## Matches two routine types structurally. `f` ends up past its whole
  ## type tree (also on the error paths); `a` is left just past its pragmas
  ## slot, still inside its tree — every caller works on a copy of `a`.
  assert f.typeKind in RoutineTypes
  let fKind = f.typeKind
  let fIsProctype = fKind in {ProctypeT, ItertypeT}
  let fStart = f
  f = sub(f)
  skipRoutinePrefix f, fKind
  assert a.typeKind in RoutineTypes
  let aKind = a.typeKind
  a = sub(a)
  skipRoutinePrefix a, aKind
  var hasParams = 0
  let fHasParamsList = f.substructureKind == ParamsU
  let aHasParamsList = a.substructureKind == ParamsU
  var fps = default(Cursor)
  var aps = default(Cursor)
  if fHasParamsList:
    fps = f
    f = sub(f)
    if f.hasMore: inc hasParams
  if aHasParamsList:
    aps = a
    a = sub(a)
    if a.hasMore: inc hasParams, 2
  if hasParams == 3:
    while f.hasMore and a.hasMore:
      var fParam = takeLocal(f, SkipFinalParRi)
      var aParam = takeLocal(a, SkipFinalParRi)
      assert fParam.kind == ParamY
      assert aParam.kind == ParamY
      linearMatch m, fParam.typ, aParam.typ
    if f.kind == ParRi:
      if a.kind == ParRi:
        discard "ok"
      else:
        m.error FormalParamsMismatch, f, a
        skipUntilEnd a
    else:
      m.error FormalParamsMismatch, f, a
      skipUntilEnd f
      skipUntilEnd a
  elif hasParams == 2:
    m.error FormalParamsMismatch, f, a
    skipUntilEnd a
  elif hasParams == 1:
    m.error FormalParamsMismatch, f, a
    skipUntilEnd f

  # close the params scopes; a DotToken params slot is just skipped:
  if fHasParamsList: f = fps; skip f
  else: inc f
  if aHasParamsList: a = aps; skip a
  else: inc a

  # match return types:
  let fret = typeKind f
  let aret = typeKind a
  if fret == aret and fret == VoidT:
    skip f
    skip a
  else:
    linearMatch m, f, a
  # match calling conventions:
  let fcc = extractProcProps(f)
  let acc = extractProcProps(a)
  if fcc.cc != acc.cc:
    m.error CallConvMismatch, f, a
  elif fcc.usesRaises != acc.usesRaises:
    m.error RaisesMismatch, f, a
  elif (fcc.usesClosure != acc.usesClosure) and (not fcc.usesClosure or acc.cc != Nimcall):
    m.error ClosureMismatch, f, a
  elif fcc.usesPassive != acc.usesPassive:
    m.error PassiveMismatch, f, a
  if not m.err and fcc.usesClosure and not acc.usesClosure:
    m.args.addParLe ToClosureX, m.argInfo
    inc m.opened
    inc m.convCosts
  # XXX consider when f or a is (params):
  if not fIsProctype:
    skip f, SkipEffects # effects
    skip f, SkipBody # body
    #skip a # effects
    #skip a # body
  expectParRi m, f, fStart
  # `a` is deliberately left inside its tree (see the contract above)

proc commonType(f, a: Cursor): Cursor =
  # XXX Refine
  result = a


proc useArg(m: var Match; arg: CallArg; f: Cursor) =
  if f.typeKind == UntypedT and not cursorIsNil(arg.orig):
    # pass arg tree before semchecking to untyped args:
    m.args.addSubtree arg.orig
  else:
    m.args.addSubtree arg.n

proc singleArgImpl(m: var Match; f: var Cursor; arg: CallArg)
proc singleArg(m: var Match; f: var Cursor; arg: CallArg)
proc isEmptyContainer*(n: Cursor): bool

proc matchObjectInheritance*(m: var Match; f, a: Cursor; fsym, asym: SymId; ptrKind: TypeKind) =
  let fbase = skipTypeInstSym(fsym)
  var diff = 1
  var objbody = objtypeImpl(asym)
  while true:
    let od = asObjectDecl(objbody)
    if od.kind != ObjectT:
      m.error InvalidMatch, f, a
      return
    var parent = od.parentType
    if parent.typeKind in {RefT, PtrT}:
      inc parent

    var psym = SymId(0)
    var pbase = SymId(0)
    if parent.typeKind == InvokeT:
      var base = parent
      inc base
      psym = base.symId
      pbase = psym
    elif parent.kind == Symbol:
      psym = parent.symId
      pbase = skipTypeInstSym(psym)
    else:
      break

    if sameSymbol(fbase, pbase):
      if f.typeKind == InvokeT:
        # infer generic params
        # XXX might need to use something like `bindSubsInvokeArgs` here
        # if `parent` contains generic parameters of the object type
        var f2 = f
        var p2 = parent
        linearMatch m, f2, p2

      m.args.addParLe BaseobjX, m.argInfo
      if m.flipped:
        if ptrKind != NoType: m.args.addParLe(ptrKind, a.info)
        m.args.addSubtree a
        if ptrKind != NoType: m.args.addParRi()
        m.args.addIntLit -diff, m.argInfo
        dec m.inheritanceCosts, diff
      else:
        if ptrKind != NoType: m.args.addParLe(ptrKind, f.info)
        m.args.addSubtree f
        if containsGenericParams(f):
          # needs to be instantiated, reuse genericConverter
          m.genericConverter = true
        if ptrKind != NoType: m.args.addParRi()
        m.args.addIntLit diff, m.argInfo
        inc m.inheritanceCosts, diff
      inc m.opened
      diff = 0 # mark as success
      break
    inc diff
    objbody = objtypeImpl(psym)
  if diff != 0:
    m.error InvalidMatch, f, a
  elif m.skippedMod == OutT:
    m.error UnavailableSubtypeRelation, f, a

proc matchObjectTypes(m: var Match; f: var Cursor, a: Cursor; ptrKind: TypeKind) =
  if f.kind == Symbol:
    # consider object sym as instantiated, can only match another object sym
    # (generic base sym case handled in `matchesConstraint`)
    if a.kind != Symbol:
      m.error InvalidMatch, f, a
    elif sameSymbol(f.symId, a.symId):
      discard "direct match, no annotation required"
    elif not isObjectType(a.symId):
      m.error InvalidMatch, f, a
    else:
      matchObjectInheritance m, f, a, f.symId, a.symId, ptrKind
    inc f
  elif f.typeKind == InvokeT:
    # check if the types are compatible first before checking for inheritance
    var aInvoke = a
    if a.kind == Symbol:
      let ad = getTypeSection(a.symId)
      if ad.kind == TypeY and ad.typevars.typeKind == InvokeT:
        aInvoke = ad.typevars
    var fBase = f
    inc fBase
    if aInvoke.typeKind == InvokeT:
      var aBase = aInvoke
      inc aBase
      if sameSymbol(fBase.symId, aBase.symId):
        linearMatch m, f, aInvoke
      else:
        let fsym = fBase.symId
        let asym = if a.kind == Symbol: a.symId else: aBase.symId
        matchObjectInheritance m, f, a, fsym, asym, ptrKind
        skip f
    else:
      # already checked that this is an object type
      assert a.kind == Symbol
      let fsym = fBase.symId
      let asym = a.symId
      matchObjectInheritance m, f, a, fsym, asym, ptrKind
      skip f

proc tryMatchEnumChoice*(choice: Cursor; enumTypeSym: SymId): SymId =
  ## Try to find a unique enum field in the OchoiceX that matches the given enum type.
  result = SymId(0)
  var matchCount = 0
  var a = choice.firstSon
  while a.hasMore:
    if a.kind == Symbol:
      let res = tryLoadSym(a.symId)
      if res.status == LacksNothing and res.decl.symKind == EfldY:
        let fieldType = asLocal(res.decl).typ
        if fieldType.kind == Symbol and sameSymbol(fieldType.symId, enumTypeSym):
          result = a.symId
          inc matchCount
    inc a
  if matchCount != 1:
    result = SymId(0)

proc procTypeOfRoutineSym(sym: SymId; buf: var TokenBuf): bool =
  ## Build the structural proc type of a routine symbol into `buf` so it can be
  ## compared against a formal proc type with `procTypeMatch`.
  let res = tryLoadSym(sym)
  result = false
  if res.status == LacksNothing and res.decl.symKind in RoutineKinds:
    let r = asRoutine(res.decl)
    buf.addParLe ProctypeT
    buf.addDotToken() # nilability tag
    buf.addSubtree r.params
    buf.addSubtree r.retType
    buf.addSubtree r.pragmas
    buf.addParRi()
    result = true

proc tryMatchProcChoice*(context: ptr SemContext; choice, f: Cursor): SymId =
  ## Find the unique overload in the OchoiceX/CchoiceX `choice` whose proc type
  ## matches the formal proc type `f`, returning SymId(0) if zero or more than
  ## one candidate matches. Resolves an overloaded routine passed where a `proc`
  ## type is expected (nim-lang/nimony#1973).
  result = SymId(0)
  var matchCount = 0
  var a = choice.firstSon
  while a.hasMore:
    if a.kind == Symbol:
      var buf = createTokenBuf(16)
      if procTypeOfRoutineSym(a.symId, buf):
        var ac = beginRead(buf)
        if ac.typeKind in RoutineTypes:
          var trial = createMatch(context)
          var fc = f
          procTypeMatch(trial, fc, ac)
          if not trial.err:
            result = a.symId
            inc matchCount
    skip a
  if matchCount != 1:
    result = SymId(0)

proc matchSymbol(m: var Match; f: Cursor; arg: CallArg) =
  let a = skipModifier(arg.typ)
  let fs = f.symId
  if isTypevar(fs):
    if isStaticTypevar(fs):
      # a value parameter is not a type; it cannot be a parameter's type
      m.error InvalidMatch, f, a
    elif m.concreteMatch:
      # generic param is from provided argument type
      # instead of considering inference, treat as a standalone value
      if not matchesConstraint(m, fs, a):
        m.error ConstraintMismatch, f, a
    elif m.inferred.contains(fs):
      var prev = m.inferred.getOrQuit(fs)
      if prev.kind == Symbol and isTypevar(prev.symId) and a.typeKind == InvokeT:
        m.errorTypevar InvalidRematch, prev, a, fs
      elif prev.kind == Symbol and isTypevar(prev.symId) and sameTrees(prev, a):
        discard "same typevar binding"
      else:
        m.concreteMatch = true
        singleArgImpl(m, prev, arg)
        m.concreteMatch = false
    elif m.ignoreConstraints or matchesConstraint(m, fs, a):
      # `ignoreConstraints` is set only by `mutualGenericMatch`'s crosswise
      # specificity probe: there the candidates already matched the real call,
      # so we measure which formal is more specialized without re-validating
      # the constraint (a concrete `int` arg need not satisfy `T`'s concept).
      m.inferred[fs] = a
    else:
      m.error ConstraintMismatch, f, a
  elif isObjectType(fs):
    var f = f
    matchObjectTypes m, f, a, NoType
  elif isConceptSym(fs):
    if not matchConceptSym(m, fs, a):
      m.error InvalidMatch, f, a
  else:
    # fast check that works for aliases too:
    if a.kind == Symbol and sameSymbol(a.symId, fs):
      discard "perfect match"
    elif (let fdecl = tryLoadSym(fs);
          fdecl.status != LacksNothing or fdecl.decl.stmtKind != TypeS):
      # `fs` is not a type declaration — e.g. a template parameter (`symKind ==
      # ParamY`) left in type position while a template body is semchecked
      # generically, before instantiation (reached via the concreteMatch rematch
      # path; the argument is typically already an error type). `typeImpl` would
      # assert here. Accept it as a wildcard rather than crash: the real type
      # check happens when the template is instantiated with concrete arguments.
      discard "non-type placeholder (e.g. template param as type) — defer to instantiation"
    else:
      var impl = typeImpl(fs)
      if impl.kind == ParLe and impl.tagId == nifstreams.ErrT:
        m.error InvalidMatch, f, a
      else:
        if impl.typeKind == DistinctT:
          m.error InvalidMatch, f, a
        elif impl.typeKind in {EnumT, HoleyEnumT, AnumT}:
          if arg.n.exprKind == OchoiceX:
            let matchedSym = tryMatchEnumChoice(arg.n, fs)
            if matchedSym != SymId(0):
              m.refineArgType = true
              m.args.addParLe HconvX, m.argInfo
              m.args.addSubtree f
              inc m.opened
              return
          m.error InvalidMatch, f, a
        else:
          singleArgImpl(m, impl, arg)

proc isPlatformNumeric(context: ptr SemContext; kind: TypeKind; bits: Cursor): bool =
  let plat = case kind
  of IntT: context.types.intType
  of UIntT: context.types.uintType
  of FloatT: context.types.floatType
  else: return false
  var p = plat
  inc p
  cmpTypeBits(context, bits, p) == 0

proc incIntegralWidenCost(m: var Match; kind: TypeKind; bits: Cursor; intLit = false, floatLit = false) =
  if intLit and kind in {IntT, UIntT, FloatT}:
    inc m.intLitCosts
  elif floatLit and kind == FloatT:
    inc m.intLitCosts
  elif isPlatformNumeric(m.context, kind, bits):
    inc m.intConvCosts
  else:
    inc m.convCosts

proc checkIntLitRange(context: ptr SemContext; f: Cursor; intLit: Cursor): bool =
  if f.typeKind == FloatT:
    result = true
  else:
    let i = createXint(pool.integers[intLit.intId])
    result = i >= firstOrd(context[], f) and i <= lastOrd(context[], f)

proc checkFloatLitRange(context: ptr SemContext; f: Cursor; floatLit: Cursor): bool =
  if f.typeKind in {IntT, UIntT}:
    result = false
  else:
    var f = f
    inc f # skip to size
    let bits = typebits(f.load)
    if bits == 32:
      let val = pool.floats[floatLit.floatId]
      result = val == val.float32.float64
    else:
      result = true

proc skipExpr*(n: Cursor): Cursor =
  result = n
  while result.exprKind in {ExprX, ParX}:
    result = sub(result) # bound the last-son scan below
    var next = result
    while next.hasMore:
      result = next
      skip next

proc matchIntegralType(m: var Match; f: var Cursor; arg: CallArg) =
  var a = skipModifier(arg.typ)
  if a.typeKind == RangetypeT:
    inc a # skip to base type
  let ex = skipExpr(arg.n)
  let isIntLit = f.typeKind != CharT and
    ex.kind == IntLit and sameTrees(a, m.context.types.intType)
  let isFloatLit = f.typeKind != CharT and
    ex.kind == FloatLit and sameTrees(a, m.context.types.floatType)
  let sameKind = f.tag == a.tag
  if sameKind or isIntLit or isFloatLit:
    inc a
  else:
    m.error InvalidMatch, f, a
    return
  let forig = f
  let fStart = f
  f = sub(f)
  let cmp = cmpTypeBits(m.context, f, a)
  # With `.feature: "lenientFloats".` a wider float *value* (not just a literal)
  # may be narrowed to a smaller float formal, e.g. a `float64` constant passed
  # to a `float32` parameter (nim-lang/nimony#1899). Float-only and opt-in,
  # since the narrowing can silently lose precision.
  let lenientFloat = sameKind and forig.typeKind == FloatT and cmp < 0 and
    m.context != nil and LenientFloatsFeature in m.context.features
  if cmp == 0 and sameKind:
    discard "same types"
  elif cmp > 0 or lenientFloat or
      (isIntLit and checkIntLitRange(m.context, forig, ex)) or
      (isFloatLit and checkFloatLitRange(m.context, forig, ex)):
    # f has more bits than a, great!
    if m.skippedMod in {MutT, OutT}:
      m.error ImplicitConversionNotMutable, forig, forig
    else:
      m.args.addParLe HconvX, m.argInfo
      m.args.addSubtree forig
      incIntegralWidenCost m, forig.typeKind, f, isIntLit, isFloatLit
      inc m.opened
  else:
    m.error InvalidMatch, f, a
  if f.hasMore: inc f # past the bits
  while f.pragmaKind in {ImportcP, ImportcppP, HeaderP}:
    skip f
  expectParRi m, f, fStart

proc matchArrayType(m: var Match; f: var Cursor; a: var Cursor) =
  if a.typeKind == ArrayT:
    var a1 = a
    var f1 = f
    inc a1
    inc f1
    skip a1
    skip f1
    # fold already-bound value typevars first (`array[R * C, T]`), falling back
    # to the plain array-length ordinal for concrete/rangetype lengths.
    var fLen = foldValueExpr(m, f1)
    if fLen.isNaN:
      fLen = lengthOrd(m.context[], f1)
    var aLen = foldValueExpr(m, a1)
    if aLen.isNaN:
      aLen = lengthOrd(m.context[], a1)
    if fLen.isNaN or aLen.isNaN:
      # match typevars
      linearMatch m, f, a
    elif fLen == aLen:
      let fStart = f
      f = sub(f)
      inc a
      linearMatch m, f, a # element types
      skip f # index type
      expectParRi m, f, fStart
    else:
      m.error InvalidMatch, f, a
  else:
    m.error InvalidMatch, f, a

proc tryTypeSymbolBase(a: var Cursor): bool =
  # returns false if non-type symbol declaration was found
  result = false
  var i = 0
  while a.kind == Symbol:
    let decl = getTypeSection(a.symId)
    if decl.kind == TypeY:
      if decl.body.kind == Symbol:
        a = decl.body
      else:
        a = decl.typevars
    else:
      return false
    inc i
    if i == 20: break
  result = true

proc isSomeSeqType*(a: Cursor, elemType: var Cursor): bool =
  # check that `a` is either an instantiation of seq or an invocation to it
  result = false
  var a = a
  if not tryTypeSymbolBase(a):
    return false
  if a.typeKind == InvokeT:
    inc a # tag
    result = a.kind == Symbol and pool.syms[a.symId] == "seq.0." & SystemModuleSuffix
    if result:
      inc a
      elemType = a

proc isSomeSeqType*(a: Cursor): bool {.inline.} =
  var dummy = default(Cursor)
  result = isSomeSeqType(a, dummy)

proc isSomeOpenArrayType*(a: Cursor, elemType: var Cursor): bool =
  # check that `a` is either an instantiation of openArray or an invocation to it
  result = false
  var a = a
  if not tryTypeSymbolBase(a):
    return false
  if a.typeKind == InvokeT:
    inc a # tag
    result = a.kind == Symbol and pool.syms[a.symId] == "openArray.0." & SystemModuleSuffix
    if result:
      inc a
      elemType = a

proc isSomeOpenArrayType*(a: Cursor): bool {.inline.} =
  var dummy = default(Cursor)
  result = isSomeOpenArrayType(a, dummy)

proc getTupleFieldTypeSkipTypedesc(c: Cursor): Cursor =
  result = getTupleFieldType(c)
  if result.typeKind == TypedescT:
    inc result

proc isMutableLvalue(n: Cursor): bool =
  ## True for expressions that resolve to a mutable storage location, i.e.
  ## that can be passed to a `var T` / `out T` parameter. Mirrors old Nim's
  ## `isLValue` (assignable lvalue): VarY-class symbols and paths through
  ## them, params declared `var`/`out`/`lent`, and paths rooted at a call
  ## returning `var T` / `lent T`. Excludes `let`/`const` (addressable but
  ## not assignable). Local to sigmatch — derefs.nim's `isAddressable` is
  ## too permissive (accepts LetY) and lives behind a circular import.
  result = false
  var n = n
  while true:
    case n.exprKind
    of DotX, AtX, ArratX, TupatX, ParX, PatX, DdotX:
      inc n
    of DconvX:
      inc n
      skip n # type
    of BaseobjX:
      inc n
      skip n # intlit
      skip n # type
    of CallKinds:
      inc n # past CallKinds tag
      if n.kind != Symbol: return false
      let r = tryLoadSym(n.symId)
      if r.status != LacksNothing: return false
      var decl = r.decl
      if decl.typeKind notin RoutineTypes: return false
      skipToReturnType decl
      return decl.typeKind in {MutT, LentT}
    else: break
  if n.kind notin {Symbol, SymbolDef}:
    return false
  let res = tryLoadSym(n.symId)
  if res.status != LacksNothing: return false
  let local = asLocal(res.decl)
  case local.kind
  of VarY, GvarY, TvarY, ResultY, CursorY, PatternvarY:
    result = true
  of ParamY:
    # plain-`T` params are immutable copies; `var`/`out`/`lent` aren't
    result = local.typ.typeKind in {MutT, OutT, LentT}
  else:
    result = false

proc singleArgImpl(m: var Match; f: var Cursor; arg: CallArg) =
  case f.kind
  of Symbol:
    matchSymbol m, f, arg
    inc f
  of ParLe:
    let fk = f.typeKind
    case fk
    of MutT, OutT, SinkT, LentT:
      var a = arg.typ
      if a.typeKind in {MutT, OutT, SinkT, LentT}:
        inc a
      else:
        m.skippedMod = f.typeKind
        if fk in {MutT, OutT} and m.context != nil and
            VarToverloadsFeature in m.context.features and
            not isMutableLvalue(arg.n):
          # Mirror old Nim's `kVarNeeded`: a `var T`/`out T` formal cannot
          # consume a non-lvalue arg. Without this rejection the new
          # `mutualGenericMatch` tiebreaker would always pick the `var T`
          # overload, leaving the immutable case for a later pass to
          # diagnose.
          m.error VarNeeded, f, arg.typ
      let fStart = f
      f = sub(f)
      singleArgImpl m, f, CallArg(n: arg.n, typ: a, orig: arg.orig)
      expectParRi m, f, fStart
    of IntT, UIntT, FloatT, CharT:
      matchIntegralType m, f, arg # consumes the whole tree, incl. the close
    of BoolT:
      var a = skipModifier(arg.typ)
      if a.typeKind != fk:
        m.error InvalidMatch, f, a
      let fStart = f
      f = sub(f)
      expectParRi m, f, fStart
    of InvokeT:
      var a = skipModifier(arg.typ)
      if isObjectType(f) and isObjectType(a):
        # specialized to handle inheritance
        matchObjectTypes m, f, a, NoType
      elif a.typeKind == VarargsT and isSomeOpenArrayType(f):
        # `varargs[U]` argument satisfies an `openArray[T]` formal — Nim 2
        # treats them as the same family at the body-iteration layer
        # (same iterator, same `[]`, same `len`). Bind the openArray's
        # type variable to the varargs element type.
        var aElem = a
        aElem = sub(aElem) # bounded: `kind` is ParRi at a bare `(varargs)`
        if aElem.kind == ParRi:
          # bare `(varargs)` has no element type — can't satisfy openArray[T]
          m.error InvalidMatch, f, a
          skip f
        else:
          var fHead = f
          inc fHead   # past `(invoke`
          inc fHead   # past openArray symbol → at element T
          linearMatch m, fHead, aElem
          skip f       # advance the outer cursor past the whole invoke
      else:
        # handled in linearMatch
        linearMatch m, f, a
    of RangetypeT:
      # A `range[lo..hi]` matches structurally on its *base type* only; whether
      # a value actually fits the bounds (and whether one range is a subset of
      # another) is a proof obligation discharged later by the contracts engine
      # (see `checkRangeAssign` in contracts_njvl.nim), not a type-match failure
      # here. This deliberately accepts legal narrowings such as
      # `range[2..5]` -> `range[0..10]` that an exact-tree match would reject.
      var a = skipModifier(arg.typ)
      if a.typeKind == RangetypeT:
        var fb = f
        var ab = a
        inc fb # -> formal base type
        inc ab # -> arg base type
        linearMatch m, fb, ab # base types must be compatible
        skip f # consume the whole formal range type
      else:
        let fStart = f # -> base type
        f = sub(f)
        linearMatch m, f, a
        skip f # lo bound
        skip f # hi bound
        expectParRi m, f, fStart
    of ArrayT:
      var a = skipModifier(arg.typ)
      matchArrayType m, f, a
    of SetT, UarrayT:
      var a = skipModifier(arg.typ)
      linearMatch m, f, a
    of CstringT:
      var a = skipModifier(arg.typ)
      if a.typeKind == NiltT:
        discard "ok"
        let fStart = f
        f = sub(f)
        expectPtrParRi m, f, fStart
      elif isStringType(a) and skipExpr(arg.n).kind == StringLit:
        m.args.addParLe HconvX, m.argInfo
        m.args.addSubtree f
        inc m.opened
        inc m.convCosts
        let fStart = f
        f = sub(f)
        expectPtrParRi m, f, fStart
      elif a.typeKind == CstringT:
        let fStart = f
        f = sub(f)
        let aStart = a
        a = sub(a)
        expectPtrParRi m, f, fStart
        expectPtrParRi m, a, aStart
      else:
        m.error InvalidMatch, f, a
    of PointerT:
      var a = skipModifier(arg.typ)
      case a.typeKind
      of NiltT:
        discard "ok"
        let fStart = f
        f = sub(f)
        expectPtrParRi m, f, fStart
      of PtrT, CstringT, RoutineTypes:
        m.args.addParLe HconvX, m.argInfo
        m.args.addSubtree f
        inc m.opened
        inc m.convCosts
        let fStart = f
        f = sub(f)
        expectPtrParRi m, f, fStart
      of PointerT:
        let fStart = f
        f = sub(f)
        let aStart = a
        a = sub(a)
        expectPtrParRi m, f, fStart
        expectPtrParRi m, a, aStart
      else:
        m.error InvalidMatch, f, a
    of PtrT, RefT:
      var a = skipModifier(arg.typ)
      let ak = a.typeKind
      if ak == NiltT:
        discard "ok"
        let fStart = f
        f = sub(f)
        skip f # base type
        expectPtrParRi m, f, fStart
      elif ak == fk:
        let fStart = f
        f = sub(f)
        inc a
        if isObjectType(f) and isObjectType(a):
          # handle inheritance
          matchObjectTypes m, f, a, fk
        else:
          linearMatch m, f, a
        expectPtrParRi m, f, fStart
      else:
        m.error InvalidMatch, f, a
    of TypedescT:
      # do not skip modifier
      var a = arg.typ
      linearMatch m, f, a, {ExactBits}
    of VarargsT:
      discard "do not even advance f here"
      if m.firstVarargPosition < 0:
        m.firstVarargPosition = m.args.len
    of UntypedT, TypedT:
      # `typed` and `untyped` simply match everything:
      let fStart = f
      f = sub(f)
      expectParRi m, f, fStart
    of VoidT:
      let fStart = f
      f = sub(f)
      expectPtrParRi m, f, fStart
      var a = arg.typ
      if not isVoidType(a):
        m.error InvalidMatch, f, a
    of TupleT:
      let fOrig = f
      let aOrig = skipModifier(arg.typ)
      var a = aOrig
      if a.typeKind != TupleT:
        m.error InvalidMatch, fOrig, aOrig
        skip f
      else:
        # skip tags:
        let fStart = f
        f = sub(f)
        a = sub(a)
        while f.hasMore:
          if a.kind == ParRi:
            # len(f) > len(a)
            m.error InvalidMatch, fOrig, aOrig
            break
          # only the type of the field is important:
          var ffld = getTupleFieldTypeSkipTypedesc(f)
          var afld = getTupleFieldTypeSkipTypedesc(a)
          linearMatch m, ffld, afld
          # skip fields:
          skip f
          skip a
        if a.hasMore:
          # len(a) > len(f)
          m.error InvalidMatch, fOrig, aOrig
        if f.kind == ParRi:
          f = fStart; skip f # normalize: end past the tree like every branch
    of RoutineTypes:
      var a = skipModifier(arg.typ)
      case a.typeKind
      of NiltT:
        if procHasPragma(f, ClosureP):
          m.args.addParLe NilX, m.argInfo
          m.args.addSubtree f
          inc m.opened
        skip f
      of RoutineTypes:
        procTypeMatch m, f, a
      else:
        if arg.n.exprKind in {OchoiceX, CchoiceX} and
            tryMatchProcChoice(m.context, arg.n, f) != SymId(0):
          # An overloaded routine was passed to a proc-typed parameter. Defer
          # the actual overload selection to `semConvArg` via an `hconv`, the
          # same mechanism enum choices use.
          m.refineArgType = true
          m.args.addParLe HconvX, m.argInfo
          m.args.addSubtree f
          inc m.opened
          skip f
        else:
          m.error InvalidMatch, f, a
    of OrT:
      # `f` is an `or`-typed parameter (e.g. `x: A | B | C`).
      #
      # Pass-through: if `arg.typ` is structurally the same OR type, accept
      # without iterating branches. This handles inner templates that
      # forward their own union-typed parameter (`template wrap*(x:
      # NimonyTagKind) = inner(x)` calling `inner(x: NimonyTagKind)`) —
      # without this, the per-branch matching would try to unify each
      # formal alternative with the WHOLE arg-OR and reject at the first
      # mismatch.
      if arg.typ.typeKind == OrT and sameTrees(f, arg.typ):
        skip f
      else:
        # Try each alternative and accept the first that matches. We can't
        # snapshot `Match` (no `=copy`), so undo-on-failure using the args
        # buffer length and the `err` flag.
        var branches = f
        branches = sub(branches) # bound the alternatives walk
        let argsSave = m.args.len
        let errSave = m.err
        let openedSave = m.opened
        var matched = false
        while branches.hasMore:
          var branch = branches
          singleArgImpl(m, branch, arg)
          if not m.err:
            matched = true
            break
          m.args.shrink argsSave
          m.err = errSave
          m.opened = openedSave
          skip branches
        if not matched:
          m.error InvalidMatch, f, arg.typ
        skip f
    of NoType, ErrT, ObjectT, EnumT, HoleyEnumT, AnumT, NiltT, AndT, NotT,
        DistinctT, StaticT, AutoT, TypekindT, OrdinalT, ConceptT, SymkindT:
      m.error UnhandledTypeBug, f, f
  else:
    m.error MismatchBug, f, arg.typ

proc isEmptyLiteral*(n: Cursor): bool =
  result = n.exprKind in {AconstrX, SetconstrX}
  if result:
    var n = n
    n = sub(n) # past the tag; bounded so the end check is exact
    skip n # type
    result = n.kind == ParRi

proc isEmptyCall*(n: Cursor): bool =
  # input needs to be semchecked, possibly in AllowEmpty context
  if n.exprKind notin CallKinds:
    return false
  var n = n
  n = sub(n) # bound the argument walk
  # overload of `@` with empty array param:
  result = n.kind == Symbol and pool.syms[n.symId] == "@.1." & SystemModuleSuffix
  inc n
  if not isEmptyLiteral(n):
    return false
  skip n
  if n.hasMore:
    return false

proc isEmptyContainer*(n: Cursor): bool =
  result = isEmptyLiteral(n) or isEmptyCall(n)

proc isEmptyOpenArrayCall*(n: Cursor): bool =
  if n.exprKind notin CallKinds:
    return false
  var n = n
  n = sub(n) # bound the argument walk
  result = n.kind == Symbol and
    # normal overload of `toOpenArray` for arrays:
    (pool.syms[n.symId] == "toOpenArray.0." & SystemModuleSuffix or
      # normal overload of `toOpenArray` for seqs:
      pool.syms[n.symId] == "toOpenArray.1." & SystemModuleSuffix)
  inc n
  if not isEmptyContainer(n):
    return false
  skip n
  if n.hasMore:
    return false

proc addEmptyRangeType(buf: var TokenBuf; c: ptr SemContext; info: PackedLineInfo) =
  buf.addParLe(RangetypeT, info)
  buf.addSubtree c.types.intType
  buf.addIntLit(0, info)
  buf.addIntLit(-1, info)
  buf.addParRi()

proc matchEmptyContainer(m: var Match; f: var Cursor; arg: CallArg) =
  # If `f` is a modifier wrapping a typevar that was already inferred
  # (e.g. `sink V` where V became `seq[Sym]` from an earlier argument),
  # substitute it here so the shape checks below see the concrete target.
  # Otherwise `@[]` against `sink V` falls through to a linearMatch of
  # `seq[Sym]` vs `auto` and fails.
  block rebind:
    var g = f
    if g.typeKind in {MutT, OutT, SinkT, LentT}:
      inc g
    if g.kind == Symbol and isTypevar(g.symId) and m.inferred.contains(g.symId):
      var inferred = m.inferred.getOrQuit(g.symId)
      matchEmptyContainer(m, inferred, arg)
      return
  # XXX handle empty containers nested inside (expr)
  if (arg.n.exprKind == AconstrX and f.typeKind == ArrayT) or
      (arg.n.exprKind == SetconstrX and f.typeKind == SetT):
    # could also handle case where `f` is a typevar
    if arg.n.exprKind == AconstrX:
      # need to match index type
      var fIndex = f
      inc fIndex # skip tag
      skip fIndex # skip element type
      let fLen = lengthOrd(m.context[], fIndex)
      if fLen.isNaN:
        # create index type to match to
        var buf = createTokenBuf(8)
        let info = arg.n.info
        addEmptyRangeType(buf, m.context, info)
        # hoist it in case it gets inferred:
        var aIndex = typeToCursor(m.context[], buf, 0)
        linearMatch(m, fIndex, aIndex)
      elif fLen != zero():
        m.error(InvalidMatch, f, arg.typ)
    inc m.inheritanceCosts
    if not m.err:
      if containsGenericParams(f): # maybe restrict to params of this routine
        # element type needs to be instantiated:
        m.refineArgType = true
      m.args.add arg.n.load # copy tag
      m.args.takeTree f
      m.args.addParRi()
  else:
    var elemType = default(Cursor)
    if (arg.n.exprKind in CallKinds and isSomeSeqType(f, elemType)):
      inc m.inheritanceCosts
      if not m.err:
        # call to `@` needs to be instantiated/template expanded,
        # also the element type needs to be instantiated if generic:
        m.refineArgType = true
        # keep the call to `@` but give the array constructor the element type:
        var call = arg.n
        m.args.takeInto call: # the call
          m.args.takeToken call # the `@` symbol
          assert call.exprKind == AconstrX
          m.args.takeInto call: # the array constructor
            # build our own array type:
            m.args.addParLe(ArrayT, call.info)
            m.args.addSubtree elemType
            addEmptyRangeType(m.args, m.context, call.info)
            m.args.addParRi()
            skip call # the original element type
    else:
      # match against `auto`, untyped/varargs should still match
      let fOrig = f
      singleArgImpl(m, f, arg)
      if not m.err:
        m.useArg arg, fOrig # since it was a match, copy it
        while m.opened > 0:
          m.args.addParRi()
          dec m.opened

proc varargsMatch(m: var Match; f: var Cursor; arg: CallArg) =
  # `(varargs T [conv])` — match each call-site arg against the element
  # type T. `sigmatchLoop` keeps `f` parked on the varargs param across
  # successive args, so this proc is reached once per varargs arg.
  # Short-circuit cases that accept the arg verbatim:
  #   * bare `(varargs)` from the `{.varargs.}` pragma (no element type;
  #     used by C importc procs and by the legacy template-`unpack` form)
  #   * `varargs[typed]`
  #   * `varargs[untyped]` — additionally `useArg` picks `arg.orig` here,
  #     preserving the raw pre-sem AST for template bodies.
  #   * the arg is already an `openArray[T]` — a previously bundled
  #     varargs call coming back through re-sem (template expansion).
  #     We deliberately leave `firstVarargPosition` unset so
  #     `compatBundleVarargsInMatch` recognises "already bundled" by the
  #     absence of a varargs span to wrap.
  # The `varargs[T, conv]` converter form is still handled by the
  # converter-retry path in `resolveOverloads` — it kicks in only when
  # the direct element match below has set `m.err`.
  var elem = f
  elem = sub(elem) # bounded: `kind` is ParRi at a bare `(varargs)`
  if elem.kind == ParRi or elem.typeKind in {UntypedT, TypedT}:
    if m.firstVarargPosition < 0:
      m.firstVarargPosition = m.args.len
    m.useArg arg, elem
  else:
    let argTyp = skipModifier(arg.typ)
    var aElem = default(Cursor)
    if isSomeOpenArrayType(argTyp, aElem):
      # `openArray[T']` (`Symbol` or `(invoke openArray T')`) — accept
      # when T' matches `elem`. Mark the slot start so
      # `compatBundleVarargsInMatch` sees the pre-bundled subtree at
      # `firstVarargPosition` and skips re-wrapping it.
      var fElem = elem
      let argsSave = m.args.len
      let errSave = m.err
      linearMatch m, fElem, aElem
      if not m.err:
        if m.firstVarargPosition < 0:
          m.firstVarargPosition = m.args.len
        m.useArg arg, argTyp
        return
      # element type didn't match — undo and fall through to per-element
      m.args.shrink argsSave
      m.err = errSave
    if m.firstVarargPosition < 0:
      m.firstVarargPosition = m.args.len
    var elemMut = elem
    singleArg(m, elemMut, arg)

proc singleArgCore(m: var Match; f: var Cursor; arg: CallArg) =
  let fOrig = f
  singleArgImpl(m, f, arg)
  if not m.err:
    m.useArg arg, fOrig # since it was a match, copy it
    while m.opened > 0:
      m.args.addParRi()
      dec m.opened

proc singleArg(m: var Match; f: var Cursor; arg: CallArg) =
  if arg.typ.typeKind == AutoT:
    if isEmptyContainer(arg.n):
      matchEmptyContainer(m, f, arg)
    elif isEmptyOpenArrayCall(arg.n):
      if isSomeOpenArrayType(f):
        # always match generated empty openarray converter call
        # argument will be instantiated after the call matches
        if not m.err:
          m.args.addSubtree arg.n
        return
      else:
        # should not happen, but still match as normal to give proper error
        singleArgCore(m, f, arg)
    else:
      singleArgCore(m, f, arg)
  elif f.typeKind == VarargsT:
    varargsMatch(m, f, arg)
  else:
    singleArgCore(m, f, arg)

proc typematch*(m: var Match; formal: Cursor; arg: Item) =
  m.argInfo = arg.n.info
  var f = formal
  singleArg m, f, CallArg(n: arg.n, typ: arg.typ)

type
  TypeRelation* = enum
    NoMatch
    IntLitMatch
    IntConvMatch
    ConvertibleMatch
    SubtypeMatch
    GenericMatch
    EqualMatch

proc usesConversion*(m: Match): bool {.inline.} =
  result = abs(m.inheritanceCosts) + m.intLitCosts + m.intConvCosts + m.convCosts > 0

proc classifyMatch*(m: Match): TypeRelation {.inline.} =
  if m.err:
    return NoMatch
  if m.convCosts != 0:
    return ConvertibleMatch
  if m.intConvCosts != 0:
    return IntConvMatch
  if m.intLitCosts != 0:
    return IntLitMatch
  if m.inheritanceCosts != 0:
    return SubtypeMatch
  if m.inferred.len != 0:
    # maybe a better way to track this
    return GenericMatch
  result = EqualMatch

proc isTypeclassConstraint*(f: TypeCursor): bool =
  var f = f
  if f.kind == Symbol:
    f = typeImpl(f.symId)
  result = f.typeKind in TypeclassKinds

proc isMatchForIs*(m: Match; formal: TypeCursor): bool =
  ## Whether `typematch(formal, arg)` should make `arg is formal` true.
  ## Unlike overload resolution, `is` requires exact types unless the
  ## formal is a typeclass / union constraint.
  if m.err:
    return false
  case classifyMatch(m)
  of NoMatch:
    return false
  of EqualMatch, GenericMatch, SubtypeMatch:
    return true
  of IntLitMatch, IntConvMatch, ConvertibleMatch:
    return isTypeclassConstraint(formal)

proc sigmatchLoop(m: var Match; f: var Cursor; args: openArray[CallArg]) =
  var i = 0
  # Trailing non-varargs params after the varargs slot, lazily computed
  # the first time the varargs branch is hit (so signatures without
  # varargs pay nothing). `< 0` means "not yet scanned".
  var trailingParams = -1
  while f.hasMore:
    m.skippedMod = NoType

    assert f.symKind == ParamY
    let param = asLocal(f)
    var ftyp = param.typ
    # This is subtle but only this order of `i >= args.len` checks
    # is correct for all cases (varargs/too few args/too many args)
    if ftyp.tagEnum != VarargsTagId:
      if i >= args.len: break
      skip f
    else:
      if trailingParams < 0:
        var scanF = f
        skip scanF
        trailingParams = 0
        while scanF.hasMore:
          inc trailingParams
          skip scanF
      if i >= args.len or (trailingParams > 0 and args.len - i <= trailingParams):
        # Done with the varargs slot: either out of args (tail varargs
        # exhausted, or non-tail varargs with all remaining args bound to
        # trailing params), or remaining args are reserved for the
        # trailing non-varargs params.
        if m.firstVarargPosition < 0:
          m.firstVarargPosition = m.args.len
        if trailingParams > 0:
          m.varargsEndPosition = m.args.len
        skip f
        if i >= args.len: break
        continue
    if args[i].n.kind == DotToken:
      # default parameter
      if param.val.kind != DotToken:
        m.args.add dotToken(param.val.info)
      else:
        # can end up here after named param ordering which doesn't check if params have default values
        # XXX error message should include param name
        m.error0 TooFewArguments
        break
    else:
      m.argInfo = args[i].n.info
      singleArg m, ftyp, args[i]
      if m.err: break
    inc m.pos
    inc i


iterator typeVars(fn: SymId): SymId {.sideEffect.} =
  let res = tryLoadSym(fn)
  assert res.status == LacksNothing
  var c = res.decl
  if isRoutine(c.symKind):
    inc c # skip routine tag
    for i in 1..3:
      skip c # name, export marker, pattern
    if c.substructureKind == TypevarsU:
      c = sub(c) # bound the typevar walk
      while c.hasMore:
        if isTypevarLike(c.symKind):
          var tv = c
          inc tv
          yield tv.symId
        skip c

proc collectDefaultValues(m: var Match; f: Cursor): seq[CallArg] =
  var f = f
  result = @[]
  while f.symKind == ParamY:
    let param = asLocal(f)
    if param.val.kind == DotToken: break
    m.insertedParam = true
    # add dot token
    result.add CallArg(n: emptyNode(m.context[]), typ: m.context.types.autoType)
    skip f

proc matchTypevars*(m: var Match; fn: FnCandidate; explicitTypeVars: Cursor) =
  m.tvars = default(HashSet[SymId])
  if fn.kind in RoutineKinds:
    var e = explicitTypeVars
    for v in typeVars(fn.sym):
      m.tvars.incl v
      if e.kind == DotToken: discard
      elif e.kind == ParRi:
        m.error0Typevar MissingExplicitGenericParameter, v
        break
      else:
        let res = tryLoadSym(v)
        assert res.status == LacksNothing
        var typevar = asTypevar(res.decl)
        if typevar.kind == StaticTypevarY:
          # explicitly given value for a value parameter, e.g. `Matrix[3, 4, int]`
          if not bindStaticTypevar(m, v, typevar.typ, e):
            m.error ConstraintMismatch, typevar.typ, e
        elif matchesConstraint(m, v, e):
          m.inferred[v] = e
        else:
          assert typevar.kind == TypevarY
          m.error ConstraintMismatch, typevar.typ, e
        skip e
    if e.kind != DotToken and e.hasMore:
      m.error0 ExtraGenericParameter
  elif explicitTypeVars.kind != DotToken:
    # aka there are explicit type vars
    if m.tvars.len == 0:
      m.error0 RoutineIsNotGeneric
      return

proc sigmatch*(m: var Match; fn: FnCandidate; args: openArray[CallArg];
               explicitTypeVars: Cursor) =
  assert fn.kind != NoSym or fn.sym == SymId(0)
  m.fn = fn
  matchTypevars m, fn, explicitTypeVars

  var f = fn.typ
  if f.typeKind in RoutineTypes:
    skipToParams f
  assert f.substructureKind == ParamsU
  let paramsStart = f
  f = sub(f)
  sigmatchLoop m, f, args

  if m.pos < args.len:
    # not all arguments where used, error:
    m.error0 TooManyArguments
  elif f.hasMore:
    # use default values for these parameters
    let moreArgs = collectDefaultValues(m, f)
    sigmatchLoop m, f, moreArgs
    if f.hasMore:
      m.error0 TooFewArguments

  if f.kind == ParRi:
    f = paramsStart; skip f
    m.returnType = f # return type follows the parameters in the token stream

proc hasUnboundTypevars*(m: Match): bool =
  ## True if `m.fn`'s generic typevars (as collected by `matchTypevars`) still
  ## lack a binding after argument matching. Cheap: just consults the
  ## `tvars`/`inferred` bookkeeping already built up, no extra lookups.
  for v in m.tvars:
    if not m.inferred.hasKey(v):
      return true
  return false

proc buildTypeArgs*(m: var Match) =
  # check all type vars have a value:
  if not m.err and m.fn.kind in RoutineKinds:
    for v in typeVars(m.fn.sym):
      let inf = m.inferred.getOrDefault(v)
      if inf == default(Cursor):
        m.error0Typevar CouldNotInferTypeVar, v
        break
      m.typeArgs.addSubtree inf

type
  DisambiguationResult* = enum
    NobodyWins,
    FirstWins,
    SecondWins

proc isTypevarFormal(f: Cursor): bool {.inline.} =
  f.kind == Symbol and isTypevar(f.symId)

proc crosswiseRelation(c: ptr SemContext; formal, otherFormal: Cursor): TypeRelation =
  ## Does `formal` accept a value typed as `otherFormal`? Used to rank the two
  ## formals' specificity. Against a *concrete* `otherFormal` we set
  ## `ignoreConstraints` so a typevar formal binds it regardless of its
  ## constraint — "concrete beats typevar" must hold even when the concrete
  ## type doesn't satisfy the constraint (e.g. `int` vs `T: StrictElem`).
  ## Against another *typevar* we keep the constraint, so constraint
  ## subsumption still decides (e.g. `OrdinalEnum ⊂ Ordinal`).
  var m = createMatch(c)
  m.ignoreConstraints = not isTypevarFormal(otherFormal)
  var f = formal
  singleArg m, f, CallArg(n: emptyNode(c[]), typ: otherFormal)
  classifyMatch(m)

proc mutualGenericMatch(a, b: Match): DisambiguationResult =
  # same goal as `checkGeneric` in old compiler: compare the two formals
  # crosswise. A concrete type is more specific than a typevar, which is
  # derivable from the signatures alone (the call argument is irrelevant here).
  result = NobodyWins
  let c = a.context
  var aParams = a.fn.typ
  var bParams = b.fn.typ
  skipToParams aParams
  assert aParams.substructureKind == ParamsU
  skipToParams bParams
  assert bParams.substructureKind == ParamsU
  aParams = sub(aParams)
  bParams = sub(bParams)
  while aParams.hasMore and bParams.hasMore:
    let aParam = takeLocal(aParams, SkipFinalParRi)
    let bParam = takeLocal(bParams, SkipFinalParRi)
    let aFormal = aParam.typ
    let bFormal = bParam.typ
    let aMatch = crosswiseRelation(c, aFormal, bFormal)
    let bMatch = crosswiseRelation(c, bFormal, aFormal)
    if aMatch == GenericMatch and bMatch == NoMatch:
      # a accepts b's formal but not vice versa: b is more specific
      if result == FirstWins: return NobodyWins
      result = SecondWins
    elif bMatch == GenericMatch and aMatch == NoMatch:
      # a is more specific
      if result == SecondWins: return NobodyWins
      result = FirstWins
    elif c != nil and VarToverloadsFeature in c.features:
      # Mirror old Nim's `sumGeneric` +1 for `tyVar`: a `var T` (or `out T`)
      # formal is more specific than a plain-`T` formal of the same base.
      # This is the whole point of the `varToverloads` gate — let two
      # routines that differ only in `var` on a parameter coexist as
      # distinct overloads, with the `var` one preferred at the call site.
      let aIsMut = aFormal.typeKind in {MutT, OutT}
      let bIsMut = bFormal.typeKind in {MutT, OutT}
      if aIsMut and not bIsMut:
        if result == SecondWins: return NobodyWins
        result = FirstWins
      elif bIsMut and not aIsMut:
        if result == FirstWins: return NobodyWins
        result = SecondWins

proc cmpMatches*(a, b: Match; preferIterators = false): DisambiguationResult =
  assert not a.err
  assert not b.err
  if a.fn.typ.typeKind == IteratorT and b.fn.typ.typeKind != IteratorT:
    if preferIterators:
      result = FirstWins
    else:
      result = SecondWins
  elif b.fn.typ.typeKind == IteratorT and a.fn.typ.typeKind != IteratorT:
    if preferIterators:
      result = SecondWins
    else:
      result = FirstWins
  elif a.convCosts < b.convCosts:
    result = FirstWins
  elif a.convCosts > b.convCosts:
    result = SecondWins
  elif a.intConvCosts < b.intConvCosts:
    result = FirstWins
  elif a.intConvCosts > b.intConvCosts:
    result = SecondWins
  elif a.intLitCosts < b.intLitCosts:
    result = FirstWins
  elif a.intLitCosts > b.intLitCosts:
    result = SecondWins
  else:
    # `inheritanceCosts` is the only ranking dimension the implicit
    # `Scope` phantom parameter contributes to, so the scope bump is
    # only consulted here (lazy, on demand).
    let aInh = a.inheritanceCosts + scopeBump(a)
    let bInh = b.inheritanceCosts + scopeBump(b)
    if aInh < bInh:
      return FirstWins
    elif aInh > bInh:
      return SecondWins
    let diff = a.inferred.len - b.inferred.len
    if diff < 0:
      result = FirstWins
    elif diff > 0:
      result = SecondWins
    else:
      if a.fn.typ.typeKind in RoutineTypes and b.fn.typ.typeKind in RoutineTypes:
        result = mutualGenericMatch(a, b)
      else:
        result = NobodyWins

type
  ParamsInfo = object
    len: int
    names: Table[StrId, int]
    isVarargs: seq[bool] # could also use a set or store the decls and check after

proc buildParamsInfo(params: Cursor): ParamsInfo =
  result = ParamsInfo(names: initTable[StrId, int](), len: 0)
  var f = params
  assert f.isParamsTag
  f = sub(f) # bound the param walk
  while f.hasMore:
    assert f.symKind == ParamY
    var param = takeLocal(f, SkipFinalParRi)
    let isVarargs = param.typ.tagEnum == VarargsTagId
    result.isVarargs.add isVarargs
    let name = getIdent(param.name)
    result.names[name] = result.len
    inc result.len

proc orderArgs*(m: var Match; paramsCursor: Cursor; args: openArray[CallArg]): seq[CallArg] =
  var params = buildParamsInfo(paramsCursor)
  var positions = newSeq[int](params.len)
  for i in 0 ..< positions.len: positions[i] = -1
  var cont: seq[bool] = @[] # could be a set but uses less memory for most common arg counts
  var inVarargs = false
  var fi = 0
  var ai = 0
  while ai < args.len:
    # original nim uses this for next positional argument regardless of named arg:
    let nextFi = fi + 1
    var n = args[ai].n
    if n.substructureKind == VvU:
      inc n
      let name = getIdent(n)
      if name in params.names:
        fi = params.names.getOrQuit(name)
        inVarargs = false
      else:
        swap m.pos, ai
        m.error0 NameNotFound
        swap m.pos, ai
        return
    elif fi >= params.len:
      swap m.pos, ai
      m.error0 TooManyArguments
      swap m.pos, ai
      return

    if inVarargs:
      if cont.len == 0:
        cont = newSeq[bool](args.len)
      assert ai != 0
      cont[ai - 1] = true
    elif positions[fi] < 0:
      positions[fi] = ai
    else:
      swap m.pos, ai
      m.error0 ParamAlreadyGiven
      swap m.pos, ai
      return

    if not params.isVarargs[fi]:
      fi = nextFi # will be checked on the next arg if it went over
    else:
      inVarargs = true
    inc ai

  result = newSeqOfCap[CallArg](args.len)
  fi = 0
  while fi < params.len:
    ai = positions[fi]
    if ai < 0:
      # does not fail early here for missing default value
      m.insertedParam = true
      result.add CallArg(n: emptyNode(m.context[]), typ: m.context.types.autoType)
    else:
      while true:
        var arg = args[ai]
        # remove name:
        if arg.n.substructureKind == VvU:
          inc arg.n
          skip arg.n
        result.add arg
        if cont.len != 0 and cont[ai]:
          inc ai
          assert ai < args.len
        else:
          break
    inc fi

proc sigmatchNamedArgs*(m: var Match; fn: FnCandidate; args: openArray[CallArg];
                        explicitTypeVars: Cursor;
                        hasNamedArgs: bool) =
  if hasNamedArgs:
    var params = fn.typ
    if params.typeKind in RoutineTypes:
      skipToParams params
    assert params.substructureKind == ParamsU
    sigmatch m, fn, orderArgs(m, params, args), explicitTypeVars
  else:
    sigmatch m, fn, args, explicitTypeVars
