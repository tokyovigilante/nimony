#
#
#           Nimony Jump Elimination Pass
#        (c) Copyright 2025 Andreas Rumpf
#
#    See the file "license.txt", included in this
#    distribution, for details about the copyright.
#

## Compiles Nimony IR to a simpler IR called [NJVL](doc/njvl.md) that does not contain jumps.

import std / [tables, hashes, sets, assertions, syncio, algorithm]
when defined(nimony):
  {.feature: "lenientnils".}
include ".." / lib / nifprelude
include ".." / lib / compat2
import ".." / nimony / [nimony_model, decls, programs, typenav, typeprops, builtintypes, xints, reporters]
import ".." / hexer / [xelim, mover, passes]
import njvl_model

#[Introducing cfvars is more complex than it looks.

Consider:

while true:
  if cond:
    if condB:
      break
    more()
  code()

# --> not only does `while` have to understand `break`, also the outer `if` is affected!

while true:
  if cond:
    if condB:
      jtrue loopGuard
    if not loopGuard:
      more()
  if not loopGuard:
    code()

It gets slightly worse:

while true:
  if cond:
    if condB:
      break
    else:
      return
    more()
  code()

# --->

while true:
  if cond:
    if condB:
      jtrue loopGuard
    else:
      jtrue loopGuard, retFlag
    if not loopGuard:
      more()
  if not loopGuard:
    code()

We solve this problem in the `trGuardedStmts` proc. The `jtrue` instruction
can now set multiple guards at once and we ensure that leaving an outer block
also always implies leaving all inner blocks! This way we can always use a single guard
condition and do not have to synthesize one with `or`!

# Example Analysis

while true:           # loopGuard
  if cond:            # ifGuard
    if condB:         # innerGuard
      break           # Sets: loopGuard=true, ifGuard=true, innerGuard=true
    more()            # This should be guarded by ifGuard (not innerGuard)
  code()              # This should be guarded by loopGuard (not ifGuard)

# Missing else exploit

If the `else` is missing or empty, it's often preferable to generate one
so that:

if cond:
  break
code()

Becomes:

if cond:
  break
else:
  code()

This is especiall important for exception handling which always has an empty `else`
branch otherwise:

f()
f2()

Is turned directly into:

f()
if failed:
  raise
else:
  f2()

This avoids most of the overhead of control flow variables by construction and keeps
our dominator trees more precise. In order to do this reliably we pass the
"current basic block" around as a parameter.]#

type
  Guard = object
    cond: SymId
    blockName: SymId # used for named `block` statements
    active: bool
    isTryGuard: bool

  ExceptionMode* = enum
    NoRaise     # proc/call cannot raise
    VoidRaise   # proc/call can raise, but is void
    TupleRaise  # proc/call can raise, and has a return value so it becomes a tuple

  BasicBlock = object
    ## Tracks the else-exploitation state within a statement list.
    ##
    ## Invariants:
    ## - `openElseBranches` counts how many `(stmts` tokens have been emitted
    ##   as else branches of ites that were left open for subsequent statements.
    ##   `closeBasicBlock` must close exactly this many before leaving the block.
    ## - `leavesWith` is set by break/return/raise to the innermost guard they
    ##   activated. It tells the parent (e.g. trIf) that the then-branch ended
    ##   with a leaving statement, enabling else exploitation for the no-else case.
    ## - `reenableOnLeave` records guards that were deactivated during else
    ##   exploitation and must be re-enabled when the basic block closes.
    ##   Each entry `(idx, cond)` is validated: the guard at `idx` must still
    ##   have `cond` as its condition (guards are a stack, so this can go stale
    ##   if guards were removed).
    openElseBranches: int
    leavesWith: int # index to the innermost guard that we activated or -1
    hasParLe: bool
    reenableOnLeave: seq[(int, SymId)]

  CurrentProc = object
    mode: ExceptionMode
    resultSym: SymId
    errorTracker: SymId # tracks error codes in try blocks (can differ from resultSym)
    guards: seq[Guard]
    tmpCounter: int
    returnType: Cursor
    tupleVars: HashSet[SymId] # variables that have been expanded to a tuple due to exception handling

  Context* = object
    typeCache: TypeCache
    counter: int
    thisModuleSuffix: string
    raisesResolved: bool  ## True when eraiser has already run; prevents double-injecting raise checks
    current: CurrentProc
    callFirstArgs: Table[SymId, TokenBuf] ## Maps local syms to the first argument of their init call (for borrow tracking)

proc addParLe*(dest: var TokenBuf; kind: NjvlKind;
               info = NoLineInfo) =
  dest.addParLe(cast[TagId](kind), info)

proc openScope(c: var Context) =
  c.typeCache.openScope()

proc closeScope(c: var Context; dest: var TokenBuf; info: NifLineInfo) =
  # insert kill instructions:
  var locals: seq[SymId] = @[]
  for s in c.typeCache.currentScopeLocals:
    locals.add s
  # sort by name: the scope table iterates in SymId-hash order, which follows
  # pool interning order and is not stable across toolchain changes — the
  # golden .nj.nif files need deterministic output (kill order is a set)
  for i in 1 ..< locals.len:
    var j = i
    while j > 0 and pool.syms[locals[j-1]] > pool.syms[locals[j]]:
      swap locals[j-1], locals[j]
      dec j
  var i = 0
  for s in locals:
    if i == 0:
      dest.addParLe("kill", info)
    dest.addSymUse s, info
    inc i
  if i > 0:
    dest.addParRi()
  c.typeCache.closeScope()

proc closeBasicBlock(c: var Context; b: var BasicBlock; dest: var TokenBuf) =
  ## Close all else branches opened by else exploitation within this block.
  ## Post-condition: openElseBranches == 0 and all borrowed guards are returned.
  while b.openElseBranches > 0:
    dest.addParRi() # close `else` branch (stmts)
    dest.addParRi() # close ite
    dec b.openElseBranches
  assert b.openElseBranches == 0
  for (idx, cond) in b.reenableOnLeave:
    if idx < c.current.guards.len and
        cond == c.current.guards[idx].cond:
      # Re-enable the guard that was deactivated during else exploitation.
      # The guard was "borrowed" by openElseBranch — we return it here.
      c.current.guards[idx].active = true

proc openElseBranch(b: var BasicBlock; dest: var TokenBuf; info: NifLineInfo) =
  ## Open an else branch on the current ite, deferring its closure to `closeBasicBlock`.
  ## This is the core of else exploitation: subsequent statements in the same block
  ## are emitted inside this else branch rather than after the ite.
  ## Pre-condition: an ite's then-branch has just been emitted (ending with jtrue).
  dest.addParLe StmtsS, info # begin of else branch
  inc b.openElseBranches

proc maybeEmitGuard(c: var Context; dest: var TokenBuf; info: NifLineInfo): (int, SymId) =
  ## If any guard is active, emit `(ite (not guard) (stmts` and temporarily
  ## disable the guard. Returns `(index, sym)` of the disabled guard, or
  ## `(-1, NoSymId)` if no guard was active.
  ##
  ## The guard is "borrowed": disabled here, re-enabled by `maybeCloseGuard`.
  ## Between emit and close, nested code runs without seeing this guard,
  ## preventing double-wrapping.
  result = (-1, NoSymId)
  for i in countdown(c.current.guards.len - 1, 0):
    let g = addr c.current.guards[i]
    if g.active:
      dest.addParLe("ite", info)
      dest.copyIntoKind NotX, info:
        dest.addSymUse g.cond, info
      result = (i, g.cond)
      g.active = false # borrow: disable until maybeCloseGuard returns it
      dest.addParLe StmtsS, info # then section
      break

proc maybeCloseGuard(c: var Context; dest: var TokenBuf; g: (int, SymId); mustCloseScope: bool) =
  ## Close the guard opened by `maybeEmitGuard`, re-enabling it.
  ## Post-condition: if a guard was emitted (g[0] >= 0), it is active again.
  let idx = g[0]
  if idx >= 0:
    if mustCloseScope:
      closeScope c, dest, NoLineInfo
    dest.addParRi() # close then section (stmts)
    dest.addDotToken() # no else section
    dest.addParRi() # close ite
    if idx < c.current.guards.len and
        g[1] == c.current.guards[idx].cond:
      # Return the borrowed guard. It may have been re-activated by raiseGuards
      # or jtrue inside the child statement — that's expected and harmless.
      c.current.guards[idx].active = true
  else:
    if mustCloseScope:
      closeScope c, dest, NoLineInfo

proc trGuardedStmts(c: var Context; b: var BasicBlock; dest: var TokenBuf; n: var Cursor; mustCloseScope: bool)
proc trExpr(c: var Context; dest: var TokenBuf; n: var Cursor)

proc declareCfVar(c: var Context; dest: var TokenBuf; s: SymId) =
  dest.addParLe("mflag", NoLineInfo)
  dest.addSymDef s, NoLineInfo
  dest.addParRi()
  let boolTyp = c.typeCache.builtins.boolType
  c.typeCache.registerLocal(s, VarY, boolTyp)

proc useErrorTracker(c: Context; dest: var TokenBuf; errorTracker: SymId; info: NifLineInfo) =
  ## Emit the correct expression to read the error code from errorTracker.
  ## In TupleRaise mode, errorTracker is a tuple and we need (tupat errorTracker +0).
  ## In VoidRaise/NoRaise mode, errorTracker is a plain ErrorCode variable.
  assert errorTracker != NoSymId
  if c.current.mode == TupleRaise:
    dest.addParLe EtupatV, info
    dest.addSymUse errorTracker, info
    dest.addIntLit 0, info
    dest.addParRi()
  else:
    dest.addSymUse errorTracker, info

proc storeToErrorTracker(c: var Context; dest: var TokenBuf; value: var Cursor; info: NifLineInfo) =
  ## Emit the correct store to set the error code in errorTracker from a source expression.
  ## In TupleRaise mode, store to (tupat errorTracker +0).
  ## In VoidRaise/NoRaise mode, store directly to errorTracker.
  assert c.current.errorTracker != NoSymId
  dest.copyIntoKind StoreV, info:
    trExpr c, dest, value
    if c.current.mode == TupleRaise:
      dest.addParLe EtupatV, info
      dest.addSymUse c.current.errorTracker, info
      dest.addIntLit 0, info
      dest.addParRi()
    else:
      dest.addSymUse c.current.errorTracker, info

proc storeConstToErrorTracker(c: Context; dest: var TokenBuf; tracker, constSym: SymId; info: NifLineInfo) =
  ## Store a constant (like Success) to errorTracker.
  assert constSym != NoSymId
  assert tracker != NoSymId
  dest.copyIntoKind StoreV, info:
    dest.addSymUse constSym, info
    if c.current.mode == TupleRaise:
      dest.addParLe EtupatV, info
      dest.addSymUse tracker, info
      dest.addIntLit 0, info
      dest.addParRi()
    else:
      dest.addSymUse tracker, info

proc declareResultVar(dest: var TokenBuf; s: SymId; info: NifLineInfo) =
  copyIntoKind dest, ResultS, info:
    dest.addSymDef s, info
    dest.addDotToken() # export marker
    dest.addDotToken() # pragmas
    dest.addSymUse pool.syms.getOrIncl(ErrorCodeName), info # type
    dest.addDotToken() # no value
  # start with: `result -> success` assignment/store instruction
  copyIntoKind dest, StoreV, info:
    dest.addSymUse pool.syms.getOrIncl(SuccessName), info
    dest.addSymUse s, info

proc trResultExpr(c: var Context; dest: var TokenBuf; n: var Cursor) =
  # we know it will be bound to `result` here:
  let info = n.info
  case c.current.mode
  of VoidRaise:
    if n.isDotToken:
      inc n
      dest.addSymUse pool.syms.getOrIncl(SuccessName), info
    else:
      trExpr c, dest, n
  of TupleRaise:
    # wrap it a tuple constructor:
    copyIntoKind dest, TupconstrX, info:
      copyIntoKind dest, TupleT, info:
        dest.addSymUse pool.syms.getOrIncl(ErrorCodeName), info
        dest.copyTree c.current.returnType
      dest.addSymUse pool.syms.getOrIncl(SuccessName), info
      trExpr c, dest, n
  of NoRaise:
    if n.isDotToken:
      inc n
    else:
      trExpr c, dest, n

proc trProcDecl(c: var Context; dest: var TokenBuf; n: var Cursor) =
  let decl = n
  var r = asRoutine(n)
  let oldProc = move c.current
  c.current = CurrentProc(tmpCounter: 1, returnType: r.retType)
  if hasPragma(r.pragmas, RaisesP):
    # Iterators don't have a `result` variable (semdecls.declareResult skips them)
    # and their bodies are not tuple-transformed by eraiser/constparams.
    # Use VoidRaise so we create a synthetic error-tracking variable instead.
    if isVoidType(r.retType) or r.kind == IteratorY:
      c.current.mode = VoidRaise
    else:
      c.current.mode = TupleRaise
  else:
    c.current.mode = NoRaise

  let retFlag = pool.syms.getOrIncl("´r.0")
  c.current.guards.add Guard(cond: retFlag, active: false)

  copyInto(dest, n):
    let isConcrete = c.typeCache.takeRoutineHeader(dest, decl, n)
    if isConcrete:
      let symId = r.name.symId
      if isLocalDecl(symId):
        c.typeCache.registerLocal(symId, r.kind, decl)
      c.typeCache.openScope()
      let info = n.info
      copyIntoKind dest, StmtsS, info:
        var b = BasicBlock(openElseBranches: 0, hasParLe: true, leavesWith: -1)
        openScope c
        # if this is a void proc that `.raises` we add a `result` variable as we actually need to return something
        if c.current.mode == VoidRaise:
          c.current.resultSym = pool.syms.getOrIncl("`result." & $c.current.tmpCounter)
          inc c.current.tmpCounter
          declareResultVar dest, c.current.resultSym, info
          # By default, errorTracker is the same as resultSym
          c.current.errorTracker = c.current.resultSym

        declareCfVar c, dest, retFlag
        trGuardedStmts c, b, dest, n, false
        closeBasicBlock c, b, dest
        closeScope c, dest, info
      c.typeCache.closeScope()
    else:
      takeTree dest, n
  c.current = ensureMove oldProc

type
  CallInfo = object
    isNoReturn: bool
    mode: ExceptionMode
    # Each entry is a small TokenBuf holding the inner location expression
    # of a `(haddr …)` argument. Recording the *full path* rather than just
    # the root symbol lets the contract analyser tell `c.field` apart from
    # other fields of `c` when checking borrow conflicts. We deliberately
    # copy the path into its own buffer instead of holding a `Cursor` into
    # `dest`: any later append to `dest` would trigger a full COW of the
    # dest buffer (potentially thousands of tokens) — the per-arg copy is
    # tiny and cheaper.
    mutates: seq[TokenBuf]
    info: NifLineInfo

proc trCall(c: var Context; dest: var TokenBuf; n: var Cursor): CallInfo =
  let info = n.info
  var fnType = skipProcTypeToParams(getType(c.typeCache, n.childCursor))
  if not isParamsTag(fnType):
    bug "njvl trCall: callee type not params at " & infoToStr(info)
  var pragmas = fnType
  skip pragmas
  let retType = pragmas
  skip pragmas
  let canRaise = hasPragma(pragmas, RaisesP)
  let isNoReturn = hasPragma(pragmas, NoreturnP)

  result = CallInfo(isNoReturn: isNoReturn, mode:
    if canRaise:
      (if isVoidType(retType): VoidRaise else: TupleRaise)
    else: NoRaise,
    info: info
  )
  dest.addParLe(n.cursorTagId, n.info)
  let callStart = n
  n = sub(n) # skip `(call)`
  trExpr c, dest, n # handle `fn`
  while n.hasMore:
    if n.exprKind == HaddrX:
      var inner = n
      inc inner # skip haddr tag
      if rootOf(inner, CanFollowCalls) != NoSymId:
        var pathBuf = createTokenBuf(4)
        pathBuf.addSubtree inner
        result.mutates.add ensureMove pathBuf
    trExpr c, dest, n
  dest.addParRi(n.endInfo)
  n = callStart; skip n

proc trExpr(c: var Context; dest: var TokenBuf; n: var Cursor) =
  case n.kind
  of Symbol:
    if c.current.tupleVars.contains(n.symId):
      let info = n.info
      copyIntoKind dest, EtupatV, info:
        dest.addSymUse n.symId, info
        dest.addIntLit 1, info
      inc n
    else:
      dest.takeTree n
  of UnknownToken, EofToken, ParLe, ParRi, ExtendedSuffix, LineInfoLit, DotToken, Ident, SymbolDef, StrLit, CharLit, IntLit, UIntLit, FloatLit:
    dest.takeTree n
  of TagLit:
    case n.exprKind
    of CallKinds:
      bug "call must have been bound to a location"
    of AndX, OrX:
      bug "and/or should have been handled by the expression elimination pass xelim.nim"
    else:
      takeInto dest, n:
        while n.hasMore:
          if n.isTagLit and n.exprKind in CallKinds:
            # A call surviving to nj in an operand position is an lvalue
            # location (xelim lifts every value-producing call), e.g. the
            # `s[i]` argument of a location-taking builtin like `wasMoved`.
            # Emit it via `trCall`, which accepts a call here, rather than the
            # plain-expression path that rejects one.
            discard trCall(c, dest, n)
          else:
            trExpr c, dest, n
  else: bug "Unmatched ParRi" # classic: a physical ParRi; nifcore: suffix kinds (never heads)

proc emitReturnGuards(c: var Context; dest: var TokenBuf; info: NifLineInfo) =
  dest.addParLe("jtrue", info)
  # we also need to break out of everything:
  for i in countdown(c.current.guards.len - 1, 0):
    let cond = c.current.guards[i].cond
    assert cond != NoSymId
    c.current.guards[i].active = true
    dest.addSymUse cond, info
  dest.addParRi()

proc callIsOver(c: var Context; dest: var TokenBuf; callInfo: CallInfo) =
  # we make `unknown` part of the `call` for now. This will be cleaned up
  # in the `versionizer` pass!
  for path in callInfo.mutates:
    dest.addParLe("unknown", callInfo.info)
    dest.add path
    dest.addParRi() # unknown
  if callInfo.isNoReturn:
    emitReturnGuards(c, dest, callInfo.info)

proc trBoundExpr(c: var Context; dest: var TokenBuf; n: var Cursor): CallInfo =
  # Indicates that the expression is about to be bound to a location.
  # Hence a `call` expression is valid here.
  if n.exprKind in CallKinds:
    result = trCall(c, dest, n)
  else:
    trExpr c, dest, n
    result = CallInfo(isNoReturn: false, mode: NoRaise, mutates: @[], info: n.endInfo)

proc raiseGuards(c: var Context; dest: var TokenBuf; info: NifLineInfo;
                 skipInnermostHandler = false) =
  let before = dest.len
  dest.addParLe("jtrue", info)
  var produced = 0
  var skippedHandler = not skipInnermostHandler
  # Break out of everything up to and INCLUDING the innermost try guard.
  # The try guard itself must be set so the except handler fires.
  # When `skipInnermostHandler` is true, we are inside a try's except handler
  # and a bare `(raise .)` (or rebuilt typed raise during reraise) needs to
  # propagate PAST that try; skip the first deactivated try guard we hit.
  for i in countdown(c.current.guards.len - 1, 0):
    let g = addr c.current.guards[i]
    if not skippedHandler and g.isTryGuard and not g.active:
      skippedHandler = true
      continue
    assert g.cond != NoSymId
    g.active = true
    dest.addSymUse g.cond, info
    inc produced
    if g.isTryGuard:
      break  # include the try guard, then stop (don't propagate further up)
  dest.addParRi()
  if produced == 0: dest.shrink before

proc trStmtCall(c: var Context; b: var BasicBlock; dest: var TokenBuf; n: var Cursor) =
  let before = dest.len
  let info = n.info
  let callInfo = trCall(c, dest, n)
  case callInfo.mode
  of NoRaise:
    discard "nothing to do"
  of VoidRaise:
    # we need to bind the call to a temporary!
    var target = createTokenBuf(10)
    target.copyTree cursorAt(dest, before)
    dest.shrink before

    let s = pool.syms.getOrIncl("`g." & $c.current.tmpCounter)
    inc c.current.tmpCounter
    copyIntoKind dest, LetS, info:
      dest.addSymDef s, info
      dest.addDotToken() # export marker
      dest.addDotToken() # pragmas
      dest.addSymUse pool.syms.getOrIncl(ErrorCodeName), info # type
      dest.add target

    # Manually emit ite to control whether we close it (for optimization)
    dest.addParLe("ite", info)
    dest.addSymUse s, info # XXX write that later as `e != Success`
    dest.copyIntoKind StmtsS, info: # then-branch
      raiseGuards(c, dest, info)
    openElseBranch b, dest, info

  of TupleRaise:
    bug "value should have been discarded"
  callIsOver(c, dest, callInfo)

proc replayLocalHeader(c: var Context; dest: var TokenBuf; n: Cursor) =
  var n = n
  takeInto dest, n:
    takeTree dest, n # name
    takeTree dest, n # export marker
    takeTree dest, n # pragmas
    dest.copyIntoKind TupleT, n.info:
      dest.addSymUse pool.syms.getOrIncl(ErrorCodeName), n.info
      takeTree dest, n # type
    takeTree dest, n # value

proc trLocal(c: var Context; b: var BasicBlock; dest: var TokenBuf; n: var Cursor) =
  let kind = n.symKind
  let beforeHead = dest.len
  dest.addParLe(n.cursorTagId, n.info)
  let localStart = n
  n = sub(n)

  let symId = n.symId
  if kind == ResultY:
    c.current.resultSym = symId
    # For TupleRaise mode, errorTracker should also point to result
    if c.current.mode == TupleRaise:
      c.current.errorTracker = symId

  takeTree dest, n # name
  takeTree dest, n # export marker
  takeTree dest, n # pragmas
  c.typeCache.registerLocal(symId, kind, n)
  takeTree dest, n # type

  # Record first argument of call inits for borrow tracking (used by trFor):
  if n.isTagLit and n.exprKind in CallKinds:
    var tmp = n
    inc tmp # skip call tag
    skip tmp # skip callee
    if tmp.hasMore: # has at least one argument
      var argBuf = createTokenBuf(8)
      argBuf.addSubtree tmp
      c.callFirstArgs[symId] = argBuf

  let info = n.info
  let callInfo = trBoundExpr(c, dest, n)
  n = localStart; skip n
  # After eraiser, every raiseable local init already has an explicit
  # `if (failed x): raise x` injected after it, so NJ must not add another check.
  let effectiveMode = if c.raisesResolved: NoRaise else: callInfo.mode
  case effectiveMode
  of NoRaise:
    dest.addParRi()
    callIsOver(c, dest, callInfo)
  of VoidRaise:
    dest.addParRi()
    callIsOver(c, dest, callInfo)
  of TupleRaise:
    # we also need to patch the type!
    dest.addParRi()
    callIsOver(c, dest, callInfo)
    var decl = createTokenBuf(10)
    decl.copyTree cursorAt(dest, beforeHead)
    dest.shrink beforeHead
    replayLocalHeader(c, dest, beginRead(decl))

    # Track guard state before emitting the error-check ite
    #let guardBefore = if c.optimizeIte: c.current.iteOpt.getLastActivated() else: InvalidGuardRef

    c.current.tupleVars.incl(symId)

    # Manually emit ite to control whether we close it (for optimization)
    dest.addParLe("ite", info)
    dest.addParLe EtupatV, info # condition
    dest.addSymUse symId, info # XXX write that later as `e != Success`
    dest.addIntLit 0, info
    dest.addParRi() # close tupat
    dest.copyIntoKind StmtsS, info:
      # then-branch
      raiseGuards(c, dest, info)

    openElseBranch b, dest, info

proc trAsgn(c: var Context; b: var BasicBlock; dest: var TokenBuf; n: var Cursor) =
  # we translate `(asgn X Y)` to `(store Y X)` as it's easier to analyze,
  # it reflects the actual evaluation order.
  let info = n.info
  dest.addParLe("store", info)
  let asgnStart = n
  n = sub(n)
  if n.isSymbol:
    let symId = n.symId
    inc n # skip `result`:
    if n.exprKind in CallKinds:
      # result = f() where xelim preserved the call without a temp:
      # Use trBoundExpr so we store the whole (ErrorCode, T) tuple into result,
      # then emit the "was successful?" branching afterwards.
      let callInfo = trBoundExpr(c, dest, n)
      dest.addSymUse symId, info
      n = asgnStart; skip n
      dest.addParRi()
      callIsOver(c, dest, callInfo)
      case callInfo.mode
      of NoRaise:
        discard
      of TupleRaise:
        # result now holds (ErrorCode, T); check the error code and propagate.
        dest.addParLe("ite", info)
        dest.addParLe EtupatV, info
        dest.addSymUse symId, info
        dest.addIntLit 0, info
        dest.addParRi()
        dest.copyIntoKind StmtsS, info:
          raiseGuards(c, dest, info)
        openElseBranch b, dest, info
      of VoidRaise:
        bug "void-raise call on right-hand side of result assignment"
      return
    elif symId == c.current.resultSym:
      trResultExpr c, dest, n
    else:
      trExpr c, dest, n
    # add `result` later due to the changed order for `(store X result)`
    dest.addSymUse symId, info
  else:
    var rhs = n
    skip rhs
    trExpr c, dest, rhs
    trExpr c, dest, n # lhs
    n = rhs
  n = asgnStart; skip n
  dest.addParRi()

proc countSons(dest: var TokenBuf; d: int): int =
  var n = cursorAt(dest, d)
  result = 0
  assert n.isTagLit
  n.into:
    while n.hasMore:
      skip n
      inc result

proc trIf(c: var Context; outerB: var BasicBlock; dest: var TokenBuf; n: var Cursor) =
  # Precondition: xelim already produced a single elif-else construct here
  let info = n.info
  dest.addParLe("ite", info)
  let ifStart = n
  n = sub(n)
  assert n.substructureKind == ElifU
  let elifStart = n
  n = sub(n)
  trExpr c, dest, n

  var b = BasicBlock(openElseBranches: 0, hasParLe: true, leavesWith: -1)
  dest.copyIntoKind StmtsS, info:
    openScope c
    trGuardedStmts c, b, dest, n, true
    closeBasicBlock c, b, dest
  n = elifStart; skip n # end of `elif`

  if n.hasMore:
    # --- Case 1: Explicit else branch ---
    assert n.substructureKind == ElseU
    let elseStart = n
    n = sub(n)
    var oldActive = false
    if b.leavesWith >= 0:
      # The then-branch ended with a leaving statement (break/return/raise)
      # that activated guard `b.leavesWith`. Disable it during else processing
      # because the else branch only executes when the then-branch was NOT taken,
      # so the jtrue didn't fire and the guard isn't set at runtime.
      assert b.leavesWith < c.current.guards.len, "leavesWith out of range"
      oldActive = c.current.guards[b.leavesWith].active
      c.current.guards[b.leavesWith].active = false

    dest.copyIntoKind StmtsS, info:
      openScope c
      var thenB = BasicBlock(openElseBranches: 0, hasParLe: true, leavesWith: -1)
      trGuardedStmts c, thenB, dest, n, true
      closeBasicBlock c, thenB, dest
    n = elseStart; skip n
    dest.addParRi(n.endInfo) # "ite"
    n = ifStart; skip n

    if b.leavesWith >= 0:
      # Restore the guard to its pre-else state.
      c.current.guards[b.leavesWith].active = oldActive

  elif b.leavesWith >= 0:
    # --- Case 2: No else branch, then-branch ended with a leaving statement ---
    # Else exploitation: the code AFTER this if becomes the else branch.
    # This is correct because if the then-branch was taken, jtrue fired and
    # subsequent guarded code is skipped. If the then-branch was NOT taken,
    # we fall through to the else which runs the subsequent code.
    n = ifStart; skip n
    assert b.leavesWith < c.current.guards.len, "leavesWith out of range"
    c.current.guards[b.leavesWith].active = false
    outerB.reenableOnLeave.add ((b.leavesWith, c.current.guards[b.leavesWith].cond))
    openElseBranch outerB, dest, info
  else:
    # --- Case 3: No else branch, normal completion ---
    dest.addDotToken() # no else section
    dest.addParRi(n.endInfo) # "ite"
    n = ifStart; skip n

proc trBreak(c: var Context; b: var BasicBlock; dest: var TokenBuf; n: var Cursor) =
  ## Emit `(jtrue guard1 guard2 ...)` and activate the guards.
  ## Sets `b.leavesWith` to the innermost guard so `trIf` knows the
  ## then-branch ended with a leaving statement.
  assert c.current.guards.len > 0, "break outside any guarded scope"

  var entries = 0 # only care about the inner most
  let breakStart = n
  n = sub(n)
  if not n.hasMore:
    entries = 1
  elif n.isDotToken:
    inc n
    inc entries
  elif n.isSymbol:
    for i in countdown(c.current.guards.len - 1, 0):
      inc entries
      if c.current.guards[i].blockName == n.symId: break
    inc n
  else:
    bug "invalid `break` structure"

  assert entries > 0, "break resolved to zero guard entries"
  b.leavesWith = c.current.guards.len-1
  dest.addParLe("jtrue", n.endInfo)
  for i in 1..entries:
    let guardIdx = c.current.guards.len - i
    assert guardIdx >= 0, "guard index underflow in break"
    let g = addr c.current.guards[guardIdx]
    dest.addSymUse g.cond, n.endInfo
    g.active = true
  dest.addParRi(n.endInfo)
  n = breakStart; skip n

type
  GuardUndoState = object
    at: int

proc addGuard(c: var Context; g: Guard): GuardUndoState =
  result = GuardUndoState(at: c.current.guards.len)
  c.current.guards.add g

proc removeGuard(c: var Context; s: GuardUndoState) =
  c.current.guards.shrink(s.at)

proc trBlock(c: var Context; outerB: BasicBlock; dest: var TokenBuf; n: var Cursor) =
  let guard = pool.syms.getOrIncl("´g." & $c.current.tmpCounter)
  inc c.current.tmpCounter

  declareCfVar c, dest, guard
  let blockStart = n # "block"
  n = sub(n)
  let blockName = if n.isSymbolDef: n.symId else: NoSymId
  inc n # name or empty
  openScope c
  let s = addGuard(c, Guard(cond: guard, active: false, blockName: blockName))

  var b = BasicBlock(openElseBranches: 0, hasParLe: outerB.hasParLe, leavesWith: -1)
  trGuardedStmts c, b, dest, n, true
  closeBasicBlock c, b, dest
  removeGuard c, s
  n = blockStart; skip n

proc containsBreak(n: Cursor): bool =
  var n = n
  linearScan n:
    if n.stmtKind == BreakS: return true
  result = false

proc findBreakSplitPoint(n: Cursor): int =
  # search for pattern `if cond: break` as all statements before that
  # can be considered to be part of the pre-condition of the loop.
  var n = n
  assert n.stmtKind == StmtsS
  n = sub(n) # stmtList; peek only, never left
  result = 0
  while n.hasMore:
    if n.stmtKind == IfS:
      var p = n
      p = sub(p)         # into the `if`
      if p.substructureKind == ElifU:
        p = sub(p)       # into the `elif`
        skip p                      # condition
        if p.stmtKind == StmtsS:
          p = sub(p)     # into the `stmts`
          if p.stmtKind == BreakS:
            skip p
            if not p.hasMore:     # the break is the sole statement
              return result

    inc result
    # skip the statement but if we find any break at this point, we don't understand the structure
    # well enough and bail out:
    if containsBreak(n): return -1
    skip n
  result = -1

proc trWhileTrue(c: var Context; dest: var TokenBuf; n: var Cursor;
                  forBorrow: TokenBuf = createTokenBuf(0)) =
  let info = n.info
  let guard = pool.syms.getOrIncl("´g." & $c.current.tmpCounter)
  inc c.current.tmpCounter
  openScope c

  dest.copyIntoKind StmtsS, info:
    declareCfVar c, dest, guard
    let s = addGuard(c, Guard(cond: guard, active: false))
    var b = BasicBlock(openElseBranches: 0, hasParLe: true, leavesWith: -1)

    var breakSplitPoint = findBreakSplitPoint(n)
    let bodyStart = n # into the loop body statement list
    n = sub(n)
    while n.hasMore and breakSplitPoint >= 1:
      trGuardedStmts c, b, dest, n, false
      dec breakSplitPoint

    closeBasicBlock c, b, dest

  dest.copyIntoKind NotX, info:
    dest.addSymUse guard, info # condition is always our artifical guard

  # post loop condition body:
  dest.copyIntoKind StmtsS, info:
    # Inject synthetic borrow local for for-loop iteration borrows:
    if forBorrow.len > 0:
      dest.add forBorrow

    var b2 = BasicBlock(openElseBranches: 0, hasParLe: true, leavesWith: -1)
    var g = (-1, NoSymId)
    while n.hasMore:
      if g[0] < 0: g = maybeEmitGuard(c, dest, n.info)
      elif g[0] < c.current.guards.len and g[1] == c.current.guards[g[0]].cond:
        c.current.guards[g[0]].active = false
      trGuardedStmts c, b2, dest, n, false
    maybeCloseGuard(c, dest, g, false)
    closeBasicBlock c, b2, dest
    closeScope c, dest, NoLineInfo
    n = bodyStart; skip n # end of body statement list

    # last statement of our loop body is the `continue`:
    dest.copyIntoKind ContinueS, info:
      dest.addDotToken() # no `join` information yet

  removeGuard c, s

proc trWhile(c: var Context; dest: var TokenBuf; n: var Cursor) =
  dest.addParLe("loop", n.info)
  let whileStart = n
  n = sub(n)

  # special case `while true` as it plays into our hands:
  if n.exprKind == TrueX:
    skip n
    trWhileTrue c, dest, n
    dest.addParRi(n.endInfo) # close "loop"
    n = whileStart; skip n
  else:
    # translate `while cond: body` to `while true: if cond: body else: break`
    # as it's too complex to handle otherwise.
    let info = n.info
    var w = createTokenBuf(10)
    w.copyIntoKind StmtsS, info:
      w.copyIntoKind IfS, info:
        w.copyIntoKind ElifU, info:
          w.takeTree n # condition
          w.takeTree n # body
          n = whileStart; skip n
        w.copyIntoKind ElseU, info:
          w.copyIntoKind StmtsS, info:
            w.addParPair BreakS, info
    var ww = beginRead(w)
    trWhileTrue c, dest, ww
    dest.addParRi() # close "loop"

proc addForBorrowDecls(dest: var TokenBuf; vars: Cursor; firstArgBuf: TokenBuf) =
  var vars = vars
  if vars.substructureKind in {UnpackflatU, UnpacktupU}:
    vars = sub(vars) # peek only, never left
    while vars.hasMore:
      addForBorrowDecls dest, vars, firstArgBuf
      skip vars
  elif isLocal(vars.symKind):
    let local = asLocal(vars)
    if local.typ.typeKind in {MutT, LentT}:
      var localDecl = vars
      dest.addParLe(if local.typ.typeKind == MutT: VarS else: LetS, vars.info)
      inc localDecl # skip original local-decl tag
      takeTree dest, localDecl # name
      takeTree dest, localDecl # export marker
      takeTree dest, localDecl # pragmas
      takeTree dest, localDecl # type
      dest.addParLe HaddrX, vars.info
      dest.add firstArgBuf
      dest.addParRi()
      dest.addParRi()

proc extractForBorrow(c: var Context; forStmt: ForStmt; info: NifLineInfo): TokenBuf =
  ## If the for-loop iterates with a borrowing iterator (yields var T or lent T),
  ## initialize the corresponding loop variables with a fake `(haddr firstArg)`.
  ## This lets contract analysis treat the real loop binders as borrowers so
  ## their borrow lifetime naturally ends with the generated `(kill ...)`.
  result = createTokenBuf(0)

  # Extract the first argument of the iterator call (the collection).
  # After xelim, the iter call may have been extracted to a temporary:
  #   let `x.N = items(s)  =>  forStmt.iter = (hderef `x.N)
  # So we trace back through hderef/temporaries to find the original call's first arg.
  var firstArgBuf = createTokenBuf(0)
  var iterCall = forStmt.iter
  if iterCall.isTagLit and iterCall.exprKind in CallKinds:
    iterCall = sub(iterCall) # peek only, never left
    skip iterCall
    if iterCall.hasMore:
      firstArgBuf = createTokenBuf(8)
      firstArgBuf.addSubtree iterCall
  elif iterCall.isTagLit and iterCall.exprKind in {HderefX, HaddrX}:
    inc iterCall
    if iterCall.isSymbol:
      let tempSym = iterCall.symId
      if tempSym in c.callFirstArgs:
        firstArgBuf = createTokenBuf(8)
        firstArgBuf.addSubtree beginRead(c.callFirstArgs.getOrQuit(tempSym))
  elif iterCall.isSymbol:
    let tempSym = iterCall.symId
    if tempSym in c.callFirstArgs:
      firstArgBuf = createTokenBuf(8)
      firstArgBuf.addSubtree beginRead(c.callFirstArgs.getOrQuit(tempSym))

  if firstArgBuf.len == 0:
    return

  addForBorrowDecls result, forStmt.vars, firstArgBuf

proc trFor(c: var Context; dest: var TokenBuf; n: var Cursor) =
  # Map `for x in i()` to `(loop ... (stmts (let x type i()) body))` so the
  # loop variable is bound from the iterator at the start of the body.
  let info = n.info
  let forStmt = asForStmt(n) # peek at structure before advancing
  dest.addParLe("loop", info)
  let forStart = n
  n = sub(n)

  let borrowBuf = extractForBorrow(c, forStmt, info)

  skip n # for loop iterator call
  skip n # for loop variables
  trWhileTrue c, dest, n, borrowBuf
  dest.addParRi(n.endInfo) # close "loop"
  n = forStart; skip n

proc buildCaseCondition(c: var Context; dest: var TokenBuf; n: var Cursor;
                        selector: SymId; selectorType: Cursor; info: NifLineInfo) =
  ## Build condition for one of-branch using OrX for multiple ranges/values
  assert n.substructureKind == RangesU
  let rangesStart = n  # into RangesU
  n = sub(n)
  # Collect all conditions
  var conditions: seq[TokenBuf] = @[]

  while n.hasMore:
    var cond = createTokenBuf(10)
    if n.substructureKind == RangeU:
      # Range: low..high => (low <= selector) and (selector <= high)
      n.into:
        cond.copyIntoKind AndX, info:
          cond.copyIntoKind LeX, info:
            cond.copyTree selectorType
            trExpr c, cond, n
            cond.addSymUse selector, info
          cond.copyIntoKind LeX, info:
            cond.copyTree selectorType
            cond.addSymUse selector, info
            trExpr c, cond, n
    else:
      # Single value: selector == value
      cond.copyIntoKind EqX, info:
        cond.copyTree selectorType
        cond.addSymUse selector, info
        trExpr c, cond, n

    conditions.add cond

  n = rangesStart; skip n  # skip closing ParRi of RangesU

  # Combine conditions with OrX
  if conditions.len == 1:
    dest.add conditions[0]
  else:
    dest.addParLe OrX, info
    for cond in conditions:
      dest.add cond
    dest.addParRi()

proc trGuardedStmtsBlock(c: var Context; dest: var TokenBuf; n: var Cursor; hasParLe = false) =
  var b = BasicBlock(openElseBranches: 0, hasParLe: hasParLe, leavesWith: -1)
  trGuardedStmts c, b, dest, n, false
  closeBasicBlock c, b, dest

const
  CaseBinarySearchThreshold = 16
    ## Lower the linear `of`-comparison chain to a balanced binary-search tree
    ## once a case has more than this many discrete searchable branches. The
    ## linear chain produces an N-deep nested `ite` which makes the recursive
    ## NJVL passes (vl.nim trIte, contracts_njvl traverseIte) and nifc genstmts
    ## overflow the call-depth limit for very large enums (e.g. Vulkan's
    ## 1145-member VkStructureType). A balanced tree nests only O(log N) deep.

type
  CaseEntry = object
    val: xint        ## ordinal value of this branch (used to sort)
    valTok: Cursor   ## cursor at the single value token inside `(ranges <val>)`
    body: Cursor     ## cursor at the branch body (the statement after `ranges`)

proc litOrdinal(val: Cursor): xint =
  ## Ordinal value of a case label. Labels are integer/char literals, or enum
  ## fields still in symbolic form (NJVL runs before enum fields are lowered to
  ## their ordinals). Anything else — a range, a conversion, a non-constant
  ## expression — yields NaN so we fall back to the linear lowering.
  case val.kind
  of CharLit: result = createXint(uint64(val.charLit))
  of IntLit: result = createXint(val.intVal)
  of UIntLit: result = createXint(val.uintVal)
  of Symbol:
    # Enum field: its declaration is `(efld … (tup <counter> <name>))`; the
    # counter is the ordinal. Mirrors expreval.nim's Symbol/EfldY handling.
    let res = tryLoadSym(val.symId)
    if res.status == LacksNothing and res.decl.symKind == EfldY:
      var fv = asLocal(res.decl).val
      if fv.kind == TagLit: # (tup <counter> <name>)
        inc fv             # -> counter
        result = litOrdinal(fv)
      else:
        result = createNaN()
    else:
      result = createNaN()
  else: result = createNaN()

proc declaresSymbol(body: Cursor): bool =
  ## True if `body` contains any symbol definition. The catch-all body is
  ## duplicated at every no-match leaf of the search tree, so it must not
  ## introduce a local (that would define the same SymId several times). When
  ## it does we fall back to the linear lowering, which emits it once.
  var n = body
  if n.kind != TagLit: return false
  var nested = 0
  while true:
    case n.kind
    of SymbolDef: return true
    of ParLe: inc nested
    of ParRi:
      dec nested
      if nested == 0: break
    else: discard
    inc n
  return false

proc emitGuardedInto(c: var Context; dest: var TokenBuf; body: Cursor; info: NifLineInfo) =
  ## Emit one branch/catch-all body into an already-open `(stmts ...)`.
  ## `body` is a fresh cursor copy, so the catch-all may be emitted at every
  ## no-match leaf without disturbing the caller's cursor.
  openScope c
  var nn = body
  trGuardedStmtsBlock c, dest, nn, true
  closeScope c, dest, info

proc emitCmp(c: var Context; dest: var TokenBuf; op: ExprKind; selector: SymId;
             selectorType, valTok: Cursor; info: NifLineInfo) =
  ## Emit `(op selectorType selector value)`, matching `buildCaseCondition`.
  dest.copyIntoKind op, info:
    dest.copyTree selectorType
    dest.addSymUse selector, info
    dest.copyTree valTok

proc emitCaseTree(c: var Context; dest: var TokenBuf; entries: seq[CaseEntry];
                  lo, hi: int; selector: SymId; selectorType, catchAll: Cursor;
                  info: NifLineInfo; isRoot: bool)

proc emitEqNode(c: var Context; dest: var TokenBuf; entries: seq[CaseEntry];
                pivot, hi: int; selector: SymId; selectorType, catchAll: Cursor;
                info: NifLineInfo; isRoot: bool) =
  ## Emit `(ite (eq sel entries[pivot]) (stmts body) (stmts <search pivot+1..hi>))`.
  ## The else searches the values above the pivot; an empty range there falls
  ## through to the catch-all.
  dest.addParLe((if isRoot: "itec" else: "ite"), info)
  emitCmp c, dest, EqX, selector, selectorType, entries[pivot].valTok, info
  dest.copyIntoKind StmtsS, info:
    emitGuardedInto c, dest, entries[pivot].body, info
  dest.copyIntoKind StmtsS, info:
    emitCaseTree c, dest, entries, pivot+1, hi, selector, selectorType, catchAll, info, false
  dest.addParRi()

proc emitCaseTree(c: var Context; dest: var TokenBuf; entries: seq[CaseEntry];
                  lo, hi: int; selector: SymId; selectorType, catchAll: Cursor;
                  info: NifLineInfo; isRoot: bool) =
  ## Emit a balanced binary-search dispatch over `entries[lo..hi]` (sorted by
  ## ordinal) into the current open `(stmts ...)`. The build recursion is
  ## O(log N) deep and the emitted `ite` nesting is O(log N) deep, so the
  ## downstream recursive passes no longer overflow.
  if lo > hi:
    # Empty interval: the selector matched no branch -> run the catch-all.
    emitGuardedInto c, dest, catchAll, info
    return
  if lo == hi:
    # Single value: (ite (eq sel v) (stmts body) (stmts catchAll))
    emitEqNode c, dest, entries, lo, hi, selector, selectorType, catchAll, info, isRoot
    return
  let mid = (lo + hi) div 2
  # (ite (lt sel pivot)
  #    (stmts <left subtree, values below pivot>)
  #    (stmts (ite (eq sel pivot) (stmts body) (stmts <right subtree>))))
  dest.addParLe((if isRoot: "itec" else: "ite"), info)
  emitCmp c, dest, LtX, selector, selectorType, entries[mid].valTok, info
  dest.copyIntoKind StmtsS, info:
    emitCaseTree c, dest, entries, lo, mid-1, selector, selectorType, catchAll, info, false
  dest.copyIntoKind StmtsS, info:
    emitEqNode c, dest, entries, mid, hi, selector, selectorType, catchAll, info, false
  dest.addParRi() # lt node

proc tryBinarySearchCase(c: var Context; dest: var TokenBuf; n: var Cursor;
                         selector: SymId; selectorType: Cursor;
                         finalBranch: Cursor; info: NifLineInfo): bool =
  ## If the case is a large ordinal dispatch over discrete single values, emit a
  ## balanced binary-search tree and advance `n` past the whole case. Returns
  ## false (without touching `dest`/`n`) for ranges, multi-value branches, too
  ## few branches, or anything else — the caller then uses the linear lowering.
  var entries: seq[CaseEntry] = @[]
  var catchAll = default(Cursor)
  var haveCatchAll = false
  var scan = n
  while scan.substructureKind == OfU:
    var br = scan
    inc br # into `of` -> at `(ranges`
    if br.substructureKind != RangesU:
      return false
    var rg = br
    inc rg # into `ranges` -> first value
    let valTok = rg
    let ordv = litOrdinal(rg)
    skip rg # past the value
    let singleVal = not rg.hasMore and not ordv.isNaN # exactly one ordinal, no range
    skip br # past `ranges` -> body
    let body = br
    if scan == finalBranch:
      catchAll = body
      haveCatchAll = true
    elif singleVal:
      entries.add CaseEntry(val: ordv, valTok: valTok, body: body)
    else:
      return false
    skip scan # past the whole `of`
  if scan.substructureKind == ElseU:
    var eb = scan
    inc eb # into `else` -> body
    catchAll = eb
    haveCatchAll = true
    skip scan # past the whole `else`
  if not haveCatchAll or entries.len <= CaseBinarySearchThreshold:
    return false
  # The catch-all is duplicated at the search tree's no-match leaves, so it must
  # not declare locals (which would define the same SymId more than once).
  if declaresSymbol(catchAll):
    return false

  sort(entries, proc (a, b: CaseEntry): int =
    if a.val < b.val: -1 elif a.val == b.val: 0 else: 1)

  emitCaseTree c, dest, entries, 0, entries.high, selector, selectorType,
               catchAll, info, isRoot = true
  n = scan
  skipParRi n # close `case`
  return true

proc trCase(c: var Context; dest: var TokenBuf; n: var Cursor) =
  let info = n.info
  let caseStart = n  # skip 'case'
  n = sub(n)

  # Evaluate selector
  let selectorType = c.typeCache.getType(n)
  let isExhaustive = isOrdinalType(selectorType, allowEnumWithHoles=true)

  var selector: SymId
  if n.isSymbol:
    selector = n.symId
    inc n
  else:
    # Complex selector - bind to temporary
    selector = pool.syms.getOrIncl("`cs." & $c.current.tmpCounter)
    inc c.current.tmpCounter
    dest.copyIntoKind LetS, info:
      dest.addSymDef selector, info
      dest.addDotToken() # export marker
      dest.addDotToken() # pragmas
      dest.copyTree selectorType
      trExpr c, dest, n

  # Find final branch if exhaustive
  var finalBranch = default(Cursor)
  if isExhaustive:
    var nn = n
    while nn.substructureKind == OfU:
      finalBranch = nn
      skip nn
    if nn.substructureKind == ElseU:
      finalBranch = default(Cursor)

  # Large ordinal cases get a balanced binary-search dispatch so the recursive
  # downstream passes don't overflow; everything else uses the linear chain.
  if tryBinarySearchCase(c, dest, n, selector, selectorType, finalBranch, info):
    return

  # Generate nested ite for each of-branch
  var iteCount = 0

  while n.substructureKind == OfU:
    if n == finalBranch:
      # Final exhaustive branch - no condition needed, just emit body
      n.into: # OfU
        skip n  # skip ranges
        openScope c
        trGuardedStmtsBlock c, dest, n
        closeScope c, dest, info
      break

    let ofStart = n  # into OfU
    n = sub(n)
    # Emit ite (or itec for first branch to mark case origin)
    if iteCount == 0:
      dest.addParLe("itec", info)
    else:
      dest.addParLe("ite", info)
    inc iteCount

    # Emit condition
    buildCaseCondition c, dest, n, selector, selectorType, info

    # Emit then-branch
    dest.addParLe StmtsS, info
    openScope c
    trGuardedStmtsBlock c, dest, n, true
    closeScope c, dest, info
    dest.addParRi()
    n = ofStart; skip n  # close OfU

    # Start else-branch (will contain next ite or final else)
    dest.addParLe StmtsS, info

  # Handle explicit else branch
  if n.substructureKind == ElseU:
    n.into:
      openScope c
      trGuardedStmtsBlock c, dest, n
      closeScope c, dest, info
  else:
    # No explicit else
    dest.addDotToken()

  n = caseStart; skip n  # close case

  # Close all the nested ite structures
  for i in 0..<iteCount:
    dest.addParRi()  # close else stmts
    dest.addParRi()  # close ite/itec

proc trTry(c: var Context; outerB: BasicBlock; dest: var TokenBuf; n: var Cursor) =
  let info = n.info


  let guard = pool.syms.getOrIncl("´g." & $c.current.tmpCounter)
  inc c.current.tmpCounter

  # Determine the error tracker variable
  # If the proc can raise, use resultSym; otherwise create a local tracker
  let oldErrorTracker = c.current.errorTracker
  var tracker: SymId
  if c.current.mode != NoRaise and c.current.resultSym != NoSymId:
    tracker = c.current.resultSym
  else:
    # Need a local variable to track errors within this try block
    tracker = pool.syms.getOrIncl("´err." & $c.current.tmpCounter)
    inc c.current.tmpCounter
    # Declare and initialize to Success
    dest.copyIntoKind VarS, info:
      dest.addSymDef tracker, info
      dest.addDotToken() # export marker
      dest.addDotToken() # pragmas
      dest.addSymUse pool.syms.getOrIncl(ErrorCodeName), info # type
      dest.addSymUse pool.syms.getOrIncl(SuccessName), info # initial value
  c.current.errorTracker = tracker

  declareCfVar c, dest, guard
  let s = addGuard(c, Guard(cond: guard, active: false, isTryGuard: true))

  # Temporarily override mode so error handling inside try block works
  let oldMode = c.current.mode
  if c.current.mode == NoRaise:
    # Make the try block think we're in a VoidRaise context
    c.current.mode = VoidRaise

  openScope c
  let tryStart = n # into the try
  n = sub(n)
  trGuardedStmtsBlock c, dest, n, outerB.hasParLe

  # Restore original mode and errorTracker
  c.current.mode = oldMode
  c.current.errorTracker = oldErrorTracker

  closeScope c, dest, info

  while n.substructureKind == ExceptU:
    let exceptStart = n # into ExceptU
    n = sub(n)
    dest.copyIntoKind IteV, info:
      dest.addSymUse guard, info
      dest.copyIntoKind StmtsS, info:
        # Handle exception type pattern (if present)
        if n.stmtKind != LetS:
          dest.takeTree n
        openScope c

        # If there's an exception variable (let e: ErrorCode), declare and initialize it
        if n.stmtKind == LetS:
          n.into:
            let excVar = n.symId
            inc n # symbol
            skip n # export marker
            skip n # pragmas
            c.typeCache.registerLocal(excVar, LetY, n)
            skip n # type
            # Initialize: e = errorTracker
            copyIntoKind dest, StoreV, info:
              useErrorTracker(c, dest, tracker, info)
              dest.addSymUse excVar, info
            assert n.isDotToken
            inc n # skip value (should be dot)

        # The except handler executes only when `guard=true` (established by the outer
        # `(ite guard ...)` we just opened). Deactivate the guard so `maybeEmitGuard`
        # does not wrap the handler body in a spurious `(ite (not guard) ...)` — that
        # inner ite would be dead code, but it buries any `jtrue` calls and prevents
        # contracts analysis from seeing that the handler is a leaving path.
        c.current.guards[s.at].active = false
        trGuardedStmtsBlock c, dest, n, true

        # Mark exception as handled by resetting error tracker to Success
        storeConstToErrorTracker(c, dest, tracker, pool.syms.getOrIncl(SuccessName), info)

        closeScope c, dest, info
        n = exceptStart; skip n
      dest.addDotToken() # no else
  if n.substructureKind == FinU:
    c.current.errorTracker = tracker
    if c.current.mode == NoRaise:
      c.current.mode = VoidRaise

    let finStart = n # into FinU
    n = sub(n)
    openScope c
    # The finally body must always execute. Deactivate the try guard so `maybeEmitGuard`
    # does not wrap the finally body in `(ite (not guard) ...)`.
    c.current.guards[s.at].active = false
    trGuardedStmtsBlock c, dest, n, true
    closeScope c, dest, info
    n = finStart; skip n

    # Re-raise any unhandled error after finally block executes
    # Check the error tracker, not the guard (guards are monotonic and can't be reset)
    dest.copyIntoKind IteV, info:
      useErrorTracker(c, dest, tracker, info)
      dest.copyIntoKind StmtsS, info:
        raiseGuards(c, dest, info)
      dest.addDotToken() # no else

    c.current.mode = oldMode
    c.current.errorTracker = oldErrorTracker
  removeGuard c, s
  n = tryStart; skip n


proc trRet(c: var Context; b: var BasicBlock; dest: var TokenBuf; n: var Cursor) =
  ## Emit the return value store and activate all return guards.
  ## Sets `b.leavesWith` to signal that this block ends with a leaving statement.
  let info = n.info
  n.into:
    if n.hasMore:
      if n.isDotToken:
        inc n
      else:
        assert c.current.resultSym != NoSymId, "could not find `result` symbol"
        if n.isSymbol and n.symId == c.current.resultSym:
          inc n
        else:
          dest.copyIntoKind StoreV, info:
            trResultExpr c, dest, n
            dest.addSymUse c.current.resultSym, info

  emitReturnGuards(c, dest, info)
  assert c.current.guards.len > 0, "return outside any guarded scope"
  b.leavesWith = c.current.guards.len-1

proc trRaise(c: var Context; b: var BasicBlock; dest: var TokenBuf; n: var Cursor) =
  ## Emit error tracker store and activate raise guards.
  ## Sets `b.leavesWith` to signal that this block ends with a leaving statement.
  let info = n.info
  var isReraise = false
  n.into:
    if n.hasMore:
      if n.isDotToken:
        # bare `(raise .)`: propagate the current `errorTracker` value past the
        # immediately-enclosing handler. Don't store anything new.
        isReraise = true
        inc n
      else:
        assert c.current.errorTracker != NoSymId, "raise outside a .raises proc or try section"
        storeToErrorTracker(c, dest, n, info)
  raiseGuards(c, dest, info, skipInnermostHandler = isReraise)
  assert c.current.guards.len > 0, "raise outside any guarded scope"
  b.leavesWith = c.current.guards.len-1

proc trCfVarDecl(c: var Context; dest: var TokenBuf; n: var Cursor) =
  # xelim can produce cfvars that we didn't generate so handle them here
  var s = NoSymId
  takeInto dest, n: # CfVarV
    s = n.symId
    dest.takeTree n # SymDef
  let boolTyp = c.typeCache.builtins.boolType
  c.typeCache.registerLocal(s, VarY, boolTyp)

proc trGuardedStmts(c: var Context; b: var BasicBlock; dest: var TokenBuf; n: var Cursor; mustCloseScope: bool) =
  ## Process one statement, wrapping it in a guard ite if a guard is active.
  ##
  ## The guard protocol:
  ## 1. `maybeEmitGuard` borrows the innermost active guard: emits `(ite (not g) (stmts`
  ##    and disables g so nested statements don't double-wrap.
  ## 2. The statement is processed (which may activate new guards via jtrue).
  ## 3. `maybeCloseGuard` returns the borrowed guard: closes `) . )` and re-enables g.
  ##
  ## For StmtsS, the g2 mechanism merges consecutive statements under the same guard:
  ## the guard is borrowed once (g2) and held open while all children are processed.
  ## This turns `(ite (not g) a .)(ite (not g) b .)` into `(ite (not g) (a b) .)`.
  let g = maybeEmitGuard(c, dest, n.info)

  var takeThisParRi = false
  case n.stmtKind
  of StmtsS, ScopeS:
    # Flatten nested stmts when the output already has a (stmts open.
    if not b.hasParLe and g[0] < 0:
      dest.addParLe(n.cursorTagId, n.info)
      b.hasParLe = true
      takeThisParRi = true
    let stmtsStart = n
    n = sub(n)
    # g2 borrows the innermost active guard for the ENTIRE statement list.
    # All children are emitted inside this single guard, achieving the merge.
    var g2 = (-1, NoSymId)
    while n.hasMore:
      if g2[0] < 0:
        g2 = maybeEmitGuard(c, dest, n.info)
      elif g2[0] < c.current.guards.len and g2[1] == c.current.guards[g2[0]].cond:
        # The guard may have been re-activated by raiseGuards inside a child
        # (e.g. a VoidRaise call). Since we're still inside the g2 scope,
        # suppress the re-activation to prevent sibling ites.
        c.current.guards[g2[0]].active = false
      trGuardedStmts(c, b, dest, n, false)
    n = stmtsStart; skip n
    maybeCloseGuard(c, dest, g2, false)

  of AsgnS:
    trAsgn c, b, dest, n
  of IfS:
    trIf c, b, dest, n
  of CaseS:
    trCase c, dest, n
  of WhileS:
    trWhile c, dest, n
  of ForS:
    trFor c, dest, n
  of LocalDecls:
    trLocal c, b, dest, n
  of ProcS, FuncS, MethodS, ConverterS, IteratorS:
    trProcDecl c, dest, n
  of RetS:
    trRet c, b, dest, n
  of BreakS:
    trBreak c, b, dest, n
  of BlockS:
    trBlock c, b, dest, n
  of MacroS, TemplateS, TypeS:
    # Macro bodies are owned by the out-of-process plugin, never lowered
    # in the user's compile output. Pass through opaquely.
    takeTree dest, n
  of RaiseS:
    trRaise c, b, dest, n
  of TryS:
    trTry c, b, dest, n
  of CallKindsS:
    trStmtCall c, b, dest, n
  else:
    if n.njvlKind in {MflagV, VflagV}:
      trCfVarDecl c, dest, n
    elif n.exprKind == PragmaxX:
      copyInto(dest, n):
        takeTree dest, n  # pragmas
        var innerB = BasicBlock(openElseBranches: 0, hasParLe: false, leavesWith: -1)
        trGuardedStmts c, innerB, dest, n, false  # body
        closeBasicBlock c, innerB, dest
        if innerB.leavesWith >= 0:
          # Propagate leaving status from the pragma body to the outer block.
          assert innerB.leavesWith < c.current.guards.len,
            "pragma body leavesWith out of range"
          b.leavesWith = innerB.leavesWith
    elif n.exprKind == ProccallX:
      trStmtCall c, b, dest, n
    else:
      trExpr c, dest, n

  maybeCloseGuard(c, dest, g, mustCloseScope)
  if takeThisParRi:
    dest.addParRi()

proc eliminateJumps*(pass: var Pass; raisesResolved = false) =
  var c = Context(counter: 0, typeCache: createTypeCache(),
                  thisModuleSuffix: pass.moduleSuffix,
                  raisesResolved: raisesResolved)
  c.openScope()
  lowerExprs(pass, TowardsNjvl)
  pass.prepareForNext("elimjumps")
  var n = pass.n
  #echo "after xelim: ", toString(n, false)
  assert n.stmtKind == StmtsS, $n.kind
  pass.dest.addParLe(n.cursorTagId, n.info)
  inc n
  # Add a top-level return guard so that noreturn calls at module level
  # have a mflag to set via jtrue, enabling the mflag-based init tracking:
  let topFlag = pool.syms.getOrIncl("´r.0." & c.thisModuleSuffix)
  c.current.guards.add Guard(cond: topFlag, active: false)
  declareCfVar c, pass.dest, topFlag
  var b = BasicBlock(openElseBranches: 0, hasParLe: true, leavesWith: -1)
  while n.hasMore:
    trGuardedStmts c, b, pass.dest, n, false
  closeScope c, pass.dest, n.endInfo
  closeBasicBlock c, b, pass.dest
  pass.dest.addParRi()
  #echo "PRODUCED: ", pass.dest.toString(false)

when isMainModule:
  from std/os import paramStr, paramCount
  import std/syncio
  import ".." / lib / symparser
  let infile = paramStr(1)
  var owningBuf = createTokenBuf(300)
  let n = setupProgram(infile, infile.changeModuleExt".njvl.nif", owningBuf)
  var initialBuf = createTokenBuf(300)
  initialBuf.addSubtree(n)
  var pass = initPass(move initialBuf, "main", "xelim_njvl", 0)
  eliminateJumps(pass)
  let output = pass.dest.toString(false)
  if paramCount() >= 2:
    # Write to specified output file
    let outfile = paramStr(2)
    var f = syncio.open(outfile, fmWrite)
    try:
      syncio.write(f, output)
    finally:
      syncio.close(f)
  else:
    # Write to stdout
    echo output
